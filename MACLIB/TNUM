         MACRO
&NAME    TNUM
         GBLA  &JOLCOM      SET BY JOLSAVE
         LCLA &LEN
&LEN     SETA  L'&SYSLIST(1)
         AIF   ('&SYSLIST(1)'(1,1) EQ '#').VAR
         AIF   (T'&SYSLIST(1) EQ 'C').CHAR
         MNOTE 4,'&SYSLIST(1) IS ALREADY NUMERIC'
&NAME    SR    R15,R15        SET RETURN CODE TO ZERO
         AIF   (T'&SYSLIST(1) EQ 'H').HALF
         AIF   (T'&SYSLIST(1) EQ 'F').FULL
         AIF   (T'&SYSLIST(1) EQ 'X').BIT
         AIF   (T'&SYSLIST(1) EQ 'R').XPLACO  EXPLICT LENGTH A
         AIF   (T'&SYSLIST(1) EQ 'G').XPLFCO   OR F CONSTANTS
         MNOTE 12,'UNKNOWN DATA TYPE FOR &SYSLIST(1)'
         AGO   .HALF
.HALF    ANOP
         LH    1,&SYSLIST(1)
         MEXIT
.FULL    ANOP
         L     1,&SYSLIST(1)  LOAD THE NUMBER IN R1
         MEXIT
.BIT     ANOP
.XPLACO  ANOP
.XPLFCO  ANOP
.* HERE WE HAVE AN EXPLICTLY DECLARED ITEM,THEREFORE IT MAY NOT
.*   BE ALIGNED. DEPENDING ON THE LENGTH,WE SHALL DO DIFFERENT
.*   THINGS
         AIF   (&LEN EQ 1).IC
         AIF   (&LEN EQ 2).ICBY2
         AIF   (&LEN EQ 3).MVC3
         AIF   (&LEN EQ 4).MVC4
         MNOTE 12,'LENGTH FOR BINARY NUMBER NOT SUPPORTED.L=&LEN'
         MEXIT
.IC      ANOP
         SR    1,1             CLEAR 1
         IC    1,&SYSLIST(1) LOAD 1 BYTE NUMBER
         MEXIT
.ICBY2   AGO   .HALF
.MVC3    ANOP
         FIX  'WE COULD USE SLR1,1 THEN ICM 1,7,&SYSLIST(1)'
         MVI   CALLAREA+8,0    ZERO FIRST BYTE
         MVC   CALLAREA+9(3),&SYSLIST(1) SHIFT
         L     1,CALLAREA+8    LOAD NUMBER
         MEXIT
.MVC4    AGO   .FULL
.CHAR    AIF   (&JOLCOM EQ 0).NOTJOL1
&NAME    L     R15,ATNUM       TEST NUMBER ROUTINE IN JOLCOM
         AGO   .REST01
.NOTJOL1 ANOP
&NAME    L     15,=V(UJTNUM)    TEST NUMBER ROUTINE ADDR
.REST01  LA    0,&LEN    LENGTH OF FIXED CHARACTER STRING
         LA    1,&SYSLIST(1)
         BALR  14,15
         MEXIT
.VAR     ANOP
         AIF   (&JOLCOM EQ 0).NOTJOL2
&NAME    L     15,ATNUMV     ADDR OF TNUM RTN IN JOLCOM
         AGO   .REST02
.NOTJOL2 ANOP
&NAME    L     15,=V(UJTNUMV) ADDR OF VARYING STRING TEST NUM RTN
.REST02  LA    1,&SYSLIST(1)
         BALR  14,15
         MEND
