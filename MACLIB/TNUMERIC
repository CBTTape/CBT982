         MACRO
&NAME    TNUMERIC &LOCN,&VALUE,&MSSG,&STMT=DDSTMT
.* THIS MACRO TESTS A LOCATION TO BE BLANK OR NUMERIC AND LESS THAN
.*   THE VALUE SPECIFIED IN VALUE.
.* IF TESTS FAIL,AN ERROR MESSAGE IS CONSTRUCTED AND OUTPUT
         LCLA  &LENMSSG
&NAME    IFNULL &LOCN,#&SYSNDX TEST NULL,BRANCH IF SO
         TNUM  &LOCN           TEST IF VARIABLE IS NUMERIC
         LTR   15,15           DID IT CONVERT ?
         BNZ   #E&SYSNDX       NO, NOT NUMERIC
         C     1,=F'&VALUE'    IS NUMERIC, TEST VALUE NOW
         BL    #&SYSNDX        OK,LESS THAN USER SPECIFIED VALUE
#E&SYSNDX CVD  R1,DBL          CONVERT
         UNPK  STRING(5),DBL   UNPACK IT
         OI    STRING+4,C'0'   FIX LAST CHARACTER
&LENMSSG SETA  K'&MSSG-2
         MVC   OP(&LENMSSG),=C&MSSG  SHIFT MESSAGE TEXT
         MVI   #OP+1,&LENMSSG  SET LEGNTH IN 'VARYING' STRING
         BAL   R10,V03INITV
         CLEAR  &LOCN
#&SYSNDX EQU *
         MEND
