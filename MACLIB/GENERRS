           MACRO
&NAME      GENERRS
           GBLC &CSECTS(100)
           GBLA &#CSECTS                       NUMBER USED TO DATE
           LCLA &THIS,&SYSNO
           LCLC &NO,&L
           ACTR 100
.* PERFORM A FEW PRELIMINARIES.
&L         SETC 'L'''
&#CSECTS   SETA &#CSECTS+1
           AIF (&#CSECTS LT 100).VT1OK
           MNOTE 16,'TOO MANY GENERRS MACROS'
           MEXIT
.VT1OK     AIF ('&NAME' NE '').VT2OK
           MNOTE 16,'NO CSECT NAME FOR GENERRS'
           MEXIT
.VT2OK     AIF (K'&NAME LT 4).VT3OK
.BADNAME   MNOTE 16,'NAME FIELD MUST 3 CHARACTERS'
           MEXIT
.VT3OK     AIF (K'&NAME NE 3).BADNAME
           AIF (N'&SYSLIST NE 0).VT4OK
           MNOTE 16,'NO ERROR MESSAGES SUPPLIED'
           MEXIT
.VT4OK     ANOP
&CSECTS(&#CSECTS) SETC '&NAME'
.* NOW SETS GET DOWN TO GENERATING SOME CONSTANTS WHICH CAN BE
.*   LOADED MULTIPICALLY FOR A BXLE LOOP TO FIND THE MESSAGE NUMBERS.
&NAME  DC  A(&NAME.MSSG)       ADDRESS OF NUMBER TABLE
       DC  A(12)               BXLE INCREMENT
* TABLE CONTAINS
* DC CL2'NUMBER'
* DC A(ADDR FIRST PART OF MESSAGE)
* DC A(ADDR SECOND PART OF MESSAGE)
&SYSNO     SETA N'&SYSLIST
&SYSNO     SETA &SYSNO*12
           DC  A(&NAME.MSSG+&SYSNO-12)         STOP ADDRESS
.* NOW START ON THE FIRST LOOP TO GENERATE THE FIRST TABLE,
.*   THEN WE'LL DO THE ACTUAL MESSAGE TEXTS.
&NAME.MSSG DS  0F
&THIS      SETA 1
.LOOP1     ANOP
&NO        SETC '&SYSLIST(&THIS,1)'(2,2)
           DC  CL4'&NO'
           DC  A(&NAME&NO.M1)
           DC  A(&NAME&NO.M3)
&THIS      SETA &THIS+1
           AIF (&THIS LE N'&SYSLIST).LOOP1
.* NOW THE MESSAGES HAVE TO BE GENERATED
&THIS      SETA 1
.LOOP2     ANOP
&NO        SETC '&SYSLIST(&THIS,1)'(2,2)
           DS 0H ALIGN ON A HALFWORD
           AIF (K'&SYSLIST(&THIS,1) EQ 4).NOMSS1
&NAME&NO.M1 DC AL2(&L&NAME&NO.M2)
&NAME&NO.M2 DC C&SYSLIST(&THIS,1)
           AGO .T2ND
.NOMSS1    ANOP
&NAME&NO.M1    DC  H'0'        ZERO LENGTH
.T2ND      ANOP
           AIF ('&SYSLIST(&THIS,2)' EQ '').NO2ND
           DS 0H ALIGN ON A HALFWORD
&NAME&NO.M3 DC AL2(&L&NAME&NO.M4)
&NAME&NO.M4 DC C&SYSLIST(&THIS,2)
           AGO .SETHIS
.NO2ND     ANOP
&NAME&NO.M3        DC H'0' ZERO LENGTH
.SETHIS    ANOP
&THIS      SETA &THIS+1
           AIF (&THIS LE N'&SYSLIST).LOOP2
           MEND
