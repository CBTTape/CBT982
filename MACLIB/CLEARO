         MACRO
&NAME    CLEARO
.* THIS MACRO WILL CLEAR (SET TO ' ' OR 0) A NUMBER OF LOCATIONS
.* IF THE AREAS BEING CLEARED ARE CHARACTER,THE MACRO WILL MOVE
.* THE FIELD 'BLANKS' TO THE AREA.
.* IF THE AREA IS A HALF WORD,FULL WORD THEN RO WILL BE SET TO
.*   0 AND STORED
.* IF A DOUBLE WORK IF FOUND,OR A BIT FIELD,THEN AN XC TO ITSELF
.*   WILL BE GENERATED.
         LCLA   &THISP,&ORIGL,&OFFSET,&L
         LCLC   &IND,&INST,&LABEL
&LABEL   SETC   '&NAME'
&THISP   SETA   1            START AT FIRST PARAMETER
&IND     SETC   ' '          SET INDICATOR TO ' '
.LUP     AIF    (&THISP LE N'&SYSLIST).CLEAR
         MEXIT
.CLEAR   ANOP
         AIF    (T'&SYSLIST(&THISP) EQ 'C').CHAR
         AIF    ('&IND' NE ' ').ST
&LABEL   SR     0,0          CLEAR 0
&IND     SETC   '1'
.ST      AIF    (T'&SYSLIST(&THISP) EQ 'H').HALF
         AIF    (T'&SYSLIST(&THISP) EQ 'F').FULL
&L       SETA   L'&SYSLIST(&THISP)
         AIF    (&L EQ 1).STC
         AIF    (&L EQ 2).HALF
         AIF    (&L EQ 3).BIT
         AIF    (&L EQ 4).FULL
         AGO    .DBL
.STC     ANOP
         STC    0,&SYSLIST(&THISP) SET VARIABLE TO 0
         AGO    .ADD1
.HALF    ANOP
         STH    0,&SYSLIST(&THISP) SET VARIABLE TO 0
         AGO    .ADD1
.FULL    ANOP
         ST     0,&SYSLIST(&THISP) SET VARIABLE TO 0
         AGO    .ADD1
.DBL     ANOP
.BIT     ANOP
&LABEL   XC     &SYSLIST(&THISP),&SYSLIST(&THISP) SET TO 0
         AGO    .ADD1
.CHAR    ANOP
         AIF    (L'&SYSLIST(&THISP) GT 80).LONG
         AIF    (L'&SYSLIST(&THISP) NE 1).MUVBLNK
&LABEL   MVI    &SYSLIST(&THISP),C' ' MOVE 1 BLANK
         AGO    .ADD1
.MUVBLNK ANOP
&LABEL   MVC    &SYSLIST(&THISP),BLANKS SHIFT BLANKS IN
         AGO    .ADD1
.LONG    ANOP
&LABEL   MVI    &SYSLIST(&THISP),C' ' SET FIRST BYTE TO ' '
.* NOW WE MUST MOVE +0 TO +1 ETC TILL ENTIRE STRING IS CLEARED
&ORIGL   SETA   L'&SYSLIST(&THISP)
&L       SETA   L'&SYSLIST(&THISP)-1
&OFFSET  SETA   1
.CLRLUP2 AIF    (&L LE 256).ONEMVC
         MVC   &SYSLIST(&THISP)+&OFFSET.(256),&SYSLIST(&THISP)-1+&OFFSE*
               T PROPOGATE
&OFFSET  SETA  &OFFSET+256
&L       SETA  &L-256
         AGO   .CLRLUP2
.ONEMVC  MVC   &SYSLIST(&THISP)+&OFFSET.(&ORIGL-&OFFSET),&SYSLIST(&THIS*
               P)-1+&OFFSET
.ADD1    ANOP
&THISP   SETA  &THISP+1
&LABEL   SETC  ' '
         AGO   .LUP
         MEND
