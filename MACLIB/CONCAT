         MACRO
&NAME    CONCAT
         LCLA  &THISP,&OFFSET,&L,&LITPOSN,&LITL
         LCLC  &LABEL
&LABEL   SETC  '&NAME'
&THISP   SETA  1
&OFFSET  SETA  0
.LUP     AIF   (&THISP LE N'&SYSLIST).GEN
         LA    10,&OFFSET.(10) ADD NUMBER OF BYTES MOVED TO BASE REG
         MEXIT
.GEN     AIF   ('&SYSLIST(&THISP)'(1,1) EQ '''').LIT
         AIF   (T'&SYSLIST(&THISP) EQ 'C').CHAR
         AIF   (T'&SYSLIST(&THISP) EQ 'H').HALF
         AIF   (T'&SYSLIST(&THISP) EQ 'F').FULL
         AIF   (T'&SYSLIST(&THISP) EQ 'X').BIT
         AIF   (T'&SYSLIST(&THISP) EQ 'R').XPLACO    EXPLICT LENGTH A
         AIF   (T'&SYSLIST(&THISP) EQ 'G').XPLFCO     OR F CONSTANTS
         MNOTE 12,'UNKNOWN DATA TYPE FOR &SYSLIST(&THISP)'
.ADD1    ANOP
&THISP   SETA  &THISP+1
&LABEL   SETC  ' '
         AGO   .LUP
.LIT     ANOP
&LITPOSN SETA  2
&L       SETA  K'&SYSLIST(&THISP)-2
         AIF   (&L EQ 1).MVI
.TQUOTE2  AIF  ('&SYSLIST(&THISP)'(&LITPOSN,2) NE '''''').NOTDBL2
.* HERE DOUBLE QUOTES HAVE HAVE BEEN FOUND
&LITPOSN SETA  &LITPOSN+1
&L       SETA  &L-1
.NOTDBL2 ANOP
&LITPOSN SETA  &LITPOSN+1
         AIF   (&LITPOSN LT K'&SYSLIST(&THISP)).TQUOTE2
&LABEL   MVC   &OFFSET.(&L,10),=C&SYSLIST(&THISP)
         AGO   .CHARADJ
.MVI     ANOP
&LABEL   MVI   &OFFSET.(10),C&SYSLIST(&THISP)
.CHARADJ ANOP
&OFFSET  SETA  &OFFSET+&L
         AGO   .ADD1
.CHAR    ANOP
&L       SETA  L'&SYSLIST(&THISP)
&LABEL   MVC   &OFFSET.(&L,10),&SYSLIST(&THISP) MOVE VARIABLE
         AGO   .CHARADJ
.HALF    ANOP
&LABEL   LH    1,&SYSLIST(&THISP)
         AGO   .NUM
.FULL    ANOP
&LABEL   L     1,&SYSLIST(&THISP)
         AGO   .NUM
.BIT     ANOP
.XPLACO  ANOP
.XPLFCO  ANOP
.* HERE WE HAVE AN EXPLICTLY DECLARED ITEM,THEREFORE IT MAY NOT
.*   BE ALIGNED. DEPENDING ON THE LENGTH,WE SHALL DO DIFFERENT
.*   THINGS
&L       SETA  L'&SYSLIST(&THISP)
         AIF   (&L EQ 1).IC
         AIF   (&L EQ 2).ICBY2
         AIF   (&L EQ 3).MVC3
         AIF   (&L EQ 4).MVC4
         MNOTE 12,'LENGTH FOR BINARY NUMBER NOT SUPPORTED.L=&L'
         AGO   .ADD1
.IC      ANOP
&LABEL   SR    1,1           CLEAR 1
         IC    1,&SYSLIST(&THISP) LOAD 1 BYTE NUMBER
         AGO   .NUM
.ICBY2   ANOP
&LABEL   MVC   CALLAREA+8(2),&SYSLIST(&THISP) SHIFT NO. SO NO 0C6
         LH    1,CALLAREA+8   LOAD NUMBER
         AGO   .NUM
.MVC3    ANOP
&LABEL   MVI   CALLAREA+8,0  ZERO FIRST BYTE
         MVC   CALLAREA+9(3),&SYSLIST(&THISP) SHIFT
         L     1,CALLAREA+8  LOAD NUMBER
         AGO   .NUM
.MVC4    ANOP
&LABEL   MVC   CALLAREA+8(4),&SYSLIST(&THISP) SHIFT
         L     1,CALLAREA+8  LOAD NUMBER
.NUM     ANOP
&LABEL   CVD   1,DBL         CONVERT TO PACKED
         MVC   &OFFSET.(8,10),=X'402020202020202020'
         ED    &OFFSET.(8,10),DBL+4
&OFFSET  SETA  &OFFSET+8
         AGO   .ADD1
         MEND
