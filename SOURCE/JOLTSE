         LCLA  &A
         LCLC  &C
JOLTSE   TITLE 'SAMPLE USER EXIT FOR JOL COMMAND PROCESSOR'
UJTSEXT1 CSECT
&A       SETA  0
         SPACE 2
* DEFINE SYMBOLIC NAMES FOR THE GENERAL PURPOSE REGISTERS
&A       SETA  0
         SPACE
.EQ010   AIF   (&A GT 15).EQ090
&C       SETC  'R'.'&A'
         AIF   (&A GT 9).EQ020
&C       SETC  'R'.'0'.'&A'
.EQ020   ANOP
&C       EQU   &A
&A       SETA  &A+1
         AGO   .EQ010
.EQ090   ANOP
         STM   R14,R12,12(R13)     SAVE REGISTERS
         XC    16(4,R13),16(R13)   INDICATE NO DATA (RC=0)
         USING UJTSEXT1,R15        DECLARE BASE REGISTER
         L     R02,0(0,R01)        LOAD  REPLY ADDRESS
         MVI   2(R02),C' '
         MVC   3(255,R02),2(R02)   SET THE AREA TO BLANKS
         L     R03,X'21C'      LOCATION PSATOLD GIVES MY TCB ADDRESS
         USING TCB,R03
         L     R04,TCBJSCB          JSCB ADDRESS
         USING IEZJSCB,R04
         L     R05,JSCBJCT         JCT: LOAD JSCBSCT FOR SCT
         ICM   R06,7,X'038'(R05)   ACT FOR THE JOB
         CLC   =H'0',X'02E'(R06)   ANY ACCOUNTING DATA?
         BE    EXT90               A... NO
         CLI   X'030'(R06),8       Q... FIRST FIELD 8 CHARS?
         BNE   EXT90               A... NO
         MVC   2(22,R02),=C'SYSACCT=1111 ROOM=2222' SET UP BASIC DATA
         MVC   10(4,R02),X'031'(R06) COPY ACCOUNTING FIELDS
         MVC   20(4,R02),X'035'(R06)
         LA    R00,22              SET LENGTH
         STH   R00,0(0,R02)
         LA    R15,4               INDICATE DATA RETURNED
         ST    R15,16(0,R13)
         MVI   19(R13),4          RC=4: PROCEED WITH CALL, SYMS PASSED
EXT90    LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 EXIT
         SPACE 2
         LTORG
         IEZJSCB
         IKJTCB
         END
