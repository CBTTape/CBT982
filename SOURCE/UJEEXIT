* THESE ARE SAMPLE USER EXITS,AND ARE ACTUALLY IN USE IN AN
*    INSTALLATION
EXIT       TITLE 'USER EXITS'
* THESE EXITS ARE GIVEN CONTROL DURING THE MAIN COMPILER PHASE OF JOL.
* THREE EXITS ARE PROVIDED CURRENTLY
*    1. WHEN DECODING THE JOB DEFINITION,AND AN UNRECOGNISABLE WORD IS
*          FOUND (UJA01JOB)
*    2. WHEN DECODING A PROGRAM DECLARATION (UJA02PGM)
*    3. WHEN DECODING A DATA SET DECLARATION (UJA03DS)
           SPACE 3
********* THESE ROUTINES ARE LOADED DYNAMICALLY ********          75128
******** THEY SHOULD (BUT NEED NOT BE) BE REENTRANT ******        75311
         SPACE 3
* FURTHER OPPORTUNITIES EXIST IN THE 'VALIDATE' OR 'VET' MODULES  75128
*   FOR YOU TO EXAMINE DSNAMES ETC TO SEE THAT THEY ARE ACCEPTABLE75128
*   TO YOUR STANDARDS                                             75128
* THESE EXITS ARE BASICALLY DESIGNED TO ALLOW YOUR INSTALLATION
*    NAMES,ETC TO BE USED EASILY,FOR INSTANCE RATHER THAN CODE
*    'DEPT=DEPARTMENT' YOU MAY SPECIFY CERTAIN CODES WHICH WILL BE
*    ACCEPTED BY YOUR EXIT AS AN ACCOUNTING CODE.
* ANOTHER USE (AS SEEN BELOW) IS WHERE A RANGE OF VOLUMES MAY BE
*    TOO LARGE TO CODE IN THE SYSGEN PROCEDURE,BUT YOUR EXIT CAN
*    DETERMINE THAT THE SYMBOL PRESENTED TO IT IS A VOLUME.
           SPACE 3
* IF YOUR EXIT CAN CORRECT THE SITUATION,RETURN A 0 IN R15,
*    OTHERWISE JOL WILL SIGNAL AN ERROR FOR YOU.
           SPACE
* IF YOU DETERMINE THAT YOU DO KNOW WHAT THE USER WANTED, THERE   75128
*    ARE TWO METHODS OF STORING THE INFORMATION
*    1.    MOVE INTO THE FIELD CALLED 'DBL' THE NAME OF THE       75311
*          MISSING KEYWORD, AND SIMPLY RETURN WITH :-
*              JOLRETN RC=0
*          JOL WILL THEN REPROCESS THE KEYWORD FOR YOU
*              SUPPOSE YOU HAVE A DSNAME IN YOUR INSTALLATION WHICH
*          DOES NOT CONTAIN A '.' (IE 1 INDEX=DSN).THE EASIEST
*          WAY TO PROCESS IT IS TO GO TO THE MAIN DSNAME HANDLER.
           SPACE 1
*          FOR EXAMPLE, THE SHELL OIL COMPANY HAVE A DATA SET
*              CALLED 'SORCMODS' IN WHICH ALL THE SOURCE MODULES
*              FOR THE INSTALLATION ARE STORED.
*          ALL THEY HAVE TO DO (AS CAN BE SEEN IN THE CODE BELOW)
*          IS:-
*              MVC DBL,=CL8'DSN'
*              JOLRETN
*
*          JOL THEN PROCESSES THE DSNAME
               SPACE
*    2.    STORE THE ITEMS ETC THAT YOU WISH,AND RETURN NORMALLY.
*          THE 'JOLSTOR' MACRO TESTS THAT THE LENGTH OF THE
*          ITEM TO BE STORED IS LESS THAN OR EQUAL TO THE LENGTH
*          OF THE SPACE ALLOCATED TO IT,AND TESTS IF THE SPACE
*          IS BLANK. ERROR MESSAGES ARE ISSUED ON YOUR BEHALF.
*          NOTE *** THIS METHOD ALSO INSISTS THAT THE'TKNCURR'
*          FIELD BE UPDATED IF YOU USE MORE THAN THE CURRENT
*          TOKEN
           SPACE 3
*          IN THE ABOVE EXAMPLE WITH THE 'SORCMODS' DATA SET,
*          YOU COULD CODE
*
*              JOLSTOR 'DSNAME',DDDSNAME,#TKN
*              JOLRETN
*
*          HOWEVER, IF YOU HAVE A VOLUME THAT YOU ARE CHECKING
*          FOR, IT IS RECOMMENDED THAT YOU USE METHOD ONE, AS
*          DONE IN JOL ITSELF; THAT IS YOU WILL HAVE TO CHECK
*          FOR A VOLUME LIST, AND FORM IT UP YOURSELF.
*
* NOTE: IF YOU USE THE EXITS FOR DETERMINING IF A VOLUME WAS CODED75311
*          WITHOUT A KEYWORD, OR IF A DSNAME WAS CODED,YOU CAN
*          ALSO SET UP A FIELD CALLED 'DDUNITYP' TO SAVE JOL
*          SEARCHING THE CATALOG TO FIND THE UNIT TYPE.
*       IF YOU SET 'DDUNIT' TO '3330' SAY, AND THE USER ALSO
*          CODES 3330, AN ERROR MESSAGE WILL BE ISSUED BECAUSE
*          THE UNIT NAME WOULD HAVE BEEN EFFECTIVELY DEFINED TWICE;-
*          BUT IF YOU SET 'DDUNITYP', THIS WILL NOT OCCUR.
* NATURALLY,YOU MUST (OR SHOULD) CODE YOUR ROUTINES FOR
*   RE-ENTRABILITY
           SPACE 3
* R2 IS ALWAYS POINTING TO JOLCOM
* R3 IS ALWAYS POINTING TO TKNX
* R4 IS ALWAYS POINTING TO THE RECIEVE AREA
*    IN EXIT UJA01JOB,R4 POINTS TO THE JOB CARD AREA
*    IN EXIT UJA02PGM,R4 POINTS TO THE EXEC CARD AREA
*    IN EXIT UJA03DS ,R4 POINTS TO THE DD CARD AREA
         SPACE 2
***********************************************************************
*
* NOTE THAT ON ENTRY, R1 ALSO POINTS TO:-
*
*        1.    JOLCOM         (ALWAYS POINTED TO BY R2 ANYWAY)
*        2.    TKNX           (ALWAYS POINTED TO BY R3 ANYWAY)
*        3.    THE EXIT AREA  (ALWAYS POINTED TO BY R4 ANYWAY)
*
* THIS IS DONE AS IT MAY BE USEFUL FOR OTHER LANGUAGES-EG PL/1
*
***********************************************************************
        SPACE 2
* OF PARTICULAR INTEREST WILL BE THE DSECTS CALLED:-
*              JOBDETS WHICH DESCRIBES THE JOB AREA
*              PGMDETS WHICH DESCRIBES THE PROGRAM AREA
*              DDDSNDET WHICH DESCRIBES THE DD CARD AREA
         SPACE 2
         COPY JOLCOM
             COPY  USERCOMS
         TITLE 'DEFINE ADDRESSES FOR DYNAMIC LOADING'
***********************************************************************
UJUEXIT  CSECT                                                    75128
         DC    A(UJA01JOB)                                        75128
         DC    A(UJA02PGM)                                        75128
         DC    A(UJA03DS)                                         75128
* DO NOT CHANGE THE ABOVE
***********************************************************************
***********************************************************************
         SPACE 2
           TITLE 'UJA01JOB:USER JOB DEFINITION EXIT'
         TITLE 'HANDLE UNRECOGNISABLE JOB TOKEN'
           JOLSAVE CSECT=UJA01JOB
           L   R4,0(R1)        LOAD PARAMETER
           USING JOLCOM,R2
           USING TKNX,R3
           USING JOBDETS,R4
         JOLRETN RC=4         ************************************75311
* NOW THIS CODING CATERS FOR DEPARTMENT NAMES (ACCOUNT CODES)
           CLC =C'FCO',TKN
           BE  A01DEPT
           CLC =C'FC0',TKN
           BE  A01DEPT
           CLC =C'FCD',TKN
           BE  A01DEPT
           CLC =C'FC04',TKN
           BE  A01DEPT
* DON'T KNOW WHAT IT IS,RETURN 4
           JOLRETN RC=4
A01DEPT  MVC   DBL,=CL8'DEPT'                                  75128
         JOLRETN RC=0                                             75128
* END JOB EXIT ROUTINE
         TITLE 'HANDLE UNRECOGNISABLE PROGRAM TOKEN'
           JOLSAVE CSECT=UJA02PGM
           L   R4,0(R1)
           USING PGMDETS,R4
* NOTE*** ANY FIDDLING WITH THE PROGRAM EXIT IS RATHER DANGEROUS
*    TO SAY THE LEAST (BECAUSE IT,UNLIKE THE JOB AND DATA SET ROUTINES
*          MUST BREAK THE INFORMATION INTO MANY ITEMS,WHICH ARE
*          STORED IN VARIOUS LOCATIONS,OR ON DISK)
* HOWEVER,SIMPLE THINGS REFERRING TO ITEMS IN 'PGMDETS' MAY BE
*    DONE
           SPACE
* I CAN'T THINK OF ANYTHING,SO RETURN
           JOLRETN RC=4
         TITLE 'HANDLE UNRECOGNISABLE DATA SET/PRINTER TOKEN'
           JOLSAVE CSECT=UJA03DS
           L   R4,0(R1)
           USING DDDSNDET,R4
* NOW THIS INSTALLATION HAS TWO TYPES OF INFORMATION WHICH ARE
*    HANDLED HERE.
*    1.    A DATA SET CALLED 'SORCMODS' IS USED
*    2.    DISK VOLUMES (WITH A FEW EXCEPTIONS) ARE
*              D001-D050,WHILE ALL TAPE VOLUMES WITH HIGH DENSITY
*              START WITH A'66',AND LOW DENSITY TAPE START
*              WITH 'CNV' OR 'VIC'
*          THIS ROUTINE HANDLES THOSE SITUATIONS.
* NOTE **********
*    IF A VOLUME IS DECIDED UPON,THE 'DDUNITYP' FIELD MUST BE
*          SET UP SO THAT THE USER DOES NOT HAVE TO SPECIFY
*          THE UNIT EITHER
*    TO MAKE THAT A LITTLE EASIER,HERE ARE SOME EQUATES
P3330      EQU B'11000010'
P2314      EQU B'11000001'
P2311      EQU B'11000011'
P2301      EQU B'11000100'
P2302      EQU B'11000101'
P23051     EQU B'11000110'
P23052     EQU B'11000111'
P33340   EQU   B'11001000'                                        75128
D3330      EQU B'10000010'
D2314      EQU B'10000001'
D2311      EQU B'10000011'
D2301      EQU B'10000100'
D2302      EQU B'10000101'
D23051     EQU B'10000110'
D23052     EQU B'10000111'
D33340   EQU   B'10001000'                                        75128
T2400      EQU B'00000001'
T24001     EQU B'00000010'
T24002     EQU B'00000011'
T24003     EQU B'00000100'
T24004     EQU B'00000101'
T34002     EQU B'00000110'
T34003     EQU B'00000111'
T34004     EQU B'00001000'
         EJECT
* FIRST OF ALL, LETS CHECK THAT THE TOKEN IS THE DATASET CALLED
*  'SORCMODS'
         SPACE 2
         JOLRETN RC=4         ************************************75311
         CLC   =C'SORCMODS ',TKN
         BNE   A03TVOL
         MVC   DBL,=CL8'DSN'                                   75128
         JOLRETN RC=0
A03TVOL    EQU  *
* TRY VOLUMES THEN.
           CLC =C'66',TKN
           BNE A03TVIC
* NOW CHECK THAT THE LENGTH OF THE VOLUME NAME IS 6 CHARACTERS
           CLC #TKN,=H'6'
           BNE A03BADRT        DON'T KNOW WHAT IT IS,LET THE
*                              MAIN JOL PROCESSOR GIVE AN ERROR
*                              MESSAGE
           MVI DDUNITYP,T24003 SET UNIT TYPE UP TO 2400-3
A03ISVOL MVC   DBL,=CL8'VOL'                                   75128
A03RET0  JOLRETN RC=0                                             75128
A03TVIC    CLC =C'VIC',TKN
           BNE A03TCNV
A03TLENV   CLC #TKN,=H'6'
           BNE A03BADRT
           MVI DDUNITYP,T24004
           B   A03ISVOL
A03TCNV    CLC =C'CNV',TKN
           BE  A03TLENV        TEST LENGTH OF VOLUME
* NOW CHECK IF  D001-D050
           CLC #TKN,=H'4'      LENGTH OF STRING = 4
           BNE A03BADRT        NO,SO IGNORE
           CLI TKN,C'D'        START WITH A 'D' ?
           BNE A03BADRT        OUT IT GOES
           MVC TKN(3),TKN+1    DROP THE 'D'
           LH R1,#TKN
           BCTR R1,0           REDUCE LENGTH BY ONE
           STH R1,#TKN
           TNUM #TKN           CONVERT TO NUMERIC
* NOW RESET THE STRING BACK TO 'DNNN', THEN *** CHECK IF THE
*  TNUM ROUTINE DID CONVERT THE ITEMS AFTER THE 'D'
           MVC TKN+1(1),TKN    LAST THREE BYTES OF THE VOLUME,1 BY 1
         MVC   TKN+1(1),TKN
           MVI TKN,C'D'        SO WE CAN RE-INSERT THE LONELY D
           LH  R1,#TKN         O.K.,LETS GET THE LENGTH OF TKN
           LA  R1,1(R1)        AND RE-ADJUST IT SO IT IS CORRECT
           STH R1,#TKN         THERE---DONE
           LTR R15,R15         WERE THE LAST THREE CHARACTERS NUMERIC
*                              FROM THE TEST ABOVE ?
         BNZ   A03BADRT       NO,IGNORE THIS TOKEN THEN
           MVI DDUNITYP,D2314
           B  A03ISVOL
A03BADRT   JOLRETN RC=4
           LTORG
           END
