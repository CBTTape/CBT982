USERVAL  TITLE 'SAMPLE VALIDATION ROUTINES'
* THESE ARE SAMPLE VALIDATION EXITS
         SPACE 3
* THESE VALIDATION ROUTINES ARE GIVEN CONTROL AT VARIOUS STAGES
*  OF THE JOL COMPILER.
         SPACE
* THE FOLLOWING EXITS ARE AVAILABLE
         SPACE
* 1.     UJU01JOB             VALIDATE THE JOB DETAILS
* 2.     UJU02PGM             VALIDATE THE PROGRAM DETAILS
* 3.     UJU03DS              VALIDATE THE DATA-SET DETAILS
* 4.     UJU05CAT             VALIDATE THE CATALOG STMT
* 5.     UJU06UNC             VALIDATE THE UNCATALOG STMT
* 6.     UJU07KEE             VALIDATE THE KEEP STMT
* 7.     UJU08SCR             VALIDATE THE SCRATCH STMT
* 8.     UJU09DEL             VALIDATE THE DELETE STMT
         SPACE 3
*** NOTE ***
* THESE ROUTINES GET CONTROL BEFORE, REPEAT BEFORE, JOL'S OWN
*  DEFAULT AND VALIDATION MODULES
***
         SPACE 1
         SPACE 3
***********************************************************************
*
* GENERAL NOTES:-
*
*        1. IN THE VALIDATION EXITS, YOU RECEIVE CONTROL BEFORE
*              JOLS OWN VALIDATE AND DEFAULT ROUTINES.
*
*        2. JOL WILL VALIDATE THE ITEMS AS MUCH AS POSSIBLE, BUT
*              THESE EXITS ARE DESIGNED SO THAT YOU MAY CHECK
*              ITEMS THAT JOL CANNOT, THAT IS FOR INSTALLATION
*              DEFINED STANDARDS AND SO FORTH.
*
*        3. THE CODE WRITTEN HERE-IN IS A SAMPLE ONLY, BUT IT
*              MAY BE OF USE WITH ONLY MINOR MODIFICATIONS.
*
*        4. ALL ROUTINES HAVE ACCESS TO THE 'JOBDETS' DETAILS AT ALL
*              TIMES.
***********************************************************************
         EJECT
***********************************************************************
*
* NOTE : CCS AND ASSOCIATED COMPANIES ARE ABSOLVED TOTALLY FROM
*        ANY ACTIONS ETC THAT MAY RESULT FROM THE FAILURE OF
*        USER WRITTEN ROUTINES
*       FURTHERMORE, SHOULD A FAILURE OCCUR, IT IS NECESSARY TO
*        TO ENSURE THAT USER WRITTEN ROUTINES DO NOT RECIEVE CONTROL
*        BEFORE ANY ATTEMPT IS MADE TO DETECT ERRORS. SHOULD AN
*        ERROR BE FOUND TO BE CAUSED BY AN INSTALLATION ROUTINE,
*        ALL REASONABLE COSTS MUST BE PAID BY THE COMPANY.
*
***********************************************************************
         EJECT
         COPY JOLCOM          COPY JOL COMMUNICATION REGION.
         COPY USERCOMS
         SPACE 3
UJUVALID CSECT
         DC    V(UJU01JOB)
         DC    V(UJU02PGM)
         DC    V(UJU03DS)
         DC    V(UJU05CAT)
         DC    V(UJU06UNC)
         DC    V(UJU07KEE)
         DC    V(UJU08SCR)
         DC    V(UJU09DEL)
         SPACE 3
*** THE ABOVE MUST NOT BE CHANGED
         SPACE
         PRINT DATA
         TITLE 'VALIDATE JOB DETAILS'
         JOLSAVE CSECT=UJU01JOB
* THIS ROUTINE VALIDATES THE JOB DETAILS - SAMPLE ONLY
       JOLRETN RC=0 *******************************************
         LM  R2,R3,0(R1)       GET ADDRESS OF JOBDETS AND JOLCOM
         USING JOBDETS,R3     USE IT.
* CHECK IF JOBNAME COMMENCES WITH 'T' | 'P'
         CLI   JOBNAME,C'T'   'T' ?
         BE    U01OK1         OK
         CLI   JOBNAME,C'P'   'P'
         BE    U01OK1         YES->
         JOLERR 301,'JOBNAME ''',JOBNAME,''' DOES NOT COMMENCE WITH ''T*
               '' OR ''P'''
U01OK1   EQU   *
* NOW CHECK IF JOBREGION IS GREATER THAN 120K, AND IF SO, CHANGE
*  THE CLASS TO 'B', UNLESS IT ALREADY IS
         IFNULL JOBREGN,U01REGOK  NOTHING CODED, IGNORE IT
         TNUM  JOBREGN        CONVERT TO BINARY
         LTR   R15,R15        DID IT CONVERT ?
         BNZ   U01REGOK       NO,DON'T GIVE ERROR, JOL WILL LATER
*** ALTERNATIVELY, CHANGE IT TO ANOTHER VALUE **
         CH    R1,=H'120'     > 120K ?
         BNH   U01REGOK       NO
         CLI   JOBCLASS,C'B'  CLASS ALREADY 'B'?
         BE    U01REGOK       YES.
         JOLERR 102,'JOBCLASS CHANGED TO ''B'' FROM ''',JOBCLASS,''''
         MVI   JOBCLASS,C'B'
U01REGOK EQU   *
* ONE COULD, OF COURSE, CHECK THAT THE SIZE CODED WASN'T SO BIG THAT
*  THE JOB WILL NOT FIT IN THE MACHINE AS WELL.
         SPACE 3
* NOW CHECK THAT THE ACCOUNT CODE IS A VALID ONE.
* ONE COULD DO RANGE CHECKS, OR SOME SUCH SIMILIAR THING, BUT
*  WE SHALL SEARCH THE TABLE.
         LM    R7,R9,U01ACCTC
U01CHKA  CLC   JOBACCT(L'ACTLEN),0(R7)
         BE    U01ACTOK
         BXLE  R7,R8,U01CHKA
         JOLERR 403,'ACCOUNT CODE ''',JOBACCT,''' IS INVALID'
U01ACTOK EQU   *
         SPACE 3
** THERE ARE OBVIOUSLY OTHER CHECKS THAT CAN BE MADE, BUT THE
*  ABOVE SHOULD BE SUFFICENT TO ACT AS A GUIDE.
         JOLRETN
U01ACCTC DC    A(*+12,L'ACTLEN,U01ACCTE-L'ACTLEN)
ACTLEN   DC    CL3'FCO'
         DC    CL3'FCO'
         DC    CL3'FCD'
         DC    CL3'POG'
U01ACCTE EQU   *
         LTORG
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*)
         TITLE 'CHECK PROGRAM USAGE'
         JOLSAVE CSECT=UJU02PGM
* AT THIS STAGE, THE DETAILS FROM '#PGMDETS' TO 'PGMNODDS' IS
*  AVAILABLE.
* DD CARDS MAY BE VALIDATED AT EXIT UJU03DS
       JOLRETN RC=0 *******************************************
         LM    R2,R4,0(R1)
         USING JOBDETS,R3
         USING PGMDETS,R4
* CHECK IF PROGRAM 'IMASPZAP' IS BEING USED BY ACCOUNT CODE
*  'FC01', AND IF NOT CANCEL THE JOB.
         CLC   =C'IMASPZAP',PGM    IMASPZAP ?
         BNE   U02RETN
         CLC   =C'FC01',JOBACCT    IS USER ACCOUNT 'FC01'
         BNE   U02ZAPER            NO, CANCEL WITH ERROR MESSAGE.
         CLC   =C'G.CORDING',JOBPRGMR  NAME CORDING ?
         BE    U02ZAPOK            YES, LET HIM GO.
         CLC   =C'A.J.CAMPBELL',JOBPRGMR
         BE    U02ZAPOK
         CLC   =C'M.STRAHAN',JOBPRGMR
         BE    U02ZAPOK
U02ZAPER JOLERR 401,'IMASPZAP EXECUTED BY INVALID PERSONELL'
* NOW SEND A MESSAGE TO THE OPERATOR SO THAT HE OR SHE CAN NOTIFY
*  MANAGEMENT
           MVC   #WORK(LU02WTOR),U02WTOR
         MVC   #WORK+47(35),JOBACCT     ACCOUNT CODE
         MVC   #WORK+83(16),JOBPRGMR    PROGRAMMER NAME
         WTOR  ,DBL,8,CALLAREA,MF=(E,#WORK)                        SOCO
         WAIT  ECB=CALLAREA                                        SOCO
         CLC   =C'CANCEL',DBL                                      SOCO
         BNE   U02RETN                                             SOCO
         JOLRETN RC=16                                             SOCO
U02ZAPOK EQU  *
U02RETN  EQU *
         JOLRETN
         FIX 'NOT RE-ENTRANT'
U02WTOR  WTOR  'SYSTEM SECURITY VIOLATION:IMASPZAP:                    *
                              :                , REPLY GO OR CANCEL',  *
               MF=L                                                SOCO
LU02WTOR EQU   *-U02WTOR
         LTORG
         TITLE 'VALIDATE DATA SET USAGE'
         JOLSAVE  CSECT=UJU03DS
*      JOLRETN RC=0 *******************************************
         LM    R2,R5,0(R1)
         L     R3,AJOBDETS ERROR HERE,R3 NOT SET CORRECTLY (VALIDATE)
         USING JOBDETS,R3
         USING PGMDETS,R4
         USING DDDSNDET,R5
* THIS CSECT ALLOWS YOU TO EXAMINE A 'DD-CARD',OR AT LEAST THE
*  INTERNAL FORMAT OF ONE
* YOU MAY EXAMINE THE JOB DETAILS AND THE PROGRAM DETAILS AS WELL.
*  IN CASE YOU WONDER WHY THERE ARE 3 EXITS WHEN PERHAPS ONE WOULD
*    DO, IT IS SO THAT YOU DO NOT HAVE TO REMEMBER IF YOU HAVE
*    ALREADY VALIDATED THE PROGRAM OR JOB DETAILS THIS TIME.
         SPACE
* WITH THIS EXIT, YOU CAN, FOR EXAMPLE, SEE IF A PARTICULAR
*  DATA SET IS BEING ACCESSED BY UNAUTHORISED PERSONEL, OR DEPARTMENTS,
*  OR PARTICULAR PERSONS.
         SPACE 3
* FIRST OF ALL, CHECK THE TYPE OF FILE WE HAVE HERE
         CLI   DDTYPE,DDSYSIN SYSIN ?
         BNE   U03TRY2        NO
U03RETN  JOLRETN RC=0         YES, IGNORE IT TOTALLY
U03TRY2  CLI   DDTYPE,DDSYSOUT SYSOUT ?
         BE    U03SYSO        YES, GO TO SYSOUT PROCESSING
         CLI   DDTYPE,DDWORKDS IS THIS A REAL DATA SET
         BL    U03RETN        PUNCH OR OTHER PECULIAR DATA SET
* HERE WE HAVE A DATA SET. WE CAN CHECK IT FURTHER TO DETERMINE
*  IF IT IS A GDG, WORKFILE OR OTHER BY EXAMINING THE 'DDTYPE'FIELD
*  FURTHER.
* ONE CAN CHECK IF ACCT CODE XYZ IS READING A SPECIFIC DATA SET,
*  AND IF ANYONE ELSE IS ATTEMPTING TO READ IT, GIVE AN ERROR
*  MESSAGE, OR CANCEL THE JOB.
         SPACE
* BUT WE SHALL DEMONSTRATE ANOTHER USE HERE (CCS CANNOT KNOW YOUR
*        REQUIREMENTS...).
* THIS NEXT CODE EXAMINES OUTPUT DATA SETS AND ISSUES A WARNING
*  IF THE BLOCKSIZE CODED BY SOMEONE IS TOO SMALL.
*
* THIS CHECK WILL BE MOVED TO 'VALIDATE' SOON.
    FIX 'CHECK THE LENGTH BEFORE CLEARING . CAUSED MANY PROBLEMS'
    FIX 'WITH QUOTED DSNAMES'
         CLI   DDTYPE,DDMEMSPC IS THERE A MEMBER SPECIFIED?
         BNE   U03NOMEM
         IFNULL DDDSORG,U03NOMEM ANY DSORG? NO,EVERYTHING IS OK
         JOLERR 202,'DSID ',DDDSID,                                    *
               ' HAD A MEMBER SPECIFIED AND DSORG ',                   *
               DDDSORG,'. DSORG OPTION CANCELLED'
         CLEAR DDDSORG
U03NOMEM TM    DDUSE,B'11000000'
         BNZ   U03OP
* HERE IS IS AN INPUT DATA SET, AND ONE COULD NOT CHECK A SPECIFIC
*  DATA SET WITH AN ACCOUNT CODE.
* HOWEVER, WE SHALL RETURN
         JOLRETN RC=0
         SPACE 3
U03OP    EQU   *
* HERE WE HAVE AN OUTPUT DATA SET.
         IFNULL DDBLKSZE,U03NBLK  ANY BLOCKSIZE ?
         LH    R1,DDBLKSZE                                       80290
         LTR   R15,R1                                            80290
         BNZ   U03IGNRB
         CH    R1,=H'400'
         BH    U03BLKOK
         JOLERR 103,'BLOCKSIZE FOR DSID ''',DDDSID,''' DSNAME ''',     *
               DDDSNAME,''' DECLARED AT ',DDSTMT,' IS TOO SMALL FOR EFF*
               ICENT USE - PLEASE SPECIFY A LARGER BLOCKSIZE'
U03IGNRB EQU   *
U03BLKOK EQU   *
U03NBLK  EQU   *
         SPACE 3
* OBVIOUSLY, THERE ARE MANY SUCH VALIDATIONS THAT CAN BE PERFORMED
U03RETN0 JOLRETN  RC=0
U03SYSO  EQU  *
         CLI   DDSYSCLS,C'*'   SYSOUT * CODED?                   CCS
         BE    U03ASTER        YES, TEST SYSTEM TYPE             CCS
         CLI   DDSYSCLS,C' '   SYSOUT CLASS NOT CODED ?          CCS
         BNE   U03RETN0        SYSOUT CLASS CODED, LEAVE IT      CCS
         MVI   DDSYSCLS,C'*'   MAKE SYSOUT=*                     CCS
U03ASTER L     R15,AJOLGEN     ADDRESS JOLGEN FOR DEFAULTS       CCS
         USING GENDETS,R15                                       CCS
         CLI   JOBMSGCL,C' '   ANY CODED ON JOB STATEMENT?       CCS
         BE    U03CHKS         NOPE, CHECK SYSTEM TYPE           CCS
         MVC   DDSYSCLS,JOBMSGCL SHIFT IN JOB ONE                CCS
         B     U03RETN0                                          CCS
U03CHKS  EQU   *               CHECK SYSTEM TYPE HERE.           CCS
         L     R1,16           GET CVT ADDRESS                   CCS
         TM    116(R1),X'11'   IS IT MVS?                        CCS
         BO    U03RETN0        YES, LET IT GO THROUGH THEN       CCS
         SPACE 2                                                 CCS
* HERE IT IS NOT MVS, AND WE HAVE AN ASTERISK IN THE SYSOUT      CCS
         CLI   IFTSO,C'T'     ARE WE RUNNING UNDER TSO?
         BE    U03TSO
         MVC   DDSYSCLS,DEFSCLAS MAKE IT DEFAULT SYSOUT THEN     CCS
         B     U03RETN0        RETURN                            CCS
U03TSO   MVC   DDSYSCLS,TSOCLASS                                 CCS
         B     U03RETN0                                          CCS
         DROP  R15
         LTORG
         TITLE 'VALIDATE CATALOG STMT'
         JOLSAVE CSECT=UJU05CAT
         JOLRETN RC=0   ************************************       SOCO
         LM    R2,R5,0(R1)
* CATALOG STMT
         $CALL UJU07KEE       CALL KEEP TO VALIDATE IT
         JOLRETN
         TITLE 'VALIDATE UNCATALOG STMT'
         JOLSAVE CSECT=UJU06UNC
         JOLRETN RC=0   ************************************       SOCO
         LM    R2,R5,0(R1)
         JOLRETN
         TITLE 'VALIDATE KEEP STMT'
         JOLSAVE  CSECT=UJU07KEE
         JOLRETN RC=0   ************************************       SOCO
         LM    R3,R5,0(R1)
* HERE YOU COULD CHECK THAT THE DATA SET IS NOT OVER A CERTAIN
*  SIZE, OR IF IT IS, PERHAPS YOUR INSTALLATION HAS SPECIFIC VOLUMES
*  ON WHICH LARGE DATA SETS ARE TO BE PLACED, IN WHICH YOU CAN
* MAKE A COMMENT (REMEMBER THAT YOU COULD CHANGE THE VOLUME
* IN THE UJU03DS EXIT ABOVE, IF YOU SO DESIRED; HERE IT IS A LITTLE
* LATE, AND ALL YOU CAN DO IS COMMENT ON IT)
         JOLRETN
         TITLE 'VALIDATE SCRATCH STATEMENT'
         JOLSAVE CSECT=UJU08SCR
         JOLRETN RC=0   ************************************       SOCO
         LM    R2,R5,0(R1)
* HERE YOU MAY CHECK IF THE USER IS SCRATCHING 'SYS1,LINKLIB'
*  OR SOME OTHER CRITICAL DATA SET.
         JOLRETN
         TITLE 'VALIDATE DELETE STATEMENT'
          JOLSAVE CSECT=UJU09DEL
         JOLRETN RC=0   ************************************       SOCO
         $CALL UJU08SCR
         $CALL UJU06UNC
         JOLRETN
         LTORG
         END
