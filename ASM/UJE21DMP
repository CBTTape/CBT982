UJE21DMP TITLE 'STORAGE DUMP'
* JOL COPYRIGHT CCS-JOL PTY LTD 1988
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-E001.
         SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
* CREATED APRIL 1988 BY J SUMMERFIELD
*                                   OF KURRAWANG COMPUTER PROGRAMMING
         COPY  JOLGLOBL
         LCLC  &C,&C1,&C2,&C3,&C4,&C5,&C6
         $UJEPARM
         PRINT ON,DATA
UJECOMM  DSECT
         USING UJECOMM,R3,R5
         COPY  UJECOMM
E21WORK  DSECT                                                   88036
         DS    18F             SAVE AREA                         88036
ENDADDR  DS    A
STARTADD DS    A
TEMP     DS    XL5                                               88036
HEXCONV  DS    CL9                                               88036
DPBLOCK  DS    F
DPLINE   DS    F
DPDATA   DS    0CL125
         DS    C
DPADDR   DS    CL6
         DS    C
DPOFFS   DS    CL3,2C
DPHEX    DS    CL75
DPCHARS  DS    CL32
DPLINEL  EQU   *-DPLINE
DPBLKL   EQU   *-DPBLOCK
         DS    0D                                                88036
E21WORKL EQU   *-E21WORK                                         88036
         TITLE 'UJE21DMP: DUMP A STORAGE AREA'
*JE21DMP CSECT                                                   88036
*        DC    D'0'                                              88036
         JOLSAVE CSECT=UJE21DMP,SIZE=E21WORKL
         USING E21WORK,R13                                       88036
         ALR   R0,R1           CALCULATE END ADDRESS             88036
         STM   R0,R1,ENDADDR      STORE PARAMETER ADDRESSES
         CLEAR DPDATA
         LA    R0,DPBLKL
         SLL   R0,16
         ST    R0,DPBLOCK
         LA    R0,DPLINEL
         SLL   R0,16
         ST    R0,DPLINE
         L     R2,STARTADD     ADDRESS OF THE AREA
         MVI   DPDATA,C' '     SINGLE SPACE
E21LP0   DS    0H
         ST    R2,TEMP
         UNPK  HEXCONV,TEMP
         TR    HEXCONV,HEX
         MVC   DPADDR,HEXCONV+2
         S     R2,STARTADD        SUBTRACT ORIGINAL ADDRESS
         ST    R2,TEMP            STORE OFFSET
         A     R2,STARTADD        RESTORE IT
         UNPK  HEXCONV,TEMP
         TR    HEXCONV,HEX
         MVC   DPOFFS,HEXCONV+5
         MVC   DPCHARS,0(R2)   COPY DATA IN CHARACTERS
         TR    DPCHARS,TRTAB   REMOVE SOME HARD-TO-PRINT DATA
         LA    R14,DPHEX       ADDRESS FOR HEX DATA
         LA    R1,2
E21LP1   DS    0H
         LA    R0,4            4 SEGMENTS
E21LP2   DS    0H
         UNPK  HEXCONV,0(5,R2) CONVERT SOME TO HEX
         TR    HEXCONV,HEX
         MVC   0(8,R14),HEXCONV INTO THE PRINT LINE
         LA    R14,9(,R14)     NEXT FOR OUTPUT
         LA    R2,4(,R2)       NEXT FOR INPUT
         BCT   R0,E21LP2
         LA    R14,1(,R14)    AN EXTRA SKIP IN PAGE CENTRE
         BCT   R1,E21LP1
         PRPUT DPBLOCK
         MVI   DPDATA,C' '        SINGLE SPACE
         C     R2,ENDADDR      Q... ANY MORE?
         BNH   E21LP0          A... YES
         JOLRETN RC=0
         DC    20S(*)   FOR PATCHES
TRTAB    DC    256AL1(*-TRTAB) ALL THINGS TO ALL THINGS
         ORG   TRTAB
         DC    64X'40'         FIX THE FIRST ONES
         ORG   TRTAB+C'9'+1
         DC    6C' '           AND THE LAST ONES
         ORG
         LTORG
         END
