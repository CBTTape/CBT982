PLI      TITLE 'PL1 OPTIMISER INTER-FACE FOR INVOKED ROUTINES'
* JOL COPYRIGHT CCS :- 1976
* JOL COPYRIGHT CCS :- 1982
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-C001.
           SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
         SPACE 3
* THIS ROUTINE WILL ACCEPT A CALL FROM A PL1 PROGRAM AND
*  OUTPUT A BLOCK TO THE JOL WORK FILE.
       EJECT
            PRINT OFF
           COPY JOLCOM
           SPACE 3
         PRINT ON,DATA                                            75128
         SPACE 3
         GENSAVE CSECT=UJPLIOP
* CSECT IS CALLED BY THE 'INVOKED' MODULE, AND IS RESPONSIBLE     75128
*  FOR STORING THE INVOKED MODULES OUTPUT IN 'WORKFILE' DATA SET  75128
         SPACE 2                                                  75128
* ON ENTRY R1 POINTS TO THE RECORD THAT THE USER HAS CREATED      75128
*  FOR US TO EXECUTE.                                             75128
         L     R8,0(R1)       GET ADDRESS OF THE RECORD           75128
         L     R2,PLIJOLC
         LTR   R2,R2
         BNZ   PLISTART
         LOAD  EP=JOLCOM
         ST    R0,PLIJOLC
         LR    R2,R0
         DELETE EP=JOLCOM
PLISTART EQU   *
P98TRY2  L     R4,AMACBUF                                         75128
         LH    R5,0(R4)       LOAD CURRENT BLOCK LENGTH           75128
         LH   R9,0(R8)         LOAD LENGTH OF RECORD              75128
         LR    R6,R5          LOAD CURRENT BLOCK LENGTH TO R6,TOO 75128
         AR    R6,R9          ADD NEW BLOCK LENGTH                75128
         LH    R15,LMACBUF    GET LENGTH OF THE WORK BUFFER       75128
         SH    R15,=H'16'      -16,COS INEED SOME ROOM AT THE END 75128
*                             OF THE LAST BLOCK                   75128
         CR    R6,R15         WILL RECORD FIT ?                   75128
         BNH   P98MUV         NO                                  75128
* HERE WE MUST OUTPUT THE BLOCK                                   75128
         LR    R1,R4          TELL 'OPBLOCK' WHERE THE BLOCK IS   75128
*                             THAT HAS TO BE OUTPUT               75128
         BAL   R14,OPBLOCK                                        75128
         MVC   0(4,R4),=AL2(4,0)                                  75128
         CLC   P98TTR1,ZERO   HAS THE FIRST BLOCK BEEN OUTPUT ?   75128
         BNE   P98TRY2        YES, GO O/P RECORD AGAIN            75128
         ST    R1,P98TTR1     NO,SET UP FIRST BLOCK INDICATOR     75128
         B     P98TRY2                                            75128
P98MUV   L     R4,AMACBUF                                         75128
*                             AS AN OUTPUT AREA
         LR    R5,R4          AND SAVE IT IN R5
         LR    R6,R9          SAVE RECORD LENGTH TO GO
         LR    R7,R6          SAVE IT IN R7 TOO
         CH    R6,=H'80'      > 80 BYTES ?                         DASD
         BNH   P98SMALL       NO,SO JUST OUTPUT IT
         LA    R6,80                                               DASD
P98SMALL LA    R6,3(R6)                                           75128
         AH    R5,0(R4)       CALC ADDRESS OF RECORD AREA         75128
         EX    R6,P98MVC                                          75128
         LA    R6,1(R6)    +1 EX MVC ANDFOR RECORD DESC
         SLL   R6,16      ZERO BOTTOM BYTES
         ST    R6,AVTBLE   SAVE IT A SECOND IN UNUSED AREA
         MVC   0(4,R5),AVTBLE  SHIFT TO RECORD DESCRIPTOR
         LH    R1,0(R4)       LOAD BLOCK LENGTH                   75128
         AH    R1,AVTBLE      ADD RECSIZE                         75128
         STH   R1,0(R4)       RESET BLOCK LENGTH                  75128
         GENRETN
P98MVC   MVC   2(*-*,R5),0(R8)                                    75128
PLIJOLC  DC    A(0)
          LTORG
           PRINT NOGEN
           DC  S(*,*,*,*)
           DC  S(*,*,*,*)
           DC  S(*,*,*,*)
           DC  S(*,*,*,*)
           DC  S(*,*,*,*)
           DC  S(*,*,*,*)
         DCBD  DSORG=PS,DEVD=DA                                   75128
           PRINT GEN                                              74303
         END
