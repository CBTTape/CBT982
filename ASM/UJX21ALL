         TITLE 'UJE21ALL: HANDLE DATASET ALLOCATION'
         JOLSAVE CSECT=UJE21ALL
         FIX   'IF DATASET IS TO BE RE-WRITTEN, CALL DELETE NEAR HERE'
*            AND THEN ALLOCATE AS A NEW DATASET
         LA    R0,WORK
         LA    R1,L'WORK
         SLR   R15,R15
         MVCL  R0,R14
         CLC   =CL8' ',DDDDNAME      Q... CONCATENATED FILE?
         BE    E21NOTCT              A... NO
         MVC   SAVEDD,DDDDNAME       COPY THE DDNAME
E21NOTCT DS    0H
         CLC   =C'PRGLIB  ',DDDDNAME Q... PRGLIB?
         BE    E21MVDDN              A... YES
         CLC   =C'STEPLIB ',DDDDNAME Q... STEPLIB?
         BNE   E21X8A                A... NO
E21MVDDN MVC   STEPLBDD,DDDDNAME      MOVE IN THE DDNAME
E21X8A   DS    0H
         CLI   DDTYPE,DDSYSIN      Q... SYSIN DATASET?
         BNE   E21NSSIN            A.. NO
         MVC   DDMBR,DDDSNAME+2    ESTABLISH MEMBER NAME
         CLEAR DDDSNAME,DSN
         MVC   DDDSNAME(L'FCTFLN),SYSINDSN   COPY DSN
         MVC   #DSN(28),#SYSINDS
         CLEAR DDVOLUME
         MVC   DDVOLUME(6),SYSINVOL
         MVI   DDUSE,B'00001000'   DISP=(SHR,KEEP)
         MVI   DDTYPE,DDMEMSPC
         MVI   DDUNITYP,X'80'         TELL ALLOC THAT IT IS A DISK.
         B     E21X8A4
E21NSSIN DS    0H
         SPACE 2
*  BEFORE GOING ANY FURTHER, WHY NOT HAVE A LOOK AROUND TO SEE
*   WHAT IS ALREADY AVAILABLE?
* ****** UNLESS THIS IS A CONCATENATED FILE, IN WHICH CASE WE SHALL
*        DO A PROPER ALLOCATION      CVC
         CLC   =CL8' ',DDDDNAME     CONCATENATED ?
         BE    E21NOFCT             YES, GO THROUGH NORMAL ALLOC
         SPACE 2
         L     R4,ATCB              ADDRESS MY TCB
         L     R4,TCBJSCB-TCB(,R4)          MY JSCB
         ICM   R4,7,JSCBFCTA-JSCB(R4)         FIRST FCT
         BZ    E21NOFCT              EXIT IF NONE ?!! & SSSSSSSSS
         SPACE
*   FOUND THE FCT CHAIN. NOW FIND THE DATASET ENTRY
         SPACE
         USING FCT,R4
E21NXFCT DS    0H
         CLC   FCTFLN,DDDSNAME       Q... IS THIS IT?
         BNE   E21NTDS               A... NO, GO AND TRY THE NEXT
         CLC   =C'$JOL',FCTACSN      Q... NOT IN USE?
         BNE   E21NTDS               A... NO
         SPACE
* NOT IN USE, BUT ONLY USE IT IF:
*     A)   ALLOCATION REQUEST INCLUDES MEMBER AND FCT HAS MEMBER
*  OR
*     B)   NEITHER HAS MEMBER SPECIFIED
         SPACE
         TM    FCTFLG1,FCTF1MEB     Q... MEMBER IN FCT?
         BO    E21MEB1              A... YES, GO AND CHECK DDDET
         CLI   DDMBR,C' '           Q... MEMBER HERE?
         BE    E21FFCT              A... NO, NEITHER HAS
         B     E21NTDS              CONTINUE THE SEARCH
         SPACE
E21MEB1  DS    0H                   MEMBER IN FCT, CHECK DDDET
         CLI   DDMBR,C' '           Q... MEMBER HERE?
         BNE   E21FFCT              A... YES, THEY BOTH HAVE
E21NTDS  DS    0H
         ICM   R4,7,FCTNFCTA         NEXT FCT
         BNZ   E21NXFCT              GO AND TEST THE NEXT ONE
         B     E21NOFCT              END OF CHAIN, AND NOT FOUND
E21FFCT  DS    0H    FOUND AN FCT TO "FIX"
         $TELL (R4)
         MODESET  KEY=ZERO
         TM    FCTFLG1,FCTF1MEB     Q... MEMBER IN FCT?
         BZ    E21NMEB              A... NO, GO AND FIX THE DDNAME
         L     R15,FCTFFBT          ADDRESS FBT
         MVC   028(8,R15),DDMBR     COPY THE MEMBER NAME
E21NMEB  DS    0H
 FIX 'ONE SHOULD ALSO FIX LRECL,BLKSIZE ETC HERE'
         MVC   FCTACSN,DDDDNAME     CHANGE THE DDNAME
         SPACE 2
*  WELL, THAT SHOULD BE ALL
         SPACE
         MODESET KEY=NZERO    BACK TO MY USUAL PROTECT KEY
         MVC   #DSN,=H'26'
         MVC   #VOL,=H'12'
         MVC   VOL(12),DDVOLUME
         JOLERR 005,                                                   .
               'DSID ',DDDSID,                                         .
               ' DDN ',DDDDNAME,' DSN ',#DSN,                          .
               ' VOL ',#VOL,' ALLOC '
         SPACE 3
         B     E21RETN
         SPACE 5
E21NOFCT DS    0H
E21X8A4  DS    0H
         SPACE 2
*  NOW CHECK FOR VOLREF
         SPACE
         CLI   DDVOLUME,C'*'         VOLREF USED?
         BNE   E21X8A5             NO
         BAL   R14,E21VREF         GO AND FIX VOLREF
E21X8A5  DS    0H
         CALL UJE03X8A,(DDOSVB,CALLARE2),VL,MF=(E,CALLAREA)
         ST    R15,TASKRETN
         LTR   R1,R15
         BZ    E21DDOK          ALLOCATED PROPERLY
         CH    R15,=H'172'      FAIL BECAUSE OF NO DEVICE AVAIL?
         BNE   U21CVCAF         NO, GIVE FAILURE MESSAGE
         XC    ALLOCECB,ALLOCECB
   WTOR  'WHEN A TAPE DRIVE IS AVAILABLE, PLEASE REPLY GO',ALLOCREP,   *
               2,ALLOCECB
         WAIT  ECB=ALLOCECB
         B     E21X8A5          TRY AGAIN
U21CVCAF CVD   R15,DBL
         MVC   TASKSSI,=X'40202120'
         ED    TASKSSI,DBL+6
         JOLERR 406,'DSID ',DDDSID,', DDNAME ',                        .
               DDDDNAME,', DS ',#DSN,' FAILED WITH RETURN CODE',       .
               TASKSSI
         CLEAR DPDATA
         LA    R0,DPBLKL
         SLL   R0,16
         ST    R0,DPBLOCK
         LA    R0,DPLINEL
         SLL   R0,16
         ST    R0,DPLINE
         L     R2,CALLARE2     ADDRESS OF AREA TO DUMP
E21LP0   DS    0H
         ST    R2,CALLAREA
         UNPK  DBL(9),CALLAREA(5)
         TR    DBL,HEX
         MVC   DPADDR,DBL+2
         MVC   DPCHARS,0(R2)   COPY DATA IN CHARACTERS
         TR    DPCHARS,TRTAB   REMOVE SOME HARD-TO-PRINT DATA
         LA    R14,DPHEX       ADDRESS FOR HEX DATA
         LA    R1,2
E21LP1   DS    0H
         LA    R0,4            4 SEGMENTS
E21LP2   DS    0H
         UNPK  DBL(9),0(5,R2)  CONVERT SOME TO HEX
         TR    DBL,HEX
         MVC   0(8,R14),DBL    INTO THE PRINT LINE
         LA    R14,9(,R14)     NEXT FOR OUTPUT
         LA    R2,4(,R2)       NEXT FOR INPUT
         BCT   R0,E21LP2
         LA    R14,1(,R14)    AN EXTRA SKIP IN PAGE CENTRE
         BCT   R1,E21LP1
         L     R1,=A(S23DECB)
         WRITE (1),SF,,DPBLOCK,MF=E
         L     R1,=A(S23DECB)
         CHECK (1)
         C     R2,CALLARE2+4   Q... ANY MORE?
         BNH   E21LP0          A... YES
         L     R1,TASKRETN
         CALL  UJS45AER
         TM    E21FLAG,E21CONT
         BO    E21RETN
         JOLERR 500,'RUN TERMINATED DUE TO ALLOCATION ERROR'
         ABEND 2 ,,STEP
E21DDOK  DS    0H
         CLI   DDTYPE,DDSYSOUT    IS IT A SYSOUT FILE
         BE    E21SYSO            YES SO ADJUST MSG
         CLC   =CL6' ',DDVOLUME   IS THERE A VOLSER?
         BE    E21PIV             NO SO PUT IN VOLUME
E21DDOK2 MVC   VOL(12),DDVOLUME
E21DDOK1 EQU   *
         MVC   #DSN,=H'28'
         MVC   #VOL,=H'12'
         JOLERR 003,                                                   .
               'DSID ',DDDSID,                                         .
               ' DDN ',DDDDNAME,' DSN ',#DSN,                          .
               ' VOL ',#VOL,' ALLOC '
         B     E21RETN
E21PIV   EQU   *
         L     R4,ATCB           POINT AT TCB
         L     R4,TCBJSCB-TCB(,R4) POINT AT JSCB
         ICM   R4,7,JSCBFCTA-JSCB(R4) POINT AT FCT CHAIN
         USING FCT,R4             MAP FCT
E21PIV1  EQU   *
         CLC   FCTFLN,DDDSNAME    IS IT THIS ONE?
         BE    E21PIV2            DO WHAT IS REQUIRED
         ICM   R4,7,FCTNFCTA      POINT AT NEXT FCT
         BNZ   E21PIV1            LOOK AT NEXT FCT
         B     E21DDOK2           IMPOSSIBLE TO GET HERE..BUT FOR
*                                  SAFETY SAKE
E21PIV2  EQU   *
         L     R4,FCTVCT           POINT AT VCT
         MVC   VOL(6),8(R4)        PLUG IN VOL
         B     E21DDOK1            AND ISSUE MESSAGE
E21SYSO  EQU   *
         MVC   #DSN,=H'1'
         JOLERR 004,                                                   .
               'DSID ',DDDSID,                                         .
               ' DDN ',DDDDNAME,' SYSOUT=',#DSN,' ALLOC '
E21RETN  DS    0H
         TM    DDUNITYP,X'80'
         BZ    E21NPROP       DON'T PROPOGATE VOLUME AS IT MAY
*                             BE A SCRATCH TAPE (IE NO VOLUME YET)
         CALL  UJE30VOL       PROPOGATE VOLS DOWN JOL 'JOBQ'
E21NPROP MVI   E21FLAG,0
         L     R15,TASKRETN
         B     RETRN
         SPACE 3
*  IT IS NECESSARY TO LOCATE THE FCT FOR THE DATASET WHOSE NAME IS
*    IN DDVOLUME, TO FIND THE CURRENT VOLUMES AND COPY THE LIST TO
*    THE ALLOCATION REQUEST FOR THIS DATASET
E21VREF  DS    0H
         LR    R10,R14              SAVE RETURN ADDRESS
         L     R4,ATCB              ADDRESS MY TCB
         L     R4,TCBJSCB-TCB(,R4)          MY JSCB
         ICM   R4,7,JSCBFCTA-JSCB(R4)         FIRST FCT
         BZ    E21VNFCT              EXIT IF NONE ?!! & SSSSSSSSS
         SPACE
*   FOUND THE FCT CHAIN. NOW FIND THE DATASET ENTRY
         SPACE
         USING FCT,R4
E21VNXTF DS    0H
         CLC   FCTFLN,DDVOLUME+1   Q... IS THIS IT?
         BE    E21VFFCT            A... YES, GO AND GET THE VOLUME INFO
         SPACE
         ICM   R4,7,FCTNFCTA         NEXT FCT
         BNZ   E21VNXTF              GO AND TEST THE NEXT ONE
E21VNFCT DS    0H                    END OF CHAIN, AND NOT FOUND
         JOLERR 507,'DSID ',DDDSID,',DS ',#DSN,                        .
               ' IS NOT ALLOCATED,',' VOLREF COULD NOT BE RESOLVED'
E21VFFCT DS    0H
         SPACE 3
* NOW I HAVE FOUND THE FCT, COPY THE VOLUME SERIAL NUMBERS
         SPACE 2
         CLEAR DDVOLUME
         SLR   R8,R8
         ICM   R8,1,FCTVOLNO        LOAD VOLUME COUNT
         BZ    E21VNFCT             EXIT IF THERE ARE NO VOLUMES
         LA    R0,L'DDVOLUME/6      LOAD NUMBER OF VOLUMES ALLOWED
         CR    R0,R8                CHECK THAT THE SERIAL NUMBERS FIT
         BH    E21VOK               BE IF OK
         JOLERR 512,'DS ',DDDSID,',',#DSN,                             .
               ' IS NOT ALLOCATED,',' REFERS TO TOO MANY VOLUMES'
E21VOK   DS    0H
         L     R6,FCTVCT
         LA    R15,DDVOLUME         ADDRESS FOR VOLUMES
E21VNXTV DS    0H
         MVC   0(6,R15),8(R6)       COPY THE VOLUME SERIAL NUMBER
         LA    R15,6(,R15)          ADDRESS FOR NEXT AREA
         L     R6,4(,R6)            ADDRESS THE NEXT VCT
         BCT   R8,E21VNXTV          CONTINUE UNTIL ALL AREA DONE
         SPACE 2
*  WELL, THAT SHOULD BE ALL: NOW FREE THE ORIGINAL DATASET
         SPACE
         MVC   SAVEDDET,DDDSNDET
         MVC   DDDDNAME,FCTACSN
         MVI   DDDISP+1,C'L'      LAST USE
         CALL  UJE14FRE           FREE THE FILE, ASSUME IT WORKED
         MVC   DDDSNDET(DDLEN),SAVEDDET RESTORE THE ORIGINAL
         UJE22UPD FCTFLN,DDVOLUME FIX FUTURE VOLUME REFERENCES
         DROP  R4
         BR    R10                  RETURN
         SPACE 5
         DC    20S(*)   FOR PATCHES
TRTAB    DC    256AL1(*-TRTAB) ALL THINGS TO ALL THINGS
         ORG   TRTAB
         DC    64X'40'         FIX THE FIRST ONES
         ORG   TRTAB+C'9'+1
         DC    6C' '           AND THE LAST ONES
         ORG
ALLOCREP DC C'   ' FOR ALLOCATION REPLIES
ALLOCECB DC  F'0'  FOR ALLOCATION REPLIES
