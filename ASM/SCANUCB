         GENSAVE CSECT=SCANUCB
         L     R5,16          LOAD CVT ADDRESS
         USING CVT,R5
         L     R6,CVTILK2     UCB LOOK UP TABLE
         LR    R15,R5
         SH    R15,=H'6'
         USING CVTMDL,R15
         LH    R1,CVTMDL
         SLL   R1,4
         ST    R1,PCKMDL
         OI    PCKMDL+3,X'0F' PUT SIGN IN
         UNPK  MODEL,PCKMDL
         OI    MODEL+L'MODEL-1,C'0'
         MVC   RELEASE,CVTRELNO
         MVI   RELEASE+2,C'.'
         MVC   RELEASE+3(1),CVTRELNO+2
         DROP  R15
         CLEAR SYSTEM1,SYSTEM2
         TM    CVTDCB,CVT1SSS
         BO    PCP
         TM    CVTDCB,CVT2SPS
         BO    MFTVS1
* MVT | VS2 HERE
         TM    CVTDCB,CVT6DAT
         BO    VS2
         MVC   SYSTEM1,=C'MVT'
         B     ENDSYS1
VS2      MVC   SYSTEM1,=C'VS2'
         MVC   SYSTEM2,=C'SVS'
         TM    CVTDCB,CVTMVS2
         BZ    ENDSYS1
         MVC   SYSTEM2,=C'MVS'
         B     ENDSYS1
         SPACE 2
PCP      MVC   SYSTEM1,=C'PCP'
         B     ENDSYS1
         SPACE 2
MFTVS1   TM    CVTDCB,CVT6DAT
         BO    VS1
         MVC   SYSTEM1,=C'MFT'
         B     ENDSYS1
VS1      MVC   SYSTEM1,=C'VS1'
ENDSYS1  EQU   *
         #PRINT 'SYSTEM IS ',MODEL,' RELEASE ',RELEASE,' OF ',SYSTEM1, *
               SYSTEM2
    #PRINT ' '
    #PRINT 'UNIT  TYPE     VOLUME'
    #PRINT ' '
         L     R6,CVTILK2     LOOK AT VCB NOW.
UCBLOOP  CLC   =X'FFFF',0(R6) REACHED END ?
         BE    ENDUCB
         CLC   =X'0000',0(R6) DUMMY ENTRY ?
         BE    BUMPUCB
* UCB ADDRESS HERE
         LH    R7,0(R6)
         USING UCBS,R7
         MVC   UNITNAME,UCBNAME
         CLI   UCBTYP+2,X'08'
         BE    URECD          UNIT RECORD
         CLI   UCBTYP+2,X'80'
         BE    MAGTAPE
         CLI   UCBTYP+2,X'20'
         BE    DA
         CLI   UCBTYP+2,X'10'
         BE    GRAF
         CLI   UCBTYP+2,X'40'
         BE    COMM
BUMPUCB  LA    R6,2(R6)
         B     UCBLOOP
         SPACE 3
URECD    #PRINT UNITNAME,' UNIT-RECORD'
         B     BUMPUCB
GRAF     #PRINT UNITNAME,' GRAPHIC'
         B     BUMPUCB
COMM     #PRINT UNITNAME,' COMMUNICATIONS'
         B     BUMPUCB
MAGTAPE  MVC   VOLUME,UCBVOLI
         #PRINT UNITNAME,' MAG TAPE ',VOLUME
         B     BUMPUCB
DA       CLEAR IBMGENM
      MVC  VOLUME,UCBVOLI
         SR    R15,R15
         IC    R15,UCBTYP+3
         CH    R15,=X'000D'
         BH    DAUNER
         SLL   R15,3          *8
         LA    R15,IBMGENDA(R15)
         MVC   IBMGENM,0(R15)
         B     DACONT
DAUNER   MVC   IBMGENM,=CL8'********'
DACONT   #PRINT UNITNAME,' ',IBMGENM,' ',VOLUME,' '
      TM UCBSTAT,UCBBPRV
         BNO   NPRIV
         #PRINT ' PRIVATE ',SKIP=0
NPRIV    TM UCBSTAB,UCBBPUB
         BNO   NPUB
         #PRINT ' PUBLIC ',SKIP=0
NPUB     TM    UCBSTAB,UCBBSTR
         BNO   NSTR
         #PRINT 'STORAGE ',SKIP=0
NSTR     EQU   *
         TM    UCBTYP+1,X'20'
         BO    DASHRD
         B     DAPRINT
DASHRD   #PRINT 'SHARED ',SKIP=0
DAPRINT  EQU   *
    TM UCBSTAT,UCBRESV
    BNO  NORESV
    #PRINT ' RESERVED ',SKIP=0
NORESV  TM UCBSTAT,UCBPRES
    BNO  NOPRES2
    #PRINT ' PERM  MOUNTED ',SKIP=0
NOPRES2   TM  UCBSTAT,UCBSYSR
   BNO  NOSYSR
   #PRINT ' SYSTEM RES ',SKIP=0
NOSYSR  EQU  *
         B     BUMPUCB
ENDUCB   #PRINT 'END'
         GENRETN
         LTORG
MODEL    DC    CL3' '
RELEASE  DC    CL4' '
SYSTEM1  DC    CL3' '
SYSTEM2  DC    CL3' '
UNITNAME DC    CL3' '
VOLUME   DC    CL6' '
IBMGENM  DC    CL8' '
MOUNTATT DC    CL8' '
IBMGENDA DC    CL8'2305(1)'        0
         DC    CL8'*****'          1
         DC    CL8'*****'          2
         DC    CL8'*****'          3
         DC    CL8'*****'          4
         DC    CL8'*****'          5
         DC    CL8'2305(1)'        6
         DC    CL8'2305(2)'        7
         DC    CL8'2314'           8
         DC    CL8'3330'           9
         DC    CL8'3340'           10
         DC    CL8'*****'          11
         DC    CL8'*****'          12
         DC    CL8'3330-1'         13
      DC   F'0'
SHARED    DC   CL8' '
PCKMDL    DC  PL4'0'
BLANKS    DC  CL40' '
CVT      CVT DSECT=YES,PREFIX=YES
UCBS     EQU   *
         IEFUCBOB   LIST=YES
         END
