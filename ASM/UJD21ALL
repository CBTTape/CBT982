UJD21ALL TITLE 'HANDLE DYNAMIC DATASET ALLOCATION'
* JOL COPYRIGHT CCS-JOL PTY LTD 1988
* JOL COPYRIGHT CLEMENT CLARKE 2010
* MODIFIED TO CALL D03 WITH A PARAMETER                           J60
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-E001.
         SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
* LAST SOURCE UPDATE
*   CHANGES:-
*     1.
         COPY  JOLGLOBL
         LCLC  &C                                                 J40B
         $UJEPARM
&TYPE    SETC  'SCHED'                                           88036
         PRINT ON,DATA
UJECOMM  DSECT
         USING UJECOMM,R3,R5
         COPY  UJECOMM
D21WORK  DSECT                                                    J40B
         DS    18F                                                J40B
D21DDN   DS    CL8                                                J40B
D21REPLY DC    C'   '           FOR ALLOCATION REPLIES
D21RPECB DC    F'0'             FOR ALLOCATION REPLIES
D21WORKS EQU   *-D21WORK                                          J40B
         JOLSAVE CSECT=UJD21ALL,                                       /
               SIZE=D21WORKS                                      J40B
         USING D21WORK,R13                                        J40B
*
*  DYNAMIC ALLOCATION OF DATA SETS.
*
*  PROBLEMS:-
*   1. SYSOUT DATA SETS GET SPUN OFF.
*      SOLUTION: ALLOCATE WITH SYSNNN NUMBER, THEN CONCATENATE
*                REAL DDNAME. AT END, DO NOT FREE, BUT DECONCATENATE.
* NO CLEM
*        PUT OUT REAL DD CARDS WITH DDNAMES NOT USED BY PGMS
*              MODIFY DDNAMES  AS REQUIRED
         FIX   'SOME PROBLEMS  WITH SYSOUT HERE'
*
*   2. TAPE SCRATCH VOLUMES WRITING LABEL 1 THEN 2 IN SAME STEP.
*      SVC99 CAN'T RETURN VOLUME NAME.
*      SOLUTION: ALLOCATE WITH SYSNNN NUMBER, OPEN FOR OUTPUT,
*                ALLOCATE REAL DDNAME, FREE SYSNNN, PRESS ON.
*
*        HOW ABOUT OPEN OUT,  READ LABEL, CLOSE W/- LEAVE
*              OPEN OUT GETS TMS'S OK TO WRITE.
*              CLOSE W/- LEAVE KEEPS TAPE MOUNTED
*
         FIX   'IF DATASET IS TO BE RE-WRITTEN, CALL DELETE NEAR HERE'
*            AND THEN ALLOCATE AS A NEW DATASET
         LA    R0,WORK
         LA    R1,L'WORK
         SLR   R15,R15
         MVCL  R0,R14
*        CLI   DDTYPE,DDSYSOUT       IS IT A SYSOUT FILE
*        BNE   D21NPRT               NO, BYPASS CLEARING DDNAME
D21NPRT  DS    0H
         CLC   =C'PRGLIB  ',DDDDNAME Q... PRGLIB?
         BE    D21MVDDN              A... YES
         CLC   =C'STEPLIB ',DDDDNAME Q... STEPLIB?
         BE    D21STEPL              A... YES
         CLC   =C'JOBLIB  ',DDDDNAME Q... JOBLIB?
         BE    D21JOBL               A... YES
         B     D21TSYS               A... NO, SEE WHAT IT IS
         FIX   'WE JUST FOUND A STEPLIB'
D21STEPL MVC   DDDDNAME,=CL8'STEPLIB$' CHANGE FOR DYN ALLOC
D21MVDDN MVC   STEPLBDD,DDDDNAME      MOVE IN THE DDNAME
         B     D21NSSIN
D21JOBL  MVC   DDDDNAME,=CL8'JOBLIB$' CHANGE FOR DYN ALLOC
         MVC   JOBLBDD,DDDDNAME       MOVE IN THE DDNAME
D21TSYS  CLI   DDTYPE,DDSYSIN      Q... SYSIN DATASET?
         BNE   D21NSSIN            A.. NO
         MVC   DDMBR,DDDSNAME+2    ESTABLISH MEMBER NAME
         CLEAR DDDSNAME,DSN
*        MVC   DDDSNAME(L'DDDSNAME),=CL44'&&INST'
         MVC   DDDSNAME,SYSINDSN
         MVC   #DSN(28),#SYSINDS
         CLEAR DDVOLUME
         MVC   DDVOLUME(6),SYSINVOL
         MVI   DDUSE,B'00001000'   DISP=(SHR,KEEP)
         MVI   DDTYPE,DDMEMSPC
         MVI   DDUNITYP,X'80'         TELL ALLOC THAT IT IS A DISK.
&C       SETC  (&X8)'DA'.(1-&X8)'SYSDA'                           J40B
*        MVC   DDUNIT(3),SYSINUNI     UNIT ADDRESS
         MVC   DDUNIT,=CL8'&C'   UNIT NAME                        J40B
         MVC   DDUNIT,=CL8'0A83' UNIT NAME                        J60Z
         B     D21GO                                              J40B
         B     D21X8A4
D21NSSIN DS    0H
         SPACE 2
*  BEFORE GOING ANY FURTHER, WHY NOT HAVE A LOOK AROUND TO SEE
*   WHAT IS ALREADY AVAILABLE?
* ****** UNLESS THIS IS A CONCATENATED FILE, IN WHICH CASE WE SHALL
*        DO A PROPER ALLOCATION      CVC
         AGO   .Z                                                 J40B
         CLC   =CL8' ',DDDDNAME     CONCATENATED ?
         B     XXXALLOC             YES, GO THROUGH NORMAL ALLOC
         BE    D21X8A4              YES, GO THROUGH NORMAL ALLOC
         SPACE 2
XXXALLOC DS    0H
.Z       ANOP                                                     J40B
         FIX   'THIS HAS TO BE MADE TO WORK'                      J40B
         CLI   DDVOLUME,C' '    Q,,, DO WE KNOW ITS VOLUME?       J40B
         BNE   D21GO            A... YES                          J40B
         DBGMSG 0001,'SEARCHING DSABS'                            J40B
         FINDQDB D21DDN,                                          J40B .
               DDDSNAME,ERRET=D21GO,MF=(E,CALLAREA)               J40B
         AIF   (&X8).X80010                                       J40B
         LR    R15,R0                                             J40B
         USING JFCB-16,R15                                        J40B
         MVC   DDVOLUME,JFCBVOLS COPY VOLUMES RETURNED BY SRCHQDB J40B
         DROP  R15                                                J40B
         DBGMSG 201,'I FOUND ',DDDSNAME,' ON ',DDVOLUME
         AGO   .NX8010                                            J40B
.X80010  ANOP                                                     J40B
         MVC   DDVOLUME(6),X'094'(R1)                             J40B
         DBGMSG 0001,'SRCHQDB SAYS ',DDVOLUME                     J40B
.NX8010  ANOP                                                     J40B
D21GO    DS    0H                                                 J40B
         CLC   =CL8' ',DDDDNAME      Q... CONCATENATED FILE?
         BE    D21NOTCT              A... NO
         MVC   SAVEDD,DDDDNAME       COPY THE DDNAME
D21NOTCT DS    0H
         SPACE 2
*  NOW CHECK FOR VOLREF
         SPACE
         DBGMSG 201,'COMPILER SAYS DS IS ON ',DDVOLUME            J40B
         CLI   DDVOLUME,C'*'         VOLREF USED?
         BNE   D21G010             NO
         CALL  UJD24VRF        RESOLVE VOLUME REF                 J40B
         DBGMSG 200,'VOLREF RESOLVED TO ',DDVOLUME                J40B
*        BAL   R14,D21VREF         GO AND FIX VOLREF
D21G010  DS    0H                                                 J40B
*        CALL  UJD03DS1         ALLOCATE THE DATA SET
        $CALL  UJD03DS1,DDDSNDET                                  J60
         SPACE 3
         LTR   R15,R15                                           88036
         BNZ   U21CVCAF                                          88036
         B     D21RETN
         SPACE
D21X8A4  DS    0H
D21X8A5  DS    0H
*        CALL  UJD03DS1         ALLOCATE THE DATA SET
        $CALL  UJD03DS1,DDDSNDET                                  J60
         LTR   R1,R15
         BZ    D21DDOK          ALLOCATED PROPERLY
         AGO   .XXXX                                              J40B
*        CALL  UJD03X8A,(DDOSVB,CALLARE2),VL,MF=(E,CALLAREA)
*        ST    R15,TASKRETN
*        LTR   R1,R15
*        BZ    D21DDOK          ALLOCATED PROPERLY
*        CH    R15,=H'172'      FAIL BECAUSE OF NO DEVICE AVAIL?
*        BNE   U21CVCAF         NO, GIVE FAILURE MESSAGE
*        XC    D21RPECB,D21RPECB
*  WTOR  'WHEN A TAPE DRIVE IS AVAILABLE, PLEASE REPLY GO',D21REPLY,   *
               2,D21RPECB
*        WAIT  ECB=D21RPECB
*        B     D21X8A4          TRY AGAIN
.XXXX    ANOP                                                     J40B
U21CVCAF DS    0H
         AGO   .XXXY                                              J40B
         CVD   R15,DBL
         MVC   TASKSSI,=X'40202120'
         ED    TASKSSI,DBL+6
         JOLERR 406,'DSID ',DDDSID,', DDNAME ',                        .
               DDDDNAME,', DS ',#DSN,' FAILED WITH RETURN CODE',       .
               TASKSSI
.XXXY    ANOP                                                     J40B
         JOLERR 600,'RUN TERMINATED DUE TO ALLOCATION ERROR'
*        JOLRETN
D21RETN  DS    0H
         TM    DDUNITYP,X'80'
         BZ    D21NPROP       DON'T PROPOGATE VOLUME AS IT MAY
*                             BE A SCRATCH TAPE (IE NO VOLUME YET)
         CALL  UJD30VOL       PROPOGATE VOLS DOWN JOL 'JOBQ'
D21NPROP MVI   D21FLAG,0
         L     R15,TASKRETN
D21DDOK  DS    0H
         B     JRETN
         AGO   .X821FCT
         SPACE 3
*  IT IS NECESSARY TO LOCATE THE FCT FOR THE DATASET WHOSE NAME IS
*    IN DDVOLUME, TO FIND THE CURRENT VOLUMES AND COPY THE LIST TO
*    THE ALLOCATION REQUEST FOR THIS DATASET
D21VREF  DS    0H
         BR    R14             ALREADY DONE                       87150
         LR    R10,R14              SAVE RETURN ADDRESS
         L     R4,ASHTCB            ADDRESS MY TCB
         L     R4,TCBJSCB-TCB(,R4)          MY JSCB
         ICM   R4,7,JSCBFCTA-JSCB(R4)         FIRST FCT
         BZ    D21VNFCT              EXIT IF NONE ?!! & SSSSSSSSS
         SPACE
*   FOUND THE FCT CHAIN. NOW FIND THE DATASET ENTRY
         SPACE
         USING FCT,R4
D21VNXTF DS    0H
         CLC   FCTFLN,DDVOLUME+1   Q... IS THIS IT?
         BE    D21VFFCT            A... YES, GO AND GET THE VOLUME INFO
         SPACE
         ICM   R4,7,FCTNFCTA         NEXT FCT
         BNZ   D21VNXTF              GO AND TEST THE NEXT ONE
D21VNFCT DS    0H                    END OF CHAIN, AND NOT FOUND
         JOLERR 507,'DSID ',DDDSID,',DS ',#DSN,                        .
               ' IS NOT ALLOCATED,',' VOLREF COULD NOT BE RESOLVED'
D21VFFCT DS    0H
         SPACE 3
* NOW I HAVE FOUND THE FCT, COPY THE VOLUME SERIAL NUMBERS
         SPACE 2
         CLEAR DDVOLUME
         SLR   R8,R8
         ICM   R8,1,FCTVOLNO        LOAD VOLUME COUNT
         BZ    D21VNFCT             EXIT IF THERE ARE NO VOLUMES
         LA    R0,L'DDVOLUME/6      LOAD NUMBER OF VOLUMES ALLOWED
         CR    R0,R8                CHECK THAT THE SERIAL NUMBERS FIT
         BH    D21VOK               BE IF OK
         JOLERR 512,'DS ',DDDSID,',',#DSN,                             .
               ' IS NOT ALLOCATED,',' REFERS TO TOO MANY VOLUMES'
D21VOK   DS    0H
         L     R6,FCTVCT
         LA    R15,DDVOLUME         ADDRESS FOR VOLUMES
D21VNXTV DS    0H
         MVC   0(6,R15),8(R6)       COPY THE VOLUME SERIAL NUMBER
         LA    R15,6(,R15)          ADDRESS FOR NEXT AREA
         L     R6,4(,R6)            ADDRESS THE NEXT VCT
         BCT   R8,D21VNXTV          CONTINUE UNTIL ALL AREA DONE
         SPACE 2
*  WELL, THAT SHOULD BE ALL: NOW FREE THE ORIGINAL DATASET
         SPACE
         MVC   SAVEDDET,DDDSNDET
         MVC   DDDDNAME,FCTACSN
         MVI   DDDISP+1,C'L'      LAST USE
         CALL  UJD14FRE           FREE THE FILE, ASSUME IT WORKED
         MVC   DDDSNDET(256),SAVEDDET RESTORE THE ORIGINAL
         UJD22UPD FCTFLN,DDVOLUME FIX FUTURE VOLUME REFERENCES
.X821FCT ANOP
         SPACE 5
         DC    20S(*)   FOR PATCHES
         END
