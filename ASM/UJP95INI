* JOL COPYRIGHT CLARKE COMPUTER SOFTWARE 1971,1972,1973,1974,1975
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-C001.
           SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
           COPY JOLCOM
           JOLSAVE CSECT=UJP95INI
            PRINT DATA
* THIS MODULE RECEIVES CONTROL AT THE START OF A NEW JOB
*    FIRST CALL,IT DECODES THE COMPILER PARM FIELD
*    IF AN *JOL CARD IS PRESENT,IT DECODES THE OPTIONS ON IT
* ANOTHER MODULE THEN (IF REQD) PRINTS OUT THE OPTIONS IN SHORT
*    OR LONG FORM.
           SPACE 3
* THE ALLOWABLE OPTIONS ARE LISTED IN THE TABLE 'OPTIONS'
           SPACE 3
***********************************************************************
*
* THE FOLLOWING NEW OPTIONS WILL BE SUPPORTED BY JOL, VERSION 3.0
*  DATE NOV 28,1975.
* 1.
*    FLAG0     PRINT ALL ERRORS, WARNINGS, ETC.
*              THIS IS THE DEFAULT
*
*    FLAG1     PRINT ONLY WARNINGS AND ABOVE SEVERITY
*
*    FLAG2     PRINT ONLY ERRORS AND ABOVE SEVERITY
*
*    FLAG3     PRINT ONLY SEVERE ERRORS AND ABOVE SEVERITY
*
*    FLAG4     PRINT ONLY TERMINAL ERRORS AND ABOVE SEVERITY
*
*    FLAG5     PRINT ONLY COMPILER ERRORS (NO ABOVE ...)
*
* THESE OPTIONS ARE TO SATISFY STANDARDS STND2703, AND SHELLS JOL26002.
           SPACE 2
* 2.
*    NPRINT OR NP TO STOP JOL PRINTING ANY JOL STATEMENTS AT
*              COMPILE TIME.
*
*              ANY ERROR MESSAGE (OR ABOVE SEVERITY) WILL BE PRINTED,
*              AND THE MESSAGE WILL ALSO BE PLACED ON THE OPERATORS
*              CONSOLE (ROUTECDE=(2,11),DESC=6)
*
* THIS OPTION SATISFIES SHELLS JOL26016.
           SPACE 2
* 3.
*    FLAGM=COL WILL PRINT AN ASTERISK PRECEEDING THE COLUMN
*              INDICATED.
*              IT MAY THEN BE DETERMINED EASILY IF A CHARACTER
*              HAS BEEN INVALIDLY CODED OUTSIDE THE SOURCE
*              MARGIN LIMITS.
*
* THIS OPTION SATIFIES SHELLS JOL26022
           SPACE 2
* 4.
*    NPC       WILL STOP JOL PRODUCING COMMENTS IN THE GENERATED JCL
*              (WITH THE EXCEPTION OF PARM INFORMATION).
*              THIS WILL STOP THE JOBQ BEING FILLED FOR BIG JOBS.
* THIS SATISFIES SHELLS JOL26006.
           SPACE 2
* 5.
*    DUMPCARD='JCLSTRING' WILL ALTER THE JOLGEN SYSUDUMP DD CARD
**** NOTE THIS OPTION MAY NOT BE NECESSARY, BECAUSE I BELIEVE THE
*    LAST SYSUDUMP | SYSABEND DDNAME IN THE TIOT IS USED INSTEAD
*    OF THE FIRST.
           SPACE 1
*
***********************************************************************
*
* IN ADDITION, THE FOLLOWING CHANGES WILL BE MADE
*
* 1. JOL26025.
*    NODEFAULT OR NODEF KEYWORD IS ADDED TO THE DATA-SET DECLARE
*          TO STOP DEFAULTS BEING ADDED TO THE DECLARATION.
           SPACE 2
* 2. JOL26
*    INFORMATIONAL MESSAGES WILL BE GIVEN WHEN DEFAULTS ARE MADE
*    TO ANY DECLARATION. THESE MAY BE SUPPRESSED BY THE FLAG1 OPTION.
           SPACE 2
* 3. STND2731
*    A WARNING MESSAGE WILL BE MADE WHEN TWO DSIDS HAVING THE SAME
*    DSNAME BUT DIFFERENT DCB INFORMATION. IF THE VOLUME OR UNIT
*    INFORMATION DIFFERS, THE DSIDS WILL BE CONSIDERED TO BE
*    FOR DIFFERENT DATA SETS, AND NO MESSAGE WILL BE PRODUCED.
           SPACE 2
* 4. TO ALLOW FOR SIMPLE RESETTING OF GENERATION NUMBERS, IF A
*    '+' SIGN IS FOLLOWED BY A '-' SIGN, THE NEGATIVE SIGN WILL
*    HAVE PRECEDENCE.
*    CONSIDER THE FOLLOWING JOL CODE:-
*          %GEN=0;
*          IF %RESTART=2 THEN %GEN=%GEN-2; /* =-2 NOW */
*          DCL X DS DSN GDG(+%GEN);
*      /* AFTER REPLACEMENT, THE DSN WILL BE 'GDG(+-2)', WHICH
*          WILL RESULT IN 'GDG(-2)'. */
* JOL2 021
           SPACE 2
* 5. JOL26.
*    WHEN A + GDG DATA SET IS FOUND WITHOUT A VOLUME OR UNIT SPECIFIED,
*    JOL WILL SEARCH THE SYSTEM CATALOG FOR THE (0) GENERATION, AND
*    COPY THE UNIT TYPE AND VOLUME (EXCEPT FOR TAPE DATA SETS,IN WHICH
*    CASE A SCRATCH VOLUME WILL BE USED).
           SPACE 2
* 6. STND2735.
*    WHEN A 'MOD' DATA SET (OR USES) IS FOUND, JOL WILL SEARCH THE
*    CATALOG IN AN ATTEMPT TO COPY THE VOLUME AND UNIT.
*    IF THE DATA SET IS NOT FOUND, NORMAL JOL DEFAULTS WILL APPLY.
           SPACE 2
* 7. JOL26026.
*    AN ATTEMP TO WRITE A (0) OR (-NN) GDG WILL GIVE AN ERROR MESSAGE.
           SPACE 2
* 8. STND2705.
*    WHEN A MACRO INCLUDES TEXT, THE INCLUDED TEXT WILL NOT BE
*    PRINTED UNLESS THE 'PM' OPTION IS TURNED ON.
           SPACE 2
* 9. STND2707.
*    WHEN DEFAULT UNITS ARE APPLIED TO A DUMMY DATA SET, THE DEFAULT
*    WILL BE 'SYSDA'.
           SPACE 2
*10. (NO NUMBER) -STANDARD AND SHELL SUGGESTION
*    DEFAULT SPACE WILL NOT BE MADE TO TAPE DATA-SETS.
           SPACE 2
*11. STND2728,STND2728.
*    THE COMPILER WILL PRODUCE DLM CARDS OF X'00FF' FOR THE INIT
*    STEP
           SPACE 2
*12. STND2741.
*    THE NUMBER OF SYMBOLICS WILL BE 200.
           SPACE 2
*13. STND2750.
*    ADDITION AND SUBTRACTION MAY BE PERFORMED ON %LIST AND %CARD
*    SUBSCRIPTS.
*          %LIST(%X-%Y) WILL BE ACCEPTABLE.
           SPACE 2
*14. (NO NUMBER)
*    TO ASSIST IN WRITING INVOKED ROUTINES, JOL WILL USE DDNAMES
*    OTHER THAN SYSPRINT,SYSUT1 ETC IF THE FOLLOWING DDNAMES ARE
*    PROVIDED INSTEAD.
*          $$JPRINT (SYSPRINT)
*          $$JWORK1 (SYSUT1)
*          $$JWORK2 (SYSUT2)
*          $$JLOAD  (JOLLOAD)
*          $$JINCL  (PROCLIB)
*          $$JMACRO (MACROLIB)
*          $$JIN    (SYSIN)
*    IF THE $$ DDNAMES ARE NOT PRESENT, JOL WILL ATTEMPT TO USE THE
*    STANDARD DDNAMES.
           SPACE 2
*15. STANDARD TELEX NOV 26,75
*    MESSAGE UJV00-01 WILL BE ALTERED TO INDICATE HOW MUCH EXTRA
*    STORAGE IS REQUIRED TO CONTAIN THE JOB.
*** NOTE, THIS IS A TEMPORARY PROBLEM AS CODE IS 75% WRITTEN TO USE
*    A DIFFERENT METHOD OF RESOLVING DATA SET USAGE.
           SPACE 1
*16. (NO NUMBER) - STANDARD,SHELL,NATUAL MUTUAL
*    REAL STEPNAMES WILL PRODUCED ON 'EXEC' CARDS.
*    IF MULTIPLE (DUPLICATE) STEP NAMES ARE FOUND, $$NNNN NAMES
*    WILL BE USED AS USUAL.
*    %SYSLABEL MAY BE USED TO TRANSFER THE LABEL ON MACRO INVOCATIONS
*    TO THE RUN STATEMENT OF PROGRAMS WITHIN MACROS
           SPACE 3
           SPACE
         MVC   ICOMMAND,=CL8'MACRO' SO ASSIGNMENT ROUTINE         75128
*                             AND DECLARE ROUTINES WON'T DO SO    75128
*                             MUCH MESSING ROUND STORING VALUES   75128
         SPACE 2
           USING JOLCOM,R2
           USING TKNX,R3
         L     R4,AJOLGEN                                         75128
         USING GENDETS,R4                                        75128
P95LEAV  EQU   *                                                  75128
          $CALL UJSPLIT        SPLIT INTO TOKENS                  74303
           GETTKN 1            SEE IF '* JOL' AND SKIP IF SO
           CLI TKN,C'*'
           BNE P95GOTST
P95SKIPF EQU   *                                                  75128
           GETTKN 3            SKIP 'JOL'
           CLI TKN,C'('
           BNE P95GOTST
P95SKIP1   EQU *
           GETTKN (1)
P95GOTST   EQU *
           STH R1,TKNCURR
           CLI TKN,C' '        REACHED END ?
           BE  P95RETN0
           CLI TKN,C')'        END OF OPTION ?                    75311
           BE  P95SKIP1                                           75311
           CLI TKN,C','        SKIP ','S
           BE  P95SKIP1
           SPACE
           CLI TKN,C'/'        IS IT THE START OF SYMBOLIC PARAMETER
*                              PROCESSING ?
           BE P95SYM
           LM  R5,R7,OPTIONS   LOAD THE BXLE CONSTANTS
P95TOPT    CLC TKN(8),0(R5)    TEST IF REQD OPTION
           BE  P95GOTOP        YES,GO LOAD ADDRESS AND DEAL WITH IT
           BXLE R5,R6,P95TOPT  NO,CONTINUE LOOKING FOR KEYWORD
* ERROR
         JOLERR 101,'UNKNOWN COMPILER OPTION ''',#TKN,''' IGNORED' 5128
P95SKIP2   LH  R1,TKNCURR      LOAD TKN POINTER
           B   P95SKIP1        BRANCH AND SKIP TO NEXT ONE
           SPACE 3
P95GOTOP   EQU *
         MVC   DBL+2(2),8(R5) SHIFT S(ADDRESS) TO DBL             75128
         MVC   DBL(2),P95LAR15 SHIFT FIRST PART OF 'LA R15'       75128
         EX    R0,DBL         15=ADDRESS NOW                      75128
           BR  R15             AND BRANCH TO IT
           SPACE
SM         GETTKN (R1)         GET NEXT TOKEN                     75311
           CLI TKN,C'='        '='                                75311
           BE  SM              YES,SKIP IT                        75311
           CLI TKN,C'('        '('                                75311
           BE  SM              YES,SKIP IT                        75311
* HERE WE HAVE THE THING AFTER THE SM=( KEYWORDS                  75311
           STH R1,TKNCURR      SAVE TKN POINTER                   75311
           TNUM #TKN           CONVERT TO NUMERIC                 75311
           LTR R15,R15         DID IT ?                           75311
           BNZ SMERR           NO -> GIVE ERROR MESSAGE           75311
           STH R1,SM1          YES, STORE SM1 FIELD               75311
           LH  R1,TKNCURR      RE-LOAD TOKEN POINTER.             75311
P95SM2     GETTKN (R1)         GET NEXT TOKEN                     75311
           CLI TKN,C','        ','                                75311
           BE  P95SM2          YES, SKIP IT                       75311
           STH R1,TKNCURR      NO, SAVE CURRENT TOKEN NUMBER.     75311
           TNUM #TKN           YES, CONVERT TO NUMBERIC           75311
           LTR R15,R15         DID IT CONVERT ?                   75311
           BNZ SMERR           NO, GIVE ERROR                     75311
           STH R1,SM2          YES, STORE SM2 FIELD               75311
           LH  R1,TKNCURR      GET NEXT                           75311
P95SM3     GETTKN (R1)         AND                                75311
           CLI TKN,C','            SO                             75311
           BE  P95SM3                 ON                          75311
           STH R1,TKNCURR                                         75311
          TNUM #TKN                                               75311
           LTR R15,R15                                            75311
           BNZ SMERR                                              75311
           STH R1,SM3                                             75311
* NOW CHECK FOR ')'                                               75311
           GETTKN TKNCURR                                         75311
           CLI TKN,C')'                                           75311
           BNE *+8                                                75311
           STH R1,TKNCURR                                         75311
* HERE WE HAVE REACHED THE END OF THE SM FIELDS.                  75311
* WE HAD BETTER PERFORM A VALIDITY CHECK OR TWO.                  75311
           CLC SM1,SM2                                            75311
           BL  P95SM1OK                                           75311
           JOLERR 206,'SM2 NOT GREATER SM1',                      75311*
               ':- DEFAULT SOURCE MARGINS USED'                   75311
           B   P95SMDEF                                           75311
* NOW CHECK THAT SM3 IS NOT IN THE MIDDLE OF THE RANGE.           75311
P95SM1OK   CLC SM3,ZERO        =0 ?                               75311
           BE  P95SM3OK        OK THEN                            75311
           CLC SM2,SM3                                            75311
           BL  P95SM3OK                                           75311
           JOLERR 207,'SM(3) IS WITHIN SM(1)-SM(2) RANGE',        75311*
               ':-DEFAULT SOURCE MARGINS USED'                    75311
           B   P95SMDEF                                           75311
P95SM3OK   B   P95SKIP2        NEXT ITEM ...                      75311
P95SMDEF   MVC  SM1,=H'1'
           MVC  SM2,=H'72'
           MVC  SM3,=H'73'
           MVC  FLAGCOL,ZERO
           B  P95SKIP2       BACK TO OTHER OPTIONS
          SPACE 3
           SPACE 3                                                75311
SMERR      JOLERR 310,'SM OPTION ERROR AT ''',#TKN,''''           75311*
               ':-DEFAULT SOURCE MARGINS USED'                    75311
           B   P95SMDEF                                           75311
FLAG       GETTKN (R1)         GET NEXT AFTER FLAG OPTION.        75311
           CLI TKN,C'='        =?                                 75311
           BE  FLAG            DON'T WANT IT                      75311
           CLI TKN,C'('        '('                                75311
           BE  FLAG            DON'T WANT IT EITHER               75311
           STH R1,TKNCURR      SAVE R1                            75311
           TNUM #TKN           CONVERT TO NUMERIC                 75311
           LTR R15,R15         DID IT CONVERT ?                   75311
           BNZ FLAGERR         NO -> ERROR                        75311
           STH R1,FLAGCOL
           LTR R1,R1           ANY FLAGCOL?
           BZ  P95SKIPL        ->NO...SKIP LEFT
           CH    R1,SM2
           BNL   P95SKIPL
           JOLERR 111,'FLAG COLUMN IS WITHIN SOURCE MARGINS RANGE'
           B   P95SKIPL        SKIPLEFT
FLAGERR    JOLERR 208,'FLAG OPTION ',#TKN,' NOT NUMERIC'
P95SKIPL   GETTKN TKNCURR
           CLI TKN,C')'
           BNE *+8
           STH R1,TKNCURR
           B   P95SKIP2
* FIX UP PRINT OPTIONS
LC        EQU *
           GETTKN (R1)          SEE WHATS AFTER THE KEYWORD
           CLI TKN,C'='
           BNE USELC
           GETTKN (R1)          GET THE SYMBOL AFTER THE = SIGN
USELC      STH R1,TKNCURR       SAVE R1
           TNUM #TKN
           LTR R15,R15     DID NUMBER CONVERT ?
         BNZ   LCERR                                              75128
         STH   R1,MAXLINE                                         75128
SKIPLC     LH R1,TKNCURR        RESET R1
           B  P95SKIP2
LCERR    JOLERR 104,'LINECOUNT OPTION NOT NUMERIC :- ',#TKN       75128
      B  P95SKIP2                                                 75128
          SPACE 1                                                 74256
SVC       GETTKN (R1)          GET SYMBOL AFTER 'SVC'             74256
          CLI TKN,C'='                                            74256
          BNE USESVC                                              74256
          GETTKN (R1)          SKIP '='                           74256
USESVC    STH  R1,TKNCURR                                         74256
          TNUM #TKN            CONVERT SVC NUMBER TO NUMERIC      74256
          LTR  R15,R15         DID IT ?                           74256
         BNZ   SVCERR                                             75128
         STC   R1,SUPRVSR+1                                       75128
SKIPSVC   LH   R1,TKNCURR                                         74256
          B    P95SKIP2                                           74256
SVCERR   JOLERR 305,'SVC NUMBER OPTION ''',#TKN,''' NOT NUMERIC'  75128
           MVI SUPRVSR+1,0     CLEAR SVC NUMBER
          B    SKIPSVC                                            74256
          SPACE
          SPACE 1                                                 J52
TRACE     GETTKN (R1)          GET SYMBOL AFTER 'TRACE'           J52
          STH  R1,TKNCURR                                         J52
          CLC  =C'ON ',TKN     TRACE ON?                          J52
          BNE  TRCOFF          TRY OFF THEN.                      J52
          MVI  TRACEIND,X'01'    IND ON                           J52
          B    P95SKIP2                                           J52
TRCOFF    CLC  =C'OFF ',TKN     TRACE OFF ?                       J52
          BNE  TRCERR          GIVE ERROR                         J52
          MVI  TRACEIND,X'00'    IND OFF                          J52
          B    P95SKIP2                                           J52
TRCERR   JOLERR 315,'TRACE SHOULD BE ''ON'' OR ''OFF'''           J52
          B    P95SKIP2                                           J52
          SPACE
           B  SKIPLC
DUMPOPT    GETTKN (R1)         GET SYMBOL AFTER DUMPCARD          75311
           CLI TKN,C'='        DUMPCARD =                         75311
           BNE DUMPNEQU        NO                                 75311
           GETTKN (R1)         SKIP =                             75311
DUMPNEQU   CLI TKN,C'('        '('                                75311
           BNE DUMPNBRA        NO                                 75311
           GETTKN (R1)         YES, SKIP IT                       75311
DUMPNBRA   STH R1,TKNCURR                                         75311
           BAL R14,DROPQUOT    DROP QUOTES FROM '//SYSABEND' ETC  75311
           CLC =H'81',#TKN      > 80 CHARS ?
           BH  USEDUMP         NOPE
           JOLERR 309,'DUMPCARD OPTION > 80 CHARS:- IGNORED'
           B   P95SKIP2        IGNORE IT
USEDUMP    MVC DUMPCARD(82),#TKN MVC NEW DUMPCARD IN              75311
           B   P95SKIP2                                           75311
         SPACE 2
CARDIN01 EQU   *                                              NEW 76200
CARDIN02 EQU   *                                              NEW 76200
CARDIN03 EQU   *                                              NEW 76200
         MVC   DBL,TKN        SAVE CARD1,2 OR 3               NEW 76200
         GETTKN (R1)          GET NEXT TOKEN                  NEW 76200
         CLI   TKN,C'='       '='                             NEW 76200
         BNE   CARDNEQU       NOT EQUAL, SO STORE TOKEN       NEW 76200
         GETTKN (R1)          SKIP '='                        NEW 76200
CARDNEQU CLI   TKN,C'('                                       NEW 76200
         BNE   CARDNBRA       NO BRACKETS EITHER              NEW 76200
         GETTKN (R1)          SKIP BRACKET                    NEW 76200
CARDNBRA STH   R1,TKNCURR     SAVE CURRENT POINTER            NEW 76200
         BAL   R14,DROPQUOT   DROP QUOTES                     NEW 76200
         CLC   =H'81',#TKN    IS LENGTH TOO LONG ?            NEW 76200
         BH    STORECD        NOPE, SO STORE CARD.            NEW 76200
         JOLERR 312,'''',DBL,'''CANNOT BE STORED:-TOO LONG'   NEW 76200
         B     P95SKIP2       IGNORE IT                       NEW 76200
         SPACE 1                                              NEW 76200
STORECD  CLI   DBL+4,C'1'     CARD1 ?                         NEW 76200
         BNE   P95CD2         NOPE                            NEW 76200
         MVC   LGENCRD1(82),#TKN STORE CARD1                  NEW 76200
         B     P95SKIP2                                       NEW 76200
P95CD2   CLI   DBL+4,C'2'     CARD2 ?                         NEW 76200
         BNE   P95CD3              ETC                        NEW 76200
         MVC   LGENCRD2(82),#TKN   ETC                        NEW 76200
         B     P95SKIP2            ETC                        NEW 76200
P95CD3   MVC   LGENCRD3(82),#TKN   ETC                        NEW 76200
         B     P95SKIP3            ETC                        NEW 76200
         SPACE 3                                              NEW 76200
LET        OI THISSYS,X'08'     SET LET OPTION UP                 75311
           B  P95SKIP2
NPC      OI    PARMPRNT,X'04' PUT COMMENTS IN JCL                 75128
         B     P95SKIP2                                           75128
PC       NI    PARMPRNT,X'FF'-X'04'  DO   PUT COMMENTS IN JCL     75128
         B     P95SKIP2                                           75128
PI         OI  PARMPRNT,X'80'   PRINT INCLUDES
           B   P95SKIP2
PM         OI PARMPRNT,X'40'   PRINT MACROS AS THEY ARE WORKING
           B   P95SKIP2
PE         OI PARMPRNT,X'20'   PRINT MACROS EXPANSIONS IN SECOND
*                              LISTING
           B   P95SKIP2
PJ         OI PARMPRNT,X'10'   PRINT GENERATED JCL
           B   P95SKIP2
PO         OI PARMPRNT,X'01'   PRINT OPTIONS SWITCHED ON
           B   P95SKIP2
NPI        NI PARMPRNT,X'FF'-X'80' DON'T PRINT INCLUDES
           B   P95SKIP2
NPM        NI PARMPRNT,X'FF'-X'40' DON'T PRINT MACROS
           B   P95SKIP2
NPE        NI PARMPRNT,X'FF'-X'20' DON'T PRINT EXPANSIONS
           B   P95SKIP2
NPJ        NI PARMPRNT,X'FF'-X'10' DON'T PRINT JCL
           B   P95SKIP2
NPO        NI PARMPRNT,X'FF'-X'01' DON'T PRINT OPTIONS
           B   P95SKIP2
         SPACE 2                                              NEW 76200
MODE     GETTKN (R1)          GET NEXT TOKEN                  NEW 76200
         CLI   TKN,C'='       '='                             NEW 76200
         BNE   MODEBRAC                                       NEW 76200
         GETTKN (R1)                                          NEW 76200
MODEBRAC CLI   TKN,C'('                                       NEW 76200
         BNE   MODE1                                          NEW 76200
         GETTKN (R1)                                          NEW 76200
MODE1    STH   R1,TKNCURR                                     NEW 76200
         CLI   TKN,C','       NO MODE 1 ?                     NEW 76200
         BE    NMODE1                                         NEW 76200
         SPACE 1                                              NEW 76200
         LM    R5,R7,MODE1OPT                                 NEW 76200
         CLC   TKN(8),0(R5)                                   NEW 76200
         BE    MODE1P                                         NEW 76200
         BXLE  R5,R6,*-10                                     NEW 76200
         JOLERR 313,'MODE(1) OPTION:-''',#TKN,''' UNKNOWN'    NEW 76200
         B     P95EMD1                                        NEW 76200
         SPACE 2                                              NEW 76200
MODE1P   MVC   DBL+2(2),8(R5) SHIFT S CON                     NEW 76200
         MVC   DBL(2),P95LAR15                                NEW 76200
         EX    R0,DBL                                         NEW 76200
         BR    R15                                            NEW 76200
         SPACE 1                                              NEW 76200
NMODE1   GETTKN (1)           SKIP ','                        NEW 76200
         STH   R1,TKNCURR                                     NEW 76200
         SPACE 1                                              NEW 76200
P95EMD1  GETTKN TKNCURR       GET NEXT TOKEN                  NEW 76200
         CLI   TKN,C','       ANY OUTPUT OPTION               NEW 76200
         BNE   P95SKIP2       NOPE, OUT                       NEW 76200
         GETTKN (R1)          GET OUTPUT MODE                 NEW 76200
         STH   R1,TKNCURR                                     NEW 76200
         LM    R5,R7,MODE2OPT                                 NEW 76200
         CLC   TKN(8),0(R5)                                   NEW 76200
         BE    MODE2P                                         NEW 76200
         BXLE  R5,R6,*-10                                     NEW 76200
         JOLERR 314,'MODE(2) OPTION:-''',#TKN,''' UNKNOWN'    NEW 76200
         B     P95SKIP2                                       NEW 76200
MODE2P   MVC   DBL+2(2),8(R5)                                 NEW 76200
         MVC   DBL(2),P95LAR15                                NEW 76200
         EX    R0,DBL                                         NEW 76200
         BR    R15                                            NEW 76200
MODE1OPT DC    A(*+12,10,MODE1E-10)                           NEW 76200
         DC    CL8'TSOIN',S(TSOINOP)                          NEW 76200
         DC    CL8'JOLIN',S(JOLINOP)                          NEW 76200
MODE1E   EQU   *                                              NEW 76200
         SPACE 2                                              NEW 76200
MODE2OPT DC    A(*+12,10,MODE2E-10)                           NEW 76200
         DC    CL8'IMMEDIATE',S(M2IMMED)                      NEW 76200
         DC    CL8'DYNAMIC  ',S(M2DYNAM)                      NEW 76200
         DC    CL8'IMM      ',S(M2IMMED)                      NEW 76200
         DC    CL8'DYN      ',S(M2DYNAM)                      NEW 76200
         DC    CL8'ONESTEP  ',S(M2ONEST)                      NEW 76200
         DC    CL8'MONITOR  ',S(M2MON)                        NEW 76200
         DC    CL8'MON      ',S(M2MON)                        NEW 76200
         DC    CL8'REALJCL  ',S(M2REAL)                       NEW 76200
         DC    CL8'REAL     ',S(M2REAL)                       NEW 76200
         DC    CL8'PROC     ',S(M2PROC)                       NEW 76200
MODE2E   EQU   *                                              NEW 76200
TSOINOP  OI    MODEIN,TSOINOPT                                NEW 76200
         B     P95EMD1                                        NEW 76200
JOLINOP  NI    MODEIN,X'FF'-TSOINOPT                          NEW 76200
         B     P95EMD1                                        NEW 76200
M2IMMED  OI    MODEOUT,IMMED                                  NEW 76200
OFFREAL  NI    MODEOUT,255-(MONITOR+REALJCL)                  NEW 86100
         B     P95SKIP2                                       NEW 76200
M2DYNAM  OI    MODEOUT,DYNAM                                  NEW 76200
         B     OFFREAL                                        NEW 86100
M2ONEST  OI    MODEOUT,ONESTEP                                NEW 76200
         B     OFFREAL                                        NEW 86100
M2MON    OI    MODEOUT,MONITOR                                NEW 76200
         B     OFFDYN                                         NEW 86100
M2REAL   OI    MODEOUT,REALJCL                                NEW 76200
OFFDYN   NI    MODEOUT,255-(DYNAM+IMMED+REALJCL)              NEW 86100
         B     P95SKIP2                                       NEW 76200
M2PROC   OI    MODEOUT,PROCJCL                                NEW 76200
         B     P95SKIP2                                       NEW 76200
PRODNTIO OI    THISSYS,PRODOPT                                NEW 76200
         B     P95SKIP2                                       NEW 76200
         SPACE 2
VS1      EQU   *                                                  75128
VS2      EQU   *                                                  75128
OS4      EQU   *                                                  80290
VM       EQU   *                                                  75128
MFT      EQU   *                                                  75128
MVT      EQU   *                                                  75128
DOS      EQU   *                                                  75128
X8       EQU   *                                                  75128
         MVC   DEFSYS,TKN                                         75128
           B   M2DYNAM        SET UP DYNAMIC (NO JCL)
HASPOPT  MVI   SPOOL,C'H'                                         75128
           B   P95SKIP2
NOHASP   MVI   SPOOL,C' '                                         75128
           B   P95SKIP2                                           74256
ASP      MVI   SPOOL,C'A'                                         75128
           B   P95SKIP2
NOCAT      OI  PARMSRCH,1      TURN ON NOCAT INDICATOR            75311
           B   P95SKIP2                                           74256
CAT        NI  PARMSRCH,255-1  TURN ON   CAT INDICATOR            80290
           B   P95SKIP2                                           80290
FS       MVI   IFFULLSC,C'Y'   TURN ON FULLSCREEN INDICATOR       80290
           B   P95SKIP2                                           80290
NOFS     MVI   IFFULLSC,C' '   TURN OFF FULLSCREEN INDICATOR      80290
           B   P95SKIP2                                           80290
NOCATGDG OI    PARMSRCH,B'00000100' SET NOCATGDG OPTION
         B     P95SKIP2
NOSRDR      NI  THISSYS,X'FF'-X'02'             OFF S SRDR INDIC  75049
            B   P95SKIP2
SRDR        OI  THISSYS,X'02'      SET START RDR INDIC            75049
            B   P95SKIP2                                          75049
JOLOPT     B    P95SKIP2                                          75128
TEST     OI    PARMPRNT,X'08' TURN ON TEST                        75128
         B     P95SKIP2                                           75128
NOTEST   NI    PARMPRNT,X'FF'-X'80' TURN OFF TEST                 75128
         B     P95SKIP2                                           75128
JES1     EQU   *                                                  75128
JES2     EQU   *                                                  75128
JES3     EQU   *                                                  75128
         MVC   SPOOL,TKN+3    SHIFT IN LAST SO THIS ENDS UP AS    75128
*                             '1' ETC                             75128
         B     P95SKIP2                                           75128
FLAG0      MVI PFLAG,0         TURN OFF ALL PRINT ERROR OPTIONS   75311
           B   P95SKIP2        -> NEXT OPTION                     75311
FLAG1      MVI PFLAG,B'00000001' 1                                75311
           B   P95SKIP2                                           75311
FLAG2      MVI PFLAG,B'00000010' 2                                75311
           B   P95SKIP2                                           75311
FLAG3      MVI PFLAG,B'00000011' 3                                75311
           B   P95SKIP2                                           75311
FLAG4      MVI PFLAG,B'00000100' 4                                75311
           B   P95SKIP2                                           75311
FLAG5      MVI PFLAG,B'00000101' 5                                75311
           B   P95SKIP2                                           75311
NFLAG0     EQU *                                                  75311
NFLAG1     EQU *                                                  75311
NFLAG2     EQU *                                                  75311
NFLAG3     EQU *                                                  75311
NFLAG4     EQU *                                                  75311
NFLAG5     MVI PFLAG,0                                            75311
           B   P95SKIP2                                           75311
NPRINT     EQU *                                                  75311
NP         EQU *                                                  75311
           OI  PARMPRNT,B'00000010'                               75311
           B   P95SKIP2                                           75311
PRINTO     NI  PARMPRNT,X'FF'-B'00000010'  TURN ON PRINT
           B   P95SKIP2
P95RETN0 $CALL UJP94INI     RESET %SYSTEM ETC AGAIN IF NECESSARY  87080
         JOLRETN RC=0                                             75128
         PRINT DATA                                               75128
OPTIONS    DC  A(*+12)
           DC  A(10)
           DC  A(ENDOPT-10)
         DC    CL8'DUMPCARD',S(DUMPOPT)                           75311
         DC    CL8'CARD1      ',S(CARDIN01)                   NEW 76200
         DC    CL8'CARD2      ',S(CARDIN02)                   NEW 76200
         DC    CL8'CARD3      ',S(CARDIN03)                   NEW 76200
         DC    CL8'JES1    ',S(JES1)                              75128
         DC    CL8'JES2    ',S(JES2)                              75128
         DC    CL8'JES3    ',S(JES3)                              75128
         DC    CL8'LINECNT',S(LC)              ADJUST LINECNT
         DC    CL8'JOLOPT ',S(JOLOPT)    DUMMY *******
         DC    CL8'LC',S(LC)                   ADJUST LINECNT
         DC    CL8'LET',S(LET)                LET OPTION
         DC    CL8'PI',S(PI)                   PRINT INCLUDES
         DC    CL8'PINCL',S(PI)                PRINT INCLUDES
         DC    CL8'PM',S(PM)                   PRINT MACRO STATEMENTS
         DC    CL8'PMACRO',S(PM)               PRINT MACRO STATEMENTS
         DC    CL8'PMAC',S(PM)                 PRINT MACRO STATEMENTS
         DC    CL8'PE',S(PE)                   PRINT EXPANSIONS IN 2ND
         DC    CL8'PEXPAND',S(PE)              PRINT EXPANSIONS IN 2ND
         DC    CL8'PEXP',S(PE) E)              PRINT EXPANSIONS IN 2ND
         DC    CL8'DOS',S(DOS)                                    84290
         DC    CL8'TSO      ',S(M2IMMED)                      NEW 76200
         DC    CL8'TSS      ',S(M2IMMED)                      NEW 86100
         DC    CL8'AIF      ',S(M2IMMED)                      NEW 86100
         DC    CL8'DYNAMIC  ',S(M2DYNAM)                      NEW 76200
         DC    CL8'DYN      ',S(M2DYNAM)                      NEW 76200
         DC    CL8'JCODE    ',S(M2DYNAM)                      NEW 86100
         DC    CL8'MFT',S(MFT)
         DC    CL8'MVT',S(MVT)
         DC    CL8'VM',S(VM)                                      74256
         DC    CL8'VS1',S(VS1)
         DC    CL8'VS2',S(VS2)
         DC    CL8'OS4',S(OS4)                                    80290
         DC    CL8'X8',S(X8)                                      84290
         DC    CL8'HASP',S(HASPOPT)
         DC    CL8'ASP',S(ASP)
         DC    CL8'NH',S(NOHASP)               NO HASP            74256
         DC    CL8'NA',S(NOHASP)               NO ASP             74256
         DC    CL8'NOHASP',S(NOHASP)           NO HASP            74256
         DC    CL8'NOASP',S(NOHASP)            NO ASP             74256
         DC    CL8'NOCAT',S(NOCAT)             NO CATALOGUE SRCH  74256
         DC    CL8'CAT  ',S(CAT)                  CATALOGUE SRCH  80290
         DC    CL8'FS   ',S(FS)     USE FULL SCREEN               80290
         DC    CL8'NOFS ',S(NOFS)   CANCEL FULL SCREEN            80290
         DC    CL8'NFS  ',S(NOFS)   CANCEL FULL SCREEN            80290
         DC    CL8'NCAT       ',S(NOCAT)                      NEW 76200
         DC    CL8'NOCATGDG',S(NOCATGDG) NO CATLG SEARCH (GDGS)   75311
         DC    CL8'NCATGDG    ',S(NOCATGDG)                   NEW 76200
         DC    CL8'PJ',S(PJ)                   PRINT JCL GENERATED
         DC    CL8'PJCL',S(PJ)                 PRINT JCL GENERATED
         DC    CL8'PO',S(PO)                   PRINT OPTIONS
         DC    CL8'POPT',S(PO)                 PRINT OPTIONS
         DC    CL8'POPTION',S(PO)              PRINT OPTIONS
         DC    CL8'NPI',S(NPI)                 DON'T PRINT INCLUDES
         DC    CL8'NOPI       ',S(NPI)                        NEW 76200
         DC    CL8'NPINCL',S(NPI)              DON'T PRINT INCLUDES
         DC    CL8'NOPINCL    ',S(NPI)                        NEW 76200
         DC    CL8'NPM',S(NPM)                   DON'T PRINT MACROS
         DC    CL8'NOPM       ',S(NPM)                        NEW 76200
         DC    CL8'NPMACRO    ',S(NPM)             DON'T PRINT MACROS
         DC    CL8'NOPMACRO   ',S(NPM)                        NEW 76200
         DC    CL8'NPE        ',S(NPE)          DON'T PRINT EXPANSIONS
         DC    CL8'NOPE       ',S(NPE)                        NEW 76200
         DC    CL8'NPEXP      ',S(NPE)          DON'T PRINT EXPANSIONS
         DC    CL8'NOPEXP     ',S(NPE)                        NEW 76200
         DC    CL8'NPEXPAND   ',S(NPE)          DON'T PRINT EXPANSIONS
         DC    CL8'NOPEXPAND  ',S(NPE)                        NEW 76200
         DC    CL8'NPJ',S(NPJ)                 DON'T PRINT JCL
         DC    CL8'NOPJ       ',S(NPJ)                        NEW 76200
         DC    CL8'NPJCL',S(NPJ)               DON'T PRINT JCL
         DC    CL8'NOPJCL     ',S(NPJ)                        NEW 76200
         DC    CL8'NPO',S(NPO)                 DON'T PRINT OPTIONS
         DC    CL8'NOPO       ',S(NPO)                        NEW 76200
         DC    CL8'NPOPT',S(NPO)               DON'T PRINT OPTIONS
         DC    CL8'NOPOPT     ',S(NPO)                        NEW 76200
         DC    CL8'NOSRDR',S(NOSRDR)           DON'T START A READER
         DC    CL8'NSRDR      ',S(NOSRDR)                     NEW 76200
         DC    CL8'PROD       ',S(PRODNTIO)                   NEW 76200
         DC    CL8'SRDR',S(SRDR)               DO START A READER
         DC    CL8'SVC',S(SVC)                 SUPRVSR SVC NO     74256
         DC    CL8'NOTEST  ',S(NOTEST)                            75128
         DC    CL8'SM      ',S(SM)                                75311
         DC    CL8'FLAG0   ',S(FLAG0)                             75311
         DC    CL8'NOFLAG0 ',S(NFLAG0)                        NEW 76200
         DC    CL8'FLAG1   ',S(FLAG1)                             75311
         DC    CL8'NOFLAG1 ',S(NFLAG1)                        NEW 76200
         DC    CL8'FLAG2   ',S(FLAG2)                             75311
         DC    CL8'NOFLAG2 ',S(NFLAG2)                        NEW 76200
         DC    CL8'FLAG3   ',S(FLAG3)                             75311
         DC    CL8'NOFLAG3 ',S(NFLAG3)                        NEW 76200
         DC    CL8'FLAG4   ',S(FLAG4)                             75311
         DC    CL8'NOFLAG4 ',S(NFLAG4)                        NEW 76200
         DC    CL8'FLAG5   ',S(FLAG5)                             75311
         DC    CL8'NOFLAG5 ',S(NFLAG5)                        NEW 76200
         DC    CL8'NFLAG0  ',S(NFLAG0)                            75311
         DC    CL8'NFLAG1  ',S(NFLAG1)                            75311
         DC    CL8'NFLAG2  ',S(NFLAG2)                            75311
         DC    CL8'NFLAG3  ',S(NFLAG3)                            75311
         DC    CL8'NFLAG4  ',S(NFLAG4)                            75311
         DC    CL8'NFLAG5  ',S(NFLAG5)                            75311
         DC    CL8'FLAGM   ',S(FLAG)                              75311
         DC    CL8'NPRINT  ',S(NPRINT)                            75311
         DC    CL8'NOPRINT ',S(NPRINT)                        NEW 76200
         DC    CL8'PRINT   ',S(PRINTO)
         DC    CL8'P       ',S(PRINTO)
         DC    CL8'NP      ',S(NP)                                75311
         DC    CL8'NOP     ',S(NPRINT)                        NEW 76200
         DC    CL8'NCONSM  ',S(TEST)                          NEW 76200
         DC    CL8'NOCONSM ',S(TEST)                          NEW 76200
         DC    CL8'MODE    ',S(MODE)                             76200
         DC    CL8'REALJCL ',S(M2REAL)                            76200
         DC    CL8'RJCL    ',S(M2REAL)                            76200
         DC    CL8'MONITOR ',S(M2MON)                             76200
         DC    CL8'MON     ',S(M2MON)                             76200
         DC    CL8'TEST    ',S(TEST)                              75128
         DC    CL8'TRACE   ',S(TRACE)                             J52
         DC    CL8'NPCOMM  ',S(NPC)                               75128
         DC    CL8'NPC     ',S(NPC)                               75128
         DC    CL8'PC      ',S(PC)                                75311
ENDOPT     EQU  *
P95SYM     EQU *
           GETTKN (1)          GET SYMBOL AFTER '/'
           STH  R1,TKNCURR
           CLI TKN,C' '        IS IT '',IE NO SYMBOLIC NAMES ?
         BE    P95RETN0                                           75128
P95SYM2    EQU *
           CLI TKN,C')'        IGNORE IT
           BE  P95SYM
           MVC ICOMMAND,=CL8'MACRO' DO THIS SO ASSIGNMENT ROUTINE
*                              WON'T TRY TO DO ANYTHING FANCY WHEN
*                              STORING THE VALUES OF THE SYMBOLICS
           CLI TKNTYPE,2       <IDENTIFIER> ?
           BE  P95IDENT
           CLI TKN,C','
           BE  P95SKIP3
         JOLERR 302,'INVALID NAME ''',#TKN,''' IGNORED IN * JOL OR PARA*
               METER FIELD OF JOL COMPILER'
P95SKIP3   EQU  *
           LH  R1,TKNCURR
           B   P95SYM
           SPACE
P95IDENT   EQU *
         MVC   SYMBOLIC,TKN   SHIFT IN SYMBOLIC NAME              75128
           XC  #WORK,#WORK     NULLIFY ITEM TO BE STORED TEMPORARILY
           GETTKN (R1)
           STH R1,TKNCURR
           CLI TKN,C'='
           BE  P95GOTVL        GOT A VALUE MAYBE,GO AND GET NEXT,
*                              IF IT IS A COMMA,IGNORE IT.
           SPACE
* NOW THE VALUE HERE MUST BE A COMMA,OR IT IS AN ERROR.
           CLI TKN,C','
           BE  P95NOV1
         JOLERR 303,'INVALID VALUE ''',#TKN,                           *
               ''' IGNORED IN * JOL OR PARAMETER FIELD OF JOL COMPILER'
           LH  R1,TKNCURR
           B   P95SYM
P95GOTVL   EQU *
           GETTKN (1)          GET VALUE AFTER '='
           STH R1,TKNCURR
           CLI TKN,C','
           BE  P95NOV1         NO VALUE,STORE NULL
           MVC #WORK(L'TKN+2),#TKN
P95NOV1   $CALL UJP85ASN       STORE NAME IN TABLE
           B   P95SYM          GO GET NEXT
P95LAR15 LA    R15,0          **** FUNNY EXECUTE ***** SEE TABLE  75128
          LTORG
          DC S(*,*,*,*)
          DC S(*,*,*,*)
          DC S(*,*,*,*)
          DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
       DC S(*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*)
         DC    S(*,*,*,*,*,*,*,*,*,*,*,*)
           END
