UJD14FRE TITLE 'FREE (MARK NOT IN USE) DATASETS'        '
* JOL COPYRIGHT CCS-JOL PTY LTD 1988
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-E001.
         SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
* LAST SOURCE UPDATE
*   CHANGES:-
*     1.
         COPY  JOLGLOBL
         $UJEPARM
&TYPE    SETC  'SCHED'                                           88036
         PRINT ON,DATA
UJECOMM  DSECT
         USING UJECOMM,R3,R5
         COPY  UJECOMM
         EJECT                                                    J40B
D14WORK  DSECT                                                    J40B
         DS    18F                                                J40B
#D14DDNM DC    H'8'
TEMPDDN2 DC    CL8' '
D14DEALL DC    2Y(0,0),C' '
D14DETME DC    CL8' '                  TIME GOES HERE
         DC    C' '
D14DSID  DC    CL8' '
         DC    C'  '
D14DDNAM DC    CL8' '
         DC    C'    '
D14ACTN  DC    CL8' '                   PASSED ETC
D14PVOLS DC    CL30' '         VOLUMES                            J40B
D14WORKS EQU   *-D14WORK                                          J40B
         JOLSAVE CSECT=UJD14FRE,SIZE=D14WORKS
         USING D14WORK,R13                                        J40B
         CLC   =C'$$VR',DDDDNAME     Q... VOLREF ?                J40B
         BE    JRETN                 A... YES - IGNORE IT         J40B
         MVC   #D14DDNM,=H'8'                                     J40B
         MVC   D14DEALL(D14ALLSZ),DEALLMSG                        J40B
         MVC   D14ACTN,=CL8'** ERROR'
*        MVC   D14PVOLS,DDVOLUME                                  J40B
         CALL  GETTIME
         CLC   =C'STEPLIB ',DDDDNAME Q... STEPLIB ?
         BNE   D14NSTEP              A... NO
         MVI   DDDDNAME+7,C'$' MAKE STEPLIB$
D14NSTEP MVC   D14DETME,CURRTIME
         MVC   TEMPDDN2,DDDDNAME
         MVC   D14DDNAM,DDDDNAME
         MVC   D14DSID,DDDSID
         CLI   DDTYPE,DDPUNCH   Q... SYSIN, SYSOUT OF SOME KIND?
         BNH   D14FREE          A... YES, FREE IT NOW
         CLC   =C'JOBLIB  ',DDDDNAME Q... JOBLIB?
         BE    D14JOBL               A... YES
*        TM    DDUNITYP,X'80'  IF IT'S A TAPE, FREE FILE ALWAYS
*        BO    D14TDISP
* PROPOGATE THE TAPE VOLUME NOW, AS IT MAY BE USED LATER
*  IN VOLUME REFERENCES OR CATALOG STATEMENTS.
*        CALL  UJD30VOL       PROPOGATE VOLS DOWN JOL 'JOBQ'
*        B     D14FREE                    FREE IT NOW
*        CLI   DDDISP+1,C'L'  LAST USE?
D14TDISP CLI   DDDISP+1,C'P'  LAST USE?
         BE    D14DISUS        NO, JUST MAKE MARKS
D14FREE  DS    0H
         CLI   TEMPDDN2,C' '
         BE    D14DDBL1
         DBGMSG 0000,'FREEING ',DDDDNAME                           J40B
         DYNPARM DDNAME=(8,#D14DDNM),VRB=UN,UNALLOC=YES
         B     D14TRC1
D14DDBL1 DS    0H
         DYNPARM DSN=(44,#DSN),VRB=UN,UNALLOC=YES
         DBGMSG 0001,'FREEING ',DDDSNAME                           J40B
D14TRC1  LTR   R1,R15
         BZ    D14FREED
         CVD   R15,DBL
         MVC   TASKSSI(4),=X'40202120'
         ED    TASKSSI,DBL+6
         JOLERR 102,'DSID ',DDDSID,''',DDNAME ',                       .
               DDDDNAME,' NOT UNALLOCATED DUE TO RETURN CODE ',        .
               TASKSSI
         B     D14RETN
D14FREED DS    0H
         MVC   D14ACTN,=CL8'FREED'
D14RETN  DS    0H
         PRPUT D14DEALL                                          88036
         B     JRETN0
D14DISUS DS    0H
*    NOW LET'S PUT IT IN OUR OWN PASSED DATASET QUEUE            88036
         CALL  UJD24PDQ                                          88036
         CLI   TEMPDDN2,C' '
         BE    D14DDBL2
         DBGMSG 0003,'FREEING DD ',DDDDNAME                        J40B
         DYNPARM  DDNAME=(8,#D14DDNM),VRB=UN,UNALLOC=YES VE.IN.USE
         B     D14JOBL
D14DDBL2 DS    0H
         DBGMSG 0004,'FREEING DS ',DDDSNAME                        J40B
         DYNPARM DSN=(44,#DSN),VRB=UN,UNALLOC=REMOVE.IN.USE
D14JOBL  DS    0H                    LEAVE JOBLIB, ISSUE 'PASSED' 87150
         MVC   D14ACTN,=CL8'PASSED'
         PRPUT D14DEALL                                          88036
         B     JRETN0
         AGO   .NOTFRE
         B     FIXCONCAT
         CLC   =CL8' ',DDDDNAME      Q... CONCATENATED FILE?
         BE    D14NXFCT              A... YES
         MVC   SAVEDD,DDDDNAME       COPY THE DDNAME
         SPACE
D14FFCT  DS    0H    FOUND AN FCT TO "FIX"
         AP    DDCTR,=P'1'          ADD TO MY COUNTER
         $TELL (R4)
         MODESET  KEY=ZERO
*        NI    FCTACSN,X'BF'        MAKE MY MARK FOR "NOT IN USE"
**CVC    MVC   FCTACSN(4),=C'$JOL'  MAKE MY MARK FOR "NOT IN USE"
**CVC    UNPK  FCTACSN+4(4),DDCTR
         SPACE
* WHILE I AM HERE, LET'S HAVE A LOOK AT SOME OTHER BITS
         SPACE
         LR    R2,R4                 SAVE THIS ADDRESS
         TM    FCTFLG6,FCTF6CF       Q... CONCATENATED?
         BZ    D14NOCAT              A... NO
         NI    FCTFLG6,X'FF'-FCTF6CF TURN THAT OFF
D14NXTCF DS    0H
         L     R1,FCTCFNO            ADDRESS NEXT IN THIS CHAIN
         XC    FCTCFNO(4),FCTCFNO    CLEAR THIS FIELD
         LA    R4,0(,R1)             CLEAR THE HIGH ORDER BYTE
         LTR   R4,R4                 Q... ANYTHING THERE?
         BZ    D14NOCAT              A... NO
         CLC   FCTACSN,DDDDNAME      CHECK THIS, JUST IN CASE
         BNE   D14NOCAT              UM, ER, I'LL SEE YOU LATER
         NI    FCTFLG6,X'FF'-FCTF6CF TURN OFF CONCAT FLAG
         B     D14NXTCF              LET'S PLAY WITH THE NEXT ONE TOO
D14NOCAT DS    0H
         LR    R4,R2                 ADDRESS OF THE ORIGINAL
         SPACE 2
*  WELL, THAT SHOULD BE ALL
         SPACE 3
         JOLERR 005,                                                   .
               'DSID ',DDDSID,                                         .
               ' DDNAME ',DDDDNAME,                                    .
               ' CALLED IN FREE'
         B     JRETN0
.NOTFRE  ANOP
* THE FOLLOWING LINES ARE USED TO PRINT OUT DE-ALLOCATION MESSAGES
*     FOR THE DYNAMIC ALLOCATION VERSION OF JOL.
DEALLMSG DC    2Y(D14END1-*,0),C' '
         DC    CL8' '                  TIME GOES HERE
         DC    C' '
         DC    CL8' '
         DC    C'  '
         DC    CL8' '
         DC    C'    '
         DC    CL8' '                   PASSED ETC
         DC    CL30' '         VOLUMES                            J40B
D14END1  DS    0C
D14ALLSZ EQU   *-DEALLMSG                                         J40B
         SPACE
         LTORG
         END
