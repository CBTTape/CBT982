***  ENQUEUE OR ENQ JOL INSTRUCTION FOR ENQUING             ***********
*          ON DATA SETS OR ANY RESOURCE                               *
*                                                                     *
*    MODIFIED FROM US26ENQ BY MIKE STRAHAN, SHELL OIL, MELBOURNE.     *
*                                                                     *
*                                                                     *
*    FUNCTION:                                                        *
*                                                                     *
*         TO ISSUE AN ENQUEUE ON MAJOR AND MINOR NAMES AS             *
*         SPECIFIED BY THE USER.                                      *
*                                                                     *
*         THIS IS A COMPILER INSTRUCTION, NOT EXECUTION.              *
*                                                                     *
*         ALTHOUGH REALLY DESIGNED FOR SAVESYMS, IT IS APPLICABLE     *
*         TO MANY MANY FUNCTIONS.                                     *
*                                                                     *
*    LINKAGE:                                                         *
*                                                                     *
*         INVOKED BY ENQUE MAJOR-NAME,MINOR-NAME {OPTIONS}            *
*                                                                     *
*           WHERE OPTIONS ARE:-                                       *
*                                                                     *
*              WAIT Â¦ NOWAIT
*              ----                                                   *
*                                                                     *
*         ON ENTRY, REGISTERS CONTAIN STANDARD JOL REGISTER VALUES.   *
*                                                                     *
*         ON RETURN, %LASTCC IS SET TO :-                             *
*         REGISTER 15 CONTAINS A RETURN CODE :                        *
*                                                                     *
*         RC = 0   CONTROL OF RESOURCE HAS BEEN ASSIGNED.             *
*                                                                     *
*         RC = 4   APPLICABLE ONLY FOR BACKGROUND TASKS.              *
*                  CONTROL OF THE RESOURCE HAS BEEN ASSIGNED,         *
*                  BUT THE TASK WAITED FOR THE RESOURCE TO            *
*                  BECOME AVAILABLE.                                  *
*                  THE OPERATOR WAS INFORMED THAT THE TASK WAITED     *
*                  AND THAT PROCESSING SUBSEQUENTLY CONTINUED.        *
*                                                                     *
*         RC = 8   A PREVIOUS REQUEST FOR THE RESOURCE HAS BEEN       *
*                  MADE. THIS REQUEST IS IGNORED.                     *
*                                                                     *
*         RC = 12  APPLICABLE ONLY TO FOREGROUND TASKS.               *
*                  CONTROL OF THE RESOURCE WAS NOT ASSIGNED           *
*                  BECAUSE THE TASK WOULD HAVE WAITED FOR CONTROL     *
*                  OF THE RESOURCE.                                   *
*                  THE FOREGROUND USER WAS INFORMED THAT THE          *
*                  RESOURCE WAS NOT IMMEDIATELY AVAILABLE.            *
* NOTE:            IT IS UP TO THE MODULE INVOKING US26ENQ TO         *
* *****            DECIDE WHAT ACTION IS TO BE TAKEN.                 *
*                                                                     *
*                                                                     *
*                                                                     *
*    MODULE ATTRIBUTES:                                               *
*                                                                     *
*         RE-ENTERABLE.                                               *
*                                                                     *
***********************************************************************
UJP26ENQ TITLE 'ENQUE ON NAMES SPECIFIED BY USER'
         PRINT OFF
         COPY  JOLCOM
         PRINT ON
         SPACE
P26WORK  DSECT
SAVE     DS    18F                 SAVE AREA
WAITBIT  DS    X'0'                NOT 0 IF WE ARE TO WAIT
TSOIND   DS    X'0'                NOT 0 IF WE ARE UNDER TSO
QNAME    DS    CL8                 MAJOR NAME COPIED HERE
LNTHWRK  EQU   *-P26WORK           LENGTH OF WORK AREA
TCBTSFLG EQU   148
         SPACE 3
         JOLSAVE CSECT=UJP26ENQ,SIZE=LNTHWRK
         USING P26WORK,R13
         LR    R10,R13             SET TO POINT TO WORK AREA
         DROP  R13
         USING P26WORK,R10
         SPACE 1
         MVC   ICOMMAND,=CL8'MACRO'
         MVC   SYMBOLIC,=CL8'LASTCC'
         MVC   #WORK(3),=X'0001F0' SET UP RC=0
         $CALL UJP85ASN
* TEST IF WE ARE RUNNING UNDER TSO OR NOT
         AGO   .X
         IFOS  =X8,P26NOTX8                                       88020
         L     R15,16
         L     R15,0(R15)
         L     R15,4(R15)          GET TCB ADDRESS VIA THE CVT
         L     R15,12(R15)         GET TIOT ADDRESS FROM TCB
P26NOTX8 DS    0H                                                 88020
.X       ANOP
* SET UP DEFAULTS
         MVI   WAITBIT,X'1'
         MVI   TSOIND,X'0'         CLEAR TSO INDICATOR
* IS THIS A TSO TASK?
         CLI   IFTSO,C'T'                                         88020
         BNE   NOTSO                                              88020
         MVI   TSOIND,X'1'         SET TSO TASK FLAG ON
         MVI   WAITBIT,X'1'        DEFAULT WAIT IN BATCH
NOTSO    EQU   *
         SPACE 2
*    FIND THE RESOURCES WE WANT TO ENQ ON
         SPACE 1
         GETTKN  2                 GET THE MAJOR QNAME
         MVC   QNAME,TKN
         GETTKN  3                 GET THE SLASH (/)
         CLI   TKN,C'/'
         BE    P26SLASH
P26BAD   JOLERR 301,'ENQUEUE/DEQUEUE:- INVALID PARAMETERS'
         JOLERR 302,'MUST BE:- MAJOR-NAME/''MINOR-NAME'''
         MVC   #WORK(3),=X'0001F4' SET UP RC=4
         $CALL UJP85ASN
         JOLRETN RC=4
P26SLASH DS    0H
         GETTKN  4                 GET THE SLASH (/)
         BAL   R14,DROPQUOT
         MVC   #WORK(L'TKN),#TKN
         CLC   =C'   ',TKN
         BE    P26BAD
         GETTKN  5
         CLI   TKN,C' '
         BE    LEAVWAIT
         CLC   =C'NOWAIT',TKN
         BNE   TWAIT
         MVI   WAITBIT,X'0'
         B     LEAVWAIT
TWAIT    CLC   =C'WAIT',TKN
         BNE   WAITERR
         MVI   WAITBIT,X'1'
         B     LEAVWAIT
WAITERR  JOLERR 306,'WAIT OR NOWAIT EXPECTED, NOT ''',#TKN,''''
         MVI   WAITBIT,X'1'
LEAVWAIT DS    0H
         CLI   WAITBIT,X'0'
         BE    WAITRET
         CLI   TSOIND,X'0'
         BE    WAITRET
         B     WAITRET                                    ******
         JOLERR 307,'WAIT NULLIFIED UNDER TSO/TSS'
         MVI   WAITBIT,X'0'
WAITRET  DS    0H
*    DO THE ENQUEUE ON REQUIRED RESOURCES
         SPACE 1
         $CALL JOLENQ,(QNAME,#WORK,WAITBIT)
         LTR   R15,R15             ENQ'D OK?
         BZ    P26ENOK             YES, OUT WE GO
* HERE WE HAVE AN ERROR
         MVC   #WORK(4),=X'0002F1F6' SET UP RC=16
         $CALL UJP85ASN
         JOLRETN RC=16
P26ENOK  DS    0F
RETURN   JOLRETN
         SPACE 3
         LTORG
         END
