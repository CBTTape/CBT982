UJD30VOL TITLE 'PROPOGATE VOLUMES IN JOL JOBQ'                   '
* JOL COPYRIGHT CCS-JOL PTY LTD 1988
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-E001.
         SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
* LAST SOURCE UPDATE
*   CHANGES:-
*     1.
         COPY  JOLGLOBL
         $UJEPARM
&TYPE    SETC  'SCHED'                                           88036
         PRINT ON,DATA
UJECOMM  DSECT
         USING UJECOMM,R3,R5
         COPY  UJECOMM
E30WORK  DSECT                                                    J40B
         DS    18F                                                J40B
E30WORKS EQU   *-E30WORK                                          J40B
         JOLSAVE CSECT=UJD30VOL,                                       .
               SIZE=E30WORKS,                                     J40B .
               BASE=R11
*        JOLRETN RC=4         GET OUT FOR NOW
* THIS ROUTINE IS CALLED TO FIND THE VOLUME THAT A DATA SET
*  WAS ALLOCATED ON, AND THEN PROPOGATES THIS INFORMATION
*  DOWN THE JOL QUEUES. IT ALSO FIXES UP VOLREFS WHEN REQD.
*
* IT IS CALLED LINK  :-
*
*        CALL  UJD30VOL       PROPOGATE VOLS DOWN JOL 'JOBQ'
*
         CLI   DDTYPE,DDPUNCH   Q... SYSIN, SYSOUT OF SOME KIND?
         BNH   E30RETN4       DON'T PROPOGATE SYSOUT VOLS
         CLC   =C'*DUMMY ',DDDSNAME
         BE    E30RETN4       DON'T PROPOGATE DUMMY VOLS
         CLC   =C'NULLFILE ',DDDSNAME
         BE    E30RETN4       DON'T PROPOGATE DUMMY VOLS
*  AT THIS POINT, UJD49VOL HAS OBTAINED THE VOLUMES FOR THE DSNAME.
*  NOW, COPY THE VOLUME LIST AS REQUIRED.
         SPACE 2
         L     R14,AJFCB                                         88036
         USING INFMJFCB,R14                                      88036
         UJE22UPD DDDSNAME,JFCBVOLS,DDDDNAME FIX ALL VOLUME REFERENCES
         DROP  R14                                               88036
           JOLRETN  RC=0
E30RETN4 DS    0H
           JOLRETN  RC=4
         AGO   .X8E30
*  IT IS NECESSARY TO LOCATE THE FCT FOR THE DATASET WHOSE NAME IS
*    IN DDDSNDET, TO FIND THE CURRENT VOLUMES AND COPY THE LIST TO
*    THE ALLOCATION REQUEST FOR THIS DATASET
         L     R4,ASHTCB            ADDRESS MY TCB
         L     R4,TCBJSCB-TCB(,R4)          MY JSCB
         ICM   R4,7,JSCBFCTA-JSCB(R4)         FIRST FCT
         BZ    E30RETN4              EXIT IF NONE ?!! & SSSSSSSSS
         SPACE
*   FOUND THE FCT CHAIN. NOW FIND THE DATASET ENTRY
         SPACE
         USING FCT,R4
E30TNXTF DS    0H
         CLC   FCTFLN,DDDSNAME     Q... IS THIS IT?
         BE    E30TFFCT            A... YES, GO AND GET THE VOLUME INFO
         SPACE
         ICM   R4,7,FCTNFCTA         NEXT FCT
         BNZ   E30TNXTF              GO AND TEST THE NEXT ONE
E30TNFCT DS    0H                    END OF CHAIN, AND NOT FOUND
         JOLERR 301,'DSID ',DDDSID,',DS ',#DSN,                        .
               ' IS NOT ALLOCATED,',' VOLUMES FOR DATA SET IN ERROR'
         B     E30RETN4       PRESS ON IN ANY CASE
E30TFFCT DS    0H
         SPACE 3
* NOW I HAVE FOUND THE FCT, COPY THE VOLUME SERIAL NUMBERS
         SPACE 2
         CLEAR DDVOLUME
         SLR   R8,R8
         ICM   R8,1,FCTVOLNO        LOAD VOLUME COUNT
         BZ    E30TNFCT             EXIT IF THERE ARE NO VOLUMES
         LA    R0,L'DDVOLUME/6      LOAD NUMBER OF VOLUMES ALLOWED
         CR    R0,R8                CHECK THAT THE SERIAL NUMBERS FIT
         BH    E30TOK               BE IF OK
         JOLERR 509,'DS ',DDDSID,',',#DSN,                             .
               ' IS NOT ALLOCATED,',' REFERS TO TOO MANY VOLUMES'
E30TOK   DS    0H
         L     R6,FCTVCT
         LA    R15,DDVOLUME         ADDRESS FOR VOLUMES
E30TNXTV DS    0H
         MVC   0(6,R15),8(R6)       COPY THE VOLUME SERIAL NUMBER
         LA    R15,6(,R15)          ADDRESS FOR NEXT AREA
         L     R6,4(,R6)            ADDRESS THE NEXT VCT
         BCT   R8,E30TNXTV          CONTINUE UNTIL ALL AREA DONE
         SPACE 2
         UJE22UPD FCTFLN,DDVOLUME FIX FUTURE VOLUME REFERENCES
         JOLRETN  RC=0
E30RETN4 DS    0H
         JOLRETN  RC=4
.X8E30   ANOP
         DC  10S(*)
         LTORG
         END
