UJEMNT   TITLE 'INFORM OPERATOR OF REQ''D VOLUMES UPD#XXX'
         COPY  JOLGLOBL
         $UJEPARM
         GENSAVE CSECT=UJEMNT
* THIS LOAD MODULE IS EXECUTED AS PART OF THE FIRST STEP OF ANY JOL
*    PRODUCED JOB.
* INITIALLY,TEST IF A DD-CARD FOR $$UJEMNT EXISTS,IF NOT JUST RETURN
*    BECAUSE THIS JOB REQUIRES NO VOLUMES TO BE MOUNTED FOR IT
         FINDDD ##MNTNAM
         LTR   R15,R15
         BZ    VOLSREQD        IS A DD-CARD,SO READ THE FILE AND
*                              PULL IN THE VOLUME NAMES
         GENRETN RC=0
VOLSREQD FINDDD ##PRNTNM       IF THERE IS A $$PRINT DD CARD WE
* ARE NOT EXECUTING UNDER THE GUIDANCE OF 'HASP'
*                              NOT RUNNING UNDER HASP
         LTR   R15,R15
         BZ    OPEN2
         OPEN (##UJEMNT,(INPUT))
         B     FILESOPN
OPEN2    OPEN (##UJEMNT,(INPUT),##PRINT,(OUTPUT))
FILESOPN DS    0H
         AIF   (&X8).X8010
         EXTRACT ANSAREA,'S',FIELDS=(TIOT)
         L     R1,ANSAREA
         MVC   JOBNAME,0(R1)   MOVE IT TO JOBNAME
         AGO   .NX8010
.X8010   ANOP
         L     R14,X'2EC'     THIS IS FLCTOLD ON X8 SYSTEMS       88020
         L     R14,12(,R14)   THIS POINTS TO THE JSCB             88020
         L     R15,0(,R14)    AND NOW THE CSCB                    88020
         MVC   JOBNAME,X'058'(R15) THE JOB NAME                   88020
.NX8010  ANOP
         LA    R1,8            DROP BLANKS OFF THE
FINDEJOB LA    R14,JOBNAME(R1) END OF THE NAME
         CLI   0(R14),C' '     SO THE MESSAGE LOOKS BETTER
         BNE   FIXJOBL         FIX-UP JOB NAME LENGTH
           BCT R1,FINDEJOB
FIXJOBL    STH R1,#JOBNAME
           SPACE 3
* NOW WE CAN FILL UP THE VOLUME STACK WITH VOLUME LABELS,WATCHING
*    OF COURSE THAT WE ONLY GET THE FIRST MENTION OF A VOLUME
*    SO THAT WE DON'T PRINT OUT VOLUME NAMES TWICE
           CLEAR VOLLIST
GETLOOP    GET ##UJEMNT
           LR  R4,R1
           USING CARDIN,R4
           CLI CARDIN,C'*'     VOLREF ?
           BE  GETLOOP         YES,IGNORE THIS CARD
           LA  R1,CARDIN
           LA  R14,6           LENGTH OF VOLUME ENTRIES
           LA  R15,CARDIN+72
TENDCARD   CLC 0(6,R1),=CL6' ' REACHED END OF VOLUMES ON THIS CARD ?
           BE  GETLOOP         YES,GET ANOTHER CARD THEN
           LA  R5,VOLLIST
           LA  R6,6
           LA  R7,VOLLIST+L'VOLLIST
COMPLOOP   CLC 0(6,R5),=CL6' ' HAVE WE REACHED END OF VOLUMES STACKED
*                              ALREADY ?
           BE  MOVIN           YES,SO SHIFT IN NEXT VOLUME FROM CARD
           CLC 0(6,R5),0(R1)   IS VOL IN STACK=ONE ON THE CARD ?
           BE  STEPCARD        YES,SO DON'T STORE IN,HAVE A LOOK AT
*                              THE NEXT VOLUME ON THE CARD
           BXLE R5,R6,COMPLOOP STEP POINTER TO LOOK AT NEXT VOLUME IN
*                              THE STACK.
* HERE WE HAVE FILLED UP THE STACK,TUT,TUT.
           WTO 'UJEMNT:TOO MANY VOLUMES'
           B   EOF
           SPACE 3
MOVIN      MVC 0(6,R5),0(R1)   MOVE IN NON-REPEATED VOLUME NAME
STEPCARD   BXLE R1,R14,TENDCARD GO AND GET NEXT VOLUME FROM CARD
         B     GETLOOP         FELL THRU CARD LOOP,GET A NEW CARD
EOF      DS    0H
PRINTVOL DS    0H
         CLEAR OP
         MVC    OP(20),INIT1MSS 1ST LINE OF MESSAGE
         LA    R1,L'INIT1MSS
         LA    R14,OP(R1)
         AH    R1,#JOBNAME
         LA    R1,1(R1)
         STH   R1,#OP
         MVC   0(8,R14),JOBNAME
* NOW LOOP ROUND ADDING TO #OP THE VOLUMES IN THE STACK UNTIL THE
*    END OF THE STACK OR UNTIL THE LINE IS LONG ENOUGH TO PRINT.
         LA    R5,VOLLIST
         LA    R6,6
         LA    R7,L'VOLLIST+VOLLIST
NEXTVOL  CLC   0(6,R5),=CL6' ' REACHED END OF VOLS IN STACK ?
         BE    ENDLIST
* NOW DROP OFF THE BLANKS IN THE VOLUME WE ARE ABOUT TO CONCATENATE
*    TO 'OP' STRING
           LA  R15,5           BXLE INCREMENT
FINDEVOL   LA  R14,0(R5,R15)   ABSOL ADDR VOLUME BYTE
           CLI 0(R14),C' '     BLANK ?
           BNE GOTEVOL         NO,GOOD FOUND LAST BYTE OF VOLUME
           BCT R15,FINDEVOL    LOOP TO FIND LAST BYTE
GOTEVOL    LH  R1,#OP          LOAD LENGTH OF STRING SO FAR
           LR  R14,R1          KEEP IT FOR A MINUTE
           LA  R1,2(R1,R15)    CALC NEW LENGTH
           CH  R1,=AL2(L'OP)   TOO LONG ?
           BH  OPSTRING        YES,OUTPUT WHAT WE'VE GOT SO FAR
           STH R1,#OP          NO,RESET LENGTH OF STRING
           LA  R1,OP+0(R14)    CALC ADDR OF WHERE TO MOVE VOLUME TOO
           MVC 0(6,R1),0(R5)   SHIFT IT IN
           LA R1,1(R1,R15)
           MVI 0(R1),C','      FOLLOWED BY A COMMA
           BXLE R5,R6,NEXTVOL  LOOK AT NEXT VOLUME
ENDLIST    LH R1,#OP
           BCTR R1,0
           STH R1,#OP
           BAL  R10,OUTPUT
           CLOSE (##UJEMNT,,##PRINT)
           FREEPOOL ##UJEMNT
           GENRETN
           SPACE 3
OPSTRING   BAL R10,OUTPUT
           CLEAR OP
           MVC  OP(20),INIT2MSS 2ND LINE MESSAGE TEXT
           LA R1,L'INIT2MSS
           LA  R14,OP(R1)
           AH  R1,#JOBNAME
           LA  R1,1(R1)
           STH R1,#OP
           MVC 0(8,R14),JOBNAME
           B   NEXTVOL
#OPBLKS    DS  H
           DC H'0'
           DC H'0'
#OP        DS  H
OP         DS  CL80
#JOBNAME   DS  H
JOBNAME    DS  CL8
VOLLIST    DS  CL600
           PRINT NOGEN
           AIF (&X8).X8020
##PRINT    DCB DDNAME=$$PRINT,RECFM=VB,LRECL=137,MACRF=W,DSORG=PS
##UJEMNT   DCB DDNAME=$$UJEMNT,RECFM=FB,LRECL=80,EODAD=EOF,MACRF=GL,   *
               DSORG=PS
         AGO   .NX8020
.X8020   ANOP
##PRINT  DCB   DDNAME=JOLPRINT,RECFM=VB,LRECL=137,MACRF=W,DSORG=PS
##UJEMNT DCB   DDNAME=SYSUT2,RECFM=FB,LRECL=80,EODAD=EOF,MACRF=GL,     *
               DSORG=PS
.NX8020  ANOP
OUTPUT   DS    0H
           LH  R1,#OP
           LA  R1,4(R1)
           STH R1,#OP-2
           XC  #OP,#OP
           LA  R1,4(R1)
           STH R1,#OP-6
           LA  R1,#OP-2
           SVC 35
           TM  ##PRINT+48,X'10'
           BO  WRITE
           BR  R10
WRITE      WRITE DECB0,SF,##PRINT,#OPBLKS
           CHECK DECB0
           BR  R10
BLANK      DS  0H
BLANKS     DC  CL80' '
##MNTNAM   DC CL8'$$UJEMNT'
##PRNTNM   DC  CL8'$$PRINT'
ANSAREA    DS  4F
INIT1MSS   DC  C'JOL--VOLS REQD FOR '
INIT2MSS   DC  C'JOL--CONT''D FOR '
CARDDS     DSECT
CARDIN     DS  CL80
           END
