UJCGETKN   CSECT
* THIS MODULE GETS A NEW TOKEN AND STORES IT AT THE END OF THE
*    TOKEN AREA IN ITS WORK AREA
           SPACE 1
* ON ENTRY R0 CONTAINS A NUMBER OF THE TOKEN TO GET
*    AND  R1 CONTAINS THE ADDRESS OF THE TOKEN
           SPACE 1
*  RE-USABLE,RE-ENTRANT, NOT SELF-RELOCATEABLE
           USING *,15
           SAVE (2,5),,*
           USING TKNX,1
           MVI TKN,C' '                        CLEAR              74293
           MVC TKN+1(L'TKN-1),TKN               TKNWORK           74293
           LTR 2,0             LOAD & TEST THE TOKEN NUMBER       74293
           BNP UJCGETER        NOT POSITIVE IS AN ERROR,SO ->     74293
           C   2,=A(((#TKN-TKNDESC)/4)+1) NUMBER WITH RANGE ?     74293
           BH  UJCGETER        NO,TOO HIGH,SO ERROR ->            74293
           SLL 2,2                             MULT BY 4
           LH  3,TKNDESC-2(2)  GET THE TOKEN OFFSET IN STRING     74293
           LA  4,TKNSTRG(3)    GET ABSOLUTE ADDRESS
           LH  5,TKNDESC-4(2)  GET L'TKN AND TYPE OF TOKEN        74293
           STC 5,TKNTYPE       STORE TOKEN TYPE IN 'TKNTYPE'      74293
           SR  5,5             CLEAR REGISTER 5                   81300
           IC  5,TKNDESC-4(2)  GET L'TKN                          81300
           STH 5,#TKN          SET LENGTH OF ANSWER STRING        74293
           LTR 5,5             NOW TEST IF POS OR >0          CVC 81300
           BNP UJCGETER        LENGTH 0 OR NEGATIVE IS ERROR      74293
           BCTR 5,0            -1 FOR THE MOVE EXECUTE            74293
           EX  5,MUVTKN
           SR  15,15           SET 0 RETURN CODE                  74293
UJCGETRT   EQU *                                                  74293
           LR 1,0
           LA 1,1(1)           REG 1 ALWAYS CONTAINS THE VALUE+1
           LTR 15,15           SAVE CALLER DOING IT EVERYWHERE    74293
           RETURN (2,5),T,RC=(15)  RETURN TO CALLER               74293
UJCGETER   MVI TKNTYPE,2       SET TO 'CHAR STRING'               74293
           XC  #TKN,#TKN        CLEAR LENGTH (STRING='' NOW)      80290
           LR  1,15            USE REG 1 AS BASE NOW              74293
           LA  15,4            SET 15=4 AS BAD RETURN CODE        74293
           B   UJCGETRT-UJCGETKN(1) BRANCH TO EPILOG              74293
MUVTKN     MVC TKN(*-*),0(4)
           DC   S(*,*,*,*,*,*,*,*)
           DC   S(*,*,*,*,*,*,*,*)
           DC   S(*,*,*,*,*,*,*,*)
           DC   S(*,*,*,*,*,*,*,*)
           DC   S(*,*,*,*,*,*,*,*)
           DC   S(*,*,*,*,*,*,*,*)
           COPY  JOLGLOBL                                         J60
           COPY TKNX
           END
