* A NOTE TO THE UNSUSPECTING MAINTENANCE PROGRAMMER
* THE RULES FOR FINDING DATASET ALLOCATIONS ON MVS AND OSIV/F4
*    TCBJSCB POINTS TO JSCB
*    JSCDSABQ POINTS TO QDB
*    QDBFELMP POINTS TO FIRST DSAB
*    QDBLELMP POINTS TO LAST DSAB
*    DSABFCHN POINTS TO THE NEXT DSAB
*    DSABBCHN POINTS TO THE PREVIOUS DSAB
*    DSABTIOT POINTS TO THE TIOT ENTRY
*    TIOT+C POINTS TO JFCB IN SWA (NOTE 3 BYTES FROM +C, NOT 4)
*
*    WHY ALL THIS BOTHER?
*
*     DSAB CONTAINS USEFUL FLAGS SUCH AS IN-USE ATTRIBUTE ETC
*
*   TO FIND THE JOB ACCOUNTING INFO
*    1) FIND CURRENT TCB
*    2) LOAD JSCB ADDRESS FROM TCBJSCB
*    3) LOAD JCT ADDRESS FROM JSCBJCT
*    4) LOAD ACT ADDRESS FROM JCT+X'38'
*    5) ACT+X'1E' CONTAINS NUMBER OF ACCOUNTING FIELDS
*    6) ACCOUNTING FIELDS START AT ACT+X'20'
*       THESE HAVE THE FOLLOWING FORMAT:
*      A) FIRST BYTE IS THE LENGTH EXCLUSIVE OF THIS BYTE
*      B) SECOND BYTE IS THE FIRST IN THE STRING
*
*    7) ACT FOR STEP IS FOUND VIA TCBJSCB, JSCBSCT, SCT+X'30'
*
*
          GENSAVE CSECT=PRINTQDB
          USING  WKAREA,R10
         L     R10,=V(WKAREA)
         OPEN  (PRINTER,OUTPUT)
         SPACE 1
         TITLE 'SUBROUTINE TO SEARCH THE DSAB QUEUE'
         L     R9,CVTPTR           LOAD CVT ADDRESS
         L     R9,0(0,R9)          TCB WORDS
         L     R9,4(0,R9)          MY TCB ADDRESS
         USING TCB,R9
         L     R9,TCBJSCB          MY JSCB ADDRESS
         DROP  R9
         USING KDJJSCB,R9
         L     R9,JSCDSABQ         DSAB Q
         DROP  R9
         USING QDB,R9              DSAB Q DESCRIPTOR BLOCK
         ICM   R8,15,QDBNELMS      ARE THERE ANY?
         BZ    QSR090              A... NO
         L     R8,QDBFELMP         LOAD DSAB ADDRESS
         USING DSAB,R8
QSR005   DS    0H
         L     R1,DSABTIOT         POINT TO TIOT ENTRY
*        CLC   =CL8' ',4(R1)       BLANK DDNAME?
*        BE    QSR020              IF SO, NOT A MATCH
         MVC   XDDNAME(8),4(R1)    MOVE DDNAME TO PRINT AREA
         ICM   R1,7,12(R1)         POINT TO JFCB
         USING JFCB-16,R1
         MVC   XDSNAME,JFCBDSNM
         MVC   XVOLUME,JFCBVOLS
         MVC   TUDSORG+6(2),DSABORG   COPY DSORG
* DISP FIELDS
         MVC   D20DISP1(8),=CL8' '
         TM    JFCBIND2,B'11000000'        NEW?
         BO    E20NEWDS                    YES
         TM    JFCBIND2,B'01001000'        SHR?
         BO    E20SHRDS                    YES
         TM    JFCBIND2,B'01000000'        OLD?
         BO    E20OLDDS                    YES
         TM    JFCBIND2,B'10000000'        MOD?
         BO    E20MODDS                    YES
         B     E20TREST                    TEST DISP(2)
E20NEWDS MVC   D20DISP1,=C'NEW'
         B     E20TREST                    TEST DISP(2)
E20SHRDS MVC   D20DISP1,=C'SHR'
         B     E20TREST                    TEST DISP(2)
E20OLDDS MVC   D20DISP1,=C'OLD'
         B     E20TREST                    TEST DISP(2)
E20MODDS MVC   D20DISP1,=C'MOD'
E20TREST DS    0H
* DISP(2) FIELDS
         MVI   XINUSE,C'*'
         TM    DSABFLG1,DSABNUSE
         BO    WRITREC
         MVI   XINUSE,C' '
WRITREC  PUT   PRINTER,OUTPUTR
         DROP  R1
QSR020   DS    0H        THIS ONE DID NOT MATCH:
         CLM   R8,7,QDBLELMP+1     Q... WAS THAT THE LAST?
         BE    QSR090              A... YES IT WAS: EXIT NOT FOUND
         L     R8,DSABFCHN         POINT TO THE NEXT
         B     QSR005              AND TRY AGAIN
QSR080   GENRETN RC=0              RETURN: FOUND
QSR090   DS    0H                  RETURN: FOUND
         CLOSE PRINTER
         GENRETN RC=00             RETURN: FOUND
         PRINT NOGEN
PRINTER  DCB   DDNAME=SYSPRINT,RECFM=VA,LRECL=80,BLKSIZE=84,     +++++++
               MACRF=PM,DSORG=PS
OUTPUTR  DC    AL2(RECEND-*,0)
XSPASCI  DC    C' '
XINUSE   DC    C' '
XDDNAME  DC    CL8' '
XSP1     DC    C' '
XDSNAME  DC    CL44' '
XSP2     DC    C' '
D20DISP1 DC    CL3' '       DISPOSITION
D20DISP2 DC    CL7' '       DISPOSITION
         DC    C' '
XVOLUME  DC    CL6' '
RECEND   DS    0C
XSP3     DC    CL20' '
         DROP  R8
         DROP  R15
*        PRINT OFF
         TITLE 'COMMUNICATIONS VECTOR TABLE'
         CVT   DSECT=YES,SYS=BOTH
         TITLE '  DATA CONTROL BLOCK'
         DCBD  DSORG=PS,DEVD=DA
         PRINT GEN
         TITLE '  USER PROFILE TABLE'
         IKJUPT
         TITLE '  PROTECTED STEP CONTROL BLOCK'
         IKJPSCB
         TITLE '  ENVIRONMENT CONTROL TABLE'
         IKJECT
         TITLE '  PARSE PARAMETER LIST'
         IKJPPL
LENMYPPL EQU   *-PPL
         AGO   .F4CB
         AIF   (&F4).F4CB
         TITLE '  TASK CONTROL BLOCK'
         IKJTCB
         TITLE '  JOB-STEP CONTROL BLOCK'
         IEZJSCB
         TITLE '  DATA-SET ASSOCIATION BLOCK'
         IHADSAB
         TITLE '  DSAB QUEUE DESCRIPTOR BLOCK'
         IHAQDB
         TITLE '  JOB FILE CONTROL BLOCK'
JFCB     DSECT
         IEFJFCBN
         AGO   .CBDUN
.F4CB    ANOP
         TITLE '  TASK CONTROL BLOCK'
*        KAATCB
         XAMTCB
         TITLE '  COMMUNICATIONS VECTOR TABLE'
         XAMCVT
         TITLE '  JOB-STEP CONTROL BLOCK'
*        KDJJSCB
         XDDJSCB
         TITLE '  DATA-SET ASSOCIATION BLOCK'
         KDJDSAB
         TITLE '  DSAB QUEUE DESCRIPTOR BLOCK'
         KDJQDB
         TITLE '  JOB FILE CONTROL BLOCK'
JFCB     DSECT
         KDJJFCBN LIST=YES,DSECT=NO
.CBDUN   ANOP
         AIF   ('&GENCB' EQ 'YES').GENCB2
         POP   PRINT
.GENCB2  ANOP
         PRINT ON
         TITLE '  WORK AREA'
WKAREA   CSECT
         DS    18F                  SAVE AREA
DOUBLE   DS    D
LINKDCBE DS    XL(DCBLEN)
JFCBPTR  DC    A(*+4+X'87000000')
WRKJFCB  DS    XL176
OPENLIST DS    A
LINKSFE  DS    XL8
ALLCREGS DS    13A
QUALREGS DS    13F
CONCREGS DS    13F
DCONCRGS DS    13F
S99MRET  DS    A
STPRETN  DS    A
SDQRETN  DS    A
QSUBRET  DS    A
PARM     DS    1F
NBRDDN   DS    H
FLAG     DC    X'00'
QSUB     EQU   X'80'               ASK WHETHER TO SUBMIT
TERMIN   EQU   X'40'               INPUT WAS FROM TERMINAL
         DS    0F
TEMP     DS    XL5
HEXCONV  DS    XL19
PPLADDR  DS    F
MYECB    DS    F
         SPACE 4
         DS    0F
DFLIST   DS    A       -> FAILING SVC 99 RB
         DS    A       -> RETURN CODE
         DS    A       -> IKJEFF02 PTR OR IS ZERO
         DS    A       -> DAIRFAIL FLAGS
DFCPPL   DS    A       -> CPPL OR IS ZERO
S99RC    DS    F       SVC 99 RETURN CODE
DFFLAGS  DC    X'0032' DAIRFAIL FLAGS
DFEFF02  DS    A
DFMSG    DC    C'DAIRFAIL RETURN CODE NN'
S99RBPTR DS    A
S99RB    DS    0XL20
S99RBLN  DS    X
S99VERB  DS    X
S99FLAG1 DS    XL2
S99ERROR DS    XL2
S99INFO  DS    XL2
S99TXTPP DS    XL4
S99FLAG2 DS    XL4
S99RESVD DS    XL4
S99TUPL  DS    20XL4
         SPACE 4
TUAREA   DS    0D
TUDSN    DS    0XL50,XL2
TUDSNNBR DS    XL2
TUDSNENT DS    XL46,CL8
TURETDSN DS    XL2
TURETDSK DS    XL2
TURETDS# DS    XL2
TURETDS  DS    CL46
TUUNIT   DS    XL14
TUDDN    DS    0XL14,2X
TUDDNNBR DS    XL2
TUDDNENT DS    CL10
TURETDDN DS    0XL14,2X
TURETDDK DS    XL2
TURETDD# DS    XL2
TURETDD  DS    CL8
TUPSD    DS    0XL14
         DS    XL2
TUPSDNBR DS    XL2
TUPSDLEN DS    XL2
TUPSDNM  DS    CL8
TUDSORG  DS    XL8
TUMBR    DS    0XL14
         DS    XL2
TUMBRNBR DS    XL2
TUMBRLEN DS    XL2
TUMBRNM  DS    CL8
TUDISP   DS    XL7
TUCATLG  DS    XL7
TUTERM   DC    X'00280000'
TUDMY    DC    X'00240000'
TUSOUT   DC    X'001800010001C1'
TUBLKSI  DC    X'0030000100020000'
TUVOL    DC    X'001000010006404040404040'
         DS    0F
TUAREAL  EQU   *-TUAREA
*
* FOLLOWING CODE BY PETER FOLEY ABS APR 1983
*
TUAREA2  DS    0D
TUROUND  DS    XL4
TUAVBLK  DS    XL9
TUPRIMS  DS    XL9
TUSECND  DS    XL9
         DS    0F
TUAREAL2 EQU   *-TUAREA2
*
* PREVIOUS CODE BY PETER FOLEY ABS APR 1983
*
         SPACE 3
*  THIS AREA IS USED TO BUILD THE TEXT UNIT FOR CONCATENATION
         SPACE
TUCONC   DC    X'0001'     TYPE
         DC    X'0000'     NUMBER
TUCONCDD DS    16CL10      SPACE FOR SIXTEEN ENTRIES
TUCONCL  EQU   *-TUCONC
CNDDTABP DS    A
CNDDTAB  DS    20CL8       SPACE FOR 20 CONCATENATED DDNAMES
         SPACE 3
IOPB     DS    4F
IOPL     DS    4F
IOECB    DS    F
OLD1CHN  DS    XL4
OLD1     DS    XL4
OLD1#1   DS    XL4
OLD1#2   DS    XL4
OLD1#3   DS    XL4
OLD1#4   DS    XL4
OLD1#5   DS    XL4
MSGRC    DS    XL6
MSGDARC  DS    XL8
MSGDAFLG DS    XL8
MSGCAT   DS    XL8
PRMOPTNS DS    XL256
USERPLST DS    A
USERPRML DS    H
USERPRM  DS    CL256
MYPPL    DS    XL(LENMYPPL)
PPLREPL  DS    A                  PTR TO REPLY
         SPACE 2
CFLAGS   DS    0XL2
CFLAG1   DS    X
CFLAG2   DS    X
         SPACE 4
APTAREA  DS    0X                  ALLOCATION PARAMETER TABLE
APDE     DS    A                   ADDRESS OF PDE FOR FILE TO ALLOCATE
ADSNAME  DS    CL44                DSNAME TO ALLOCATE
AMBRNAME DS    CL8                 MEMBER NAME TO ALLOCATE
ADDNAME  DS    CL8                 DDNAME SUPPLIED TO USE
AUNIT    DS    CL8                 UNIT NAME/ADDRESS TO USE
AVOL     DS    CL6                 VOLUME CONTAINING DATASET
*DEVT    DS    XL4                 UCB DEVICE TYPE OF DEVICE (NOT USED)
ASUFFIXL DS    H                   SUFFIX LENGTH
ASUFFIX  DS    CL9                 SUFFIX IF DSN TO BE QUALIFIED
AOUTLIM  DS    F                   OUTLIM
ABLKSIZ  DS    H                   BLOCK SIZE
ALRECL   DS    H                   RECORD LENGTH
APRIM    DS    H                   PRIMARY ALLOCATION
ASEC     DS    H                   SECONDARY ALLOCATION
ADIR     DS    H                   DIRECTORY BLOCKS
ADSORG   DS    H                   DSORG REQUIRED
AFLAGS   DS    0XL2                ALLOCATION FLAGS
AFLAG1   DC    X'00'               ALLOCATION FLAG BYTE 1
FF       EQU   X'FF'
ASOUT    EQU   X'80'               ALLOCATE A SYSOUT DATASET
AMOD     EQU   X'40'               ALLOCATE AS NEW IF NO DATASET
ATEMP    EQU   X'20'               TEMPORARY DATASET
ANULL    EQU   X'10'               DUMMY DATASET REQUIRED
APDS     EQU   X'08'               CONCATENATE REQUEST
ASHR     EQU   X'04'               A DATASET CAN BE SHARED
ATERM    EQU   X'02'               ALLOCATE DATASET TO THE TERMINAL
         SPACE
AFLAG2   DC    X'00'               SECOND BYTE OF ALLOCATION FLAGS
APERMA   EQU   X'80'               MARK PERMANENTLY ALLOCATED
APERMC   EQU   X'40'               MARK PERMANENTLY CONCATENATED
ASOUTC   DS    C                   SYSOUT CLASS
APTL     EQU   *-APTAREA
         ORG   APTAREA
APT      DS    XL(APTL)
CSUFFIX  DS    CL(L'ASUFFIXL+L'ASUFFIX)
         SPACE 4
CATRC    DS    X
CATFLG   DS    X
         DS    0D
CATWORK  DS    XL265,0D
CATPARMS DS    4XL4
CATDSPT  DS    XL4
CATRET   DS    XL4
         DS    0D
WKALEN   EQU   *-WKAREA
         EJECT
DSPDE    DSECT
DSENT    DS    0CL28
DSNPT    DS    XL4
DSLEN    DS    Y
DSFLG    DS    2X
MBRPT    DS    XL4
MBRLEN   DS    Y
MBRFLG   DS    2X
PSDPT    DS    XL4
PSDLEN   DS    Y
PSDFLG   DS    2X
DSNEXT   DS    XL4
         DROP
         END   PRINTQDB
         TITLE 'CVT'
         TITLE 'FLC'
         XAMFLC
         TITLE 'RB '
         XAMRB
         TITLE 'CIB'
         XDDCIB
         TITLE 'FCT'
         XDDFCT
         TITLE 'MSCVT'
         XDDMSCVT
         END
