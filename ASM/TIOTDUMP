TIOTDUMP TITLE 'JOL ** DYNAMIC ALLOCATION ** RESIDENT JOB STEP MONITOR'
         MACRO
&L       $DMP &FROM,&SIZE,&TITLE=
         LCLC  &C
         AIF   (T'&L EQ 'O').N
&L       DS    0H
.N       ANOP
         AIF   (K'&TITLE EQ 0).NOTITLE
&C       SETC  'KCP'.'&SYSNDX'
         BAL   R0,&C           BRANCH AROUND THE MESSAGE
         DC    Y(&C-*,0),C&TITLE
&C       PUT   PRINT,(0)
.NOTITLE ANOP
         IHBINNRA &FROM,&SIZE
         L     R15,=V(UJE21DMP)
         BALR  R14,R15
         MEND
         COPY  JOLGLOBL
         PUNCH '   PAGE         TIO00DMP'                         J40B
         $UJEPARM
         GENSAVE CSECT=TIO00DMP,BASE=R11                          J40B
         USING PSA,0
         SPACE 2
         LR    R2,R1
         OPEN  (PRINT,OUTPUT)
         L     R2,PSATOLD    LOAD TCB ADDRESS
         USING TCB,R2        ADDRESSIBILITY FOR TASK CONTROL BLOCK
         L     R2,TCBTIO
         DROP  R2                                                 J40B
         SL    R2,=A(32)
         $DMP  (R2),256,TITLE='TIOT PREFIX AND TIOT'
RETNOS   DS    0H
         LA    R3,32+24(,R2)                                      J40B
NEXT     DS    0H
         SLR   R4,R4                                              J40B
         ICM   R4,1,0(R3)                                         J40B
         BZ    DONE                                               J40B
         $DMP  (R3),(R4)                                          J40B
         ALR   R3,R4                                              J40B
         B     NEXT                                               J40B
DONE     DS    0H
M00NCLSJ DS    0H
         CLOSE PRINT
         L     R13,4(,R13)       DRATTED THING
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14             AND BACK WE GO
         PRINT DATA
         DC    28S(*)
         LTORG
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=PM,                         .
               LRECL=137,                                              .
               BLKSIZE=141,                                            .
               RECFM=VAB
CONSTART DS    0D
SAVEAREA DS    18F
MSGAREA  DS    XL256
         DS    0D
CONEND   DS    0X
         COPY  UJECBS
.END     ANOP
         TITLE 'UJE21DMP: DUMP A STORAGE AREA'
         GENSAVE CSECT=UJE21DMP,BASE=R12
         ALR   R0,R1           CALCULATE END ADDRESS
         STM   R0,R1,ENDADDR      STORE PARAMETER ADDRESSES
         CLEAR DPDATA
         LA    R0,DPBLKL
         SLL   R0,16
         ST    R0,DPBLOCK
         LA    R0,DPLINEL
         SLL   R0,16
         ST    R0,DPLINE
         L     R2,STARTADD     ADDRESS OF THE AREA
         MVI   DPDATA,C' '     SINGLE SPACE
E21LP0   DS    0H
         ST    R2,TEMP
         UNPK  HEXCONV,TEMP
         TR    HEXCONV,HEX
         MVC   DPADDR,HEXCONV+2
         S     R2,STARTADD        SUBTRACT ORIGINAL ADDRESS
         ST    R2,TEMP            STORE OFFSET
         A     R2,STARTADD        RESTORE IT
         UNPK  HEXCONV,TEMP
         TR    HEXCONV,HEX
         MVC   DPOFFS,HEXCONV+5
         MVC   DPCHARS,0(R2)   COPY DATA IN CHARACTERS
         TR    DPCHARS,TRTAB   REMOVE SOME HARD-TO-PRINT DATA
         LA    R14,DPHEX       ADDRESS FOR HEX DATA
         LA    R1,2
E21LP1   DS    0H
         LA    R0,4            4 SEGMENTS
E21LP2   DS    0H
         UNPK  HEXCONV,0(5,R2) CONVERT SOME TO HEX
         TR    HEXCONV,HEX
         MVC   0(8,R14),HEXCONV INTO THE PRINT LINE
         LA    R14,9(,R14)     NEXT FOR OUTPUT
         LA    R2,4(,R2)       NEXT FOR INPUT
         BCT   R0,E21LP2
         LA    R14,1(,R14)    AN EXTRA SKIP IN PAGE CENTRE
         BCT   R1,E21LP1
*{       PUT   PRINT,DPBLOCK
         PUT   PRINT,DPLINE
         MVI   DPDATA,C' '        SINGLE SPACE
         C     R2,ENDADDR      Q... ANY MORE?
         BNH   E21LP0          A... YES
         GENRETN RC=0
         DC    20S(*)   FOR PATCHES
TRTAB    DC    256AL1(*-TRTAB) ALL THINGS TO ALL THINGS
         ORG   TRTAB
         DC    64X'40'         FIX THE FIRST ONES
         ORG   TRTAB+C'9'+1
         DC    6C' '           AND THE LAST ONES
         ORG
         LTORG
         ORG   *-240                                              J40B
HEX      DS    0X
         ORG
         DC    C'0123456789ABCDEF'
E21WORK  DS    0D
         DS    18F             SAVE AREA
ENDADDR  DS    A
STARTADD DS    A
TEMP     DS    XL5
HEXCONV  DS    CL9
DPBLOCK  DS    F
DPLINE   DS    F
DPDATA   DS    0CL125
         DS    C
DPADDR   DS    CL6
         DS    C
DPOFFS   DS    CL3,2C
DPHEX    DS    CL75
DPCHARS  DS    CL32
DPLINEL  EQU   *-DPLINE
DPBLKL   EQU   *-DPBLOCK
         DS    0D
E21WORKL EQU   *-E21WORK
         END   TIO00DMP
