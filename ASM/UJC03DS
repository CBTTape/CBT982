UJC03DS  TITLE '        DECODE DATA SET DECLARE'                74303
* JOL COPYRIGHT CLARKE COMPUTER SOFTWARE 1971,1972,1973,1974,1975
* COPYRIGHT CCS 1976,1980
* COPYRIGHT C. V. CLARKE 1982
* COPYRIGHT C. V. CLARKE 1983
* COPYRIGHT CCS-JOL P.L  1986.
* COPYRIGHT C. V. CLARKE 2010
*
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-C001.
           SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
*JOL30030:-    WHEN UNIT KEYWORD USED,JOL DOESNT LOOK UP DEVNAMET
*JOL30034:-    UNITNO & UCOUNT NOT ALLOWED FOR SYSOUT FILES
*JOL30041:-    UCOUNT=P STORES UCOUNT 1
*JOL30048:-    JOL OVER-WRITES SYSOUT CLASS WHEN FORM USED
*JOL30053:-    FIELDS NOT MERGED WITH LIKE OVERRIDE
*JOL30055:-    RETPD TOO SHORT
*
* MODIFIED BY C.V. CLARKE FOR BINARY TABLE SEARCH, JULY, 1986.
*
* MODIFIED BY C.V. CLARKE TO INCLUDE XA SUPPORT DEC, 1983
*    THIS SUPPORT REPLACES THE CODE THAT CHECKED THE DEVNAMET
*    WITH A CALL TO A NEW MODULE UJC03DEV IN SOURCE MODULE
*    UJS02XA
*
* MODIFIED BY D.J.DOUBTFIRE TO INCLUDE SPECIAL PARAMETERS
* FOR LASER PRINTERS (FLASH,,CHARS,BURST,MODIFY).
*
*
           SPACE 3
         COPY JOLCOM
         TITLE  'DATA SET AND SYSOUT ANALYSER - DSIDS'            75128
         PRINT DATA                                               75128
C03SAVE  DSECT                                                    75128
         DS    18F                                                75128
C03STKN  DS    H      SAVE AREA FOR CURRENT TOKEN #                DASD
C03DEXT  DS    CL1                                                75128
C03DLIKE DS    CL8                                                75128
         COPY  DDEXTRA
*DWORK   DS    (600/4)F  WORK AREA FOR DDSNDET.  IT USED TO USE     J60
*                        THE 'TRANS' WORK AREA IN JOLCOM, BUT IT    J60
*                        IS TOO SMALL FOR THE PATH ETC.             J60
C03DLEN  EQU   *-C03SAVE
         JOLSAVE CSECT=UJC03DS,BASE=(11,12),SIZE=C03DLEN          75128
* THIS CSECT IS RESPONSIBLE FOR DECODING A DATA SET DECLARATION
*    ON ENTRY,R1 CONTAINS THE ADDRESS OF A PARM LIST WHICH CONTAINS
*    THE FOLLOWING INFORMATION:-
*          1. THE ADDRESS OF TKNX
*          2. THE ADDRESS OF JOLCOM,WHICH CONTAINS VARIOUS PIECES
*              OF INFORMATION,SUCH AS DCB'S ETC
*          3. THE ADDRESS OF AN AREA INTO WHICH THE DECODED DECLARE
*              IS TO BE PLACED AS TABLES.
           SPACE
* LET'S ESTABLISH A BIT OF ADDRESSALITY.
           LM  R2,R4,0(R1)     LOAD PARAMETER LIST
           USING JOLCOM,R2
           USING TKNX,R3
           USING C03SAVE,R13                                      75128
*          LA    R4,DDWORK    CHANGED NOV 28, 2010                J60
           USING DDDSNDET,R4
           SPACE
*        MVI   DDDSNDET,C' '                                      J60
*        MVC   DDDSNDET+1(256),DDDSNDET                           J60
*        MVC   DDDSNDET(4),ZERO                                   J60
         CLEAR C03DEXT,C03DLIKE,DDSPECTP,DDSPECT2,                75128*
               DDCANCEL,DDLABEL,DDUNITNO,DDPRIM,DDSEC,DDDIRECT,   80290*
               DDBLKSZE,DDLRECL,DDSIZE,DDSIZE2,DDAVREC,DDBUFNO,   80290*
               DDBUFND,DDBUFNI,                                   82300*
               DDBUFL,DDRKP,DDKEYLEN,DDOVERFL,DDVOLSEQ,DDMAXVOL,  80290*
               DDUNITQU
         CLEAR DDAEXTRA,DDLENXTR,DDLENXTR+2,#DDSUBS,#DDVOL,       87150*
               DDSUBS,DDLONGVL                                    87150
           MVI DDDD1,C'D'                      SET RECORD TYPE TO'D'
           MVC DDSTMT,STMT                     MOVE STMT NO IN FOR
*                                              ERRORS LATER
           MVC DDLEVEL,LEVEL+1
           MVC DDNEST,NEST+1
           MVC DDOSVB,=AL2(DDLEN)
           GETTKN NO=2                         GET THE 2ND TOKEN
           MVC DDDSID,TKN                      MOVE THE DSIDENTIFIER
*                                              IN
           SPACE 3
* NOW THE HARD WORK BEGINS,I.E DECODING MADLY
           GETTKN NO=3
           CLI TKN,C'*'                        SYSIN DATA SET ?
           BNE NOTSYSIN
           STH R1,TKNCURR
           MVC DDDSNAME(4),=C'*,$$'
           MVC DDDSNAME+4(4),DDSTMT
           OC  DDDSNAME+4(4),=C'0000'
         MVI   DDTYPE,DDSYSIN                                     75128
           B C                                 SEE IF ANY OTHER INFO
           SPACE 3
NOTSYSIN   EQU *
* NOW TRY SYSOUT PROCESSING
           CLC TKN(6),=C'SYSOUT'
           BE ISSYSOUT
           CLC =C'PRINTER',TKN
           BE ISSYSOUT
           CLC =C'PUNCH ',TKN
           BNE NOSYSOUT
*   PUNCH DATA SET HERE
          CLEAR DDCOPIES,DDOUTLIM                                 75128
         MVI   DDTYPE,DDPUNCH TURN ON PUNCH INDICATOR             75128
           L   R1,AJOLGEN                                         75311
           USING GENDETS,R1                                       75311
           MVC DDSYSCLS,PUNCHCLS                                  75311
           DROP R1                                                75311
           MVI TKNCURR+1,4     SET TKNCURR=4 (MVI OK)             74256
           B   C               START NORMAL PROCESSING
ISSYSOUT    EQU *
         CLEAR DDCOPIES,DDOUTLIM,                                 75311*
               DDFLASH,DDCHARS,DDMODIFY,DDBURST                   75311
           MVI TKNCURR+1,4     SET TKNCURR=4 (MVI OK)             74256
         MVI   DDTYPE,DDSYSOUT TURN ON SYSOUT INDICATOR           75128
           GETTKN NO=4
           CLI TKN,C'='                        SYSOUT=?
           BNE NOSYSEQU
           STH R1,TKNCURR
           GETTKN (R1)         GET SYMBOL AFTER '='               74256
           CLC TKN,C' '        USER MAY HAVE CODED ' 'ACCIDENTLY  74256
           BE  C               IF SO,DON'T UPSET DEFAULT CLASS 'A'74256
C03STRCL CLI   TKN,C'''' SYSOUT='(A,X,C)' ????                    75128
         BNE   C03SYSNS       NOT SPECIAL SYSOUT                  75128
         MVI   DDTYPE,DDSPECOU TURN ON SPECIAL SYSOUT PROCESSING  75128
         XC    DDDSNAME(L'DDDSNAME+L'DDMBR),DDDSNAME SO NOTHING   75128
*                                  ELSE CAN BE STORED 'JOLSTOR'   75128
           BAL  R14,DROPQUOT      DROP ANY QUOTES
         LH    R15,#TKN            LOAD LENGTH                    75128
         LA    R15,2-1(R15)        +1                             75128
         EX    R15,SYSXMUV         SHIFT IT IN                    75128
         B     STORR1                                             75128
SYSXMUV  MVC   DDDSNAME(*-*),#TKN                                 75128
         SPACE 3                                                  75128
C03SYSNS MVC   DDDSNAME(1),TKN POP IN REQUESTED CLASS             75128
           B   STORR1          STORE R1 IN TKNCURR,CARRY ON       74256
NOSYSEQU   EQU *
* HERE,WE MAY HAVE A BLANK, IE USER CODED 'DCL X PRINTER;'        74256
           CLI TKN,C' '        ' '                                74256
           BE  C               YES,GO TO END PROCESSING           74256
* NOW HERE WE COULD HAVE:-                                        74256
*    'DCL X PRINTER A';                                           74256
* OR 'DCL X PRINTER 2 PART'; ETC                                  74256
*    IF PRINTER 'A' | SYSOUT A THEN WE SHALL OVER-RIDE OUR DEFAULT74256
*          OF SYSOUT=(A)                                          74256
* HOWEVER,WE COULD ALSO HAVE 'FORM XYZ' OR'2 PART' OR '2 COPIES'  74256
* SO:- LETS SORT THAT OUT A BIT MORE                              74256
           CLI #TKN+1,1                        LENGTH OF TOKEN=1 ?
*                                              IE SYSOUT= A ?
           BNE C               GO JOIN NORMAL LOOP                74256
          FIX 'CHECK THIS MORE THOROUGHLY LATER' 80290
         CLI   TKN,C'*'                SYSOUT = * ?              A DASD
         BNE   C03CKTYP                                          A DASD
         L     R1,AJOLGEN                                        A DASD
         USING GENDETS,R1                                        A DASD
         LA    R1,5                    NEXT TOKEN IS NO 5        A DASD
         CLC   DEFSYS(3),=CL3'VS2'     IS SYSTEM VS2 ?           A DASD
         BE    C03STRCL                SYSOUT=* & SYSTEM IS VS2  A DASD
         CLC   DEFSYS(3),=CL3'MVS'     IS SYSTEM VS2 ?            82200
         BE    C03STRCL                SYSOUT=* & SYSTEM IS VS2   82200
         DROP  R1                                                A DASD
C03CKTYP TM    TKNTYPE,X'01' ALPHABETIC OR NUMERIC ?       C63537  SOCO
         BO   C
* HERE IT IS A 1 CHARACTER,ALPHABETIC TOKEN,SO SYSOUT=X           74256
           LA  R1,5            NEXT TOKEN WILL BE NO 5            74256
           B   C03STRCL        GO STORE SYSOUT CLASS              74256
           SPACE 3
NOSYSOUT   EQU *
* HERE IT IS NOT A SYSIN OR SYSOUT DATA-SET
*    WE MUST SEE IF HE SAID'DCL X DS'OR'DCL X DATA SET'
           CLC     TKN(2),=C'DS'
           BE  SKIP1
           LA  R1,1(R1)
STORR1     EQU *                                                  74256
SKIP1      STH R1,TKNCURR
           SPACE 3
C          EQU *
* WHEW,NOW WE CAN FINALLY GET INTO OUR LITTLE DECODING LOOP
*    AND QUIETLY GO BESERK
           CLC TKNCURR,TKNNO                   HAVE WE FINISHED ?
           BNH FINDKEY                         NO,PRESS ONWARDS
           SPACE 3
* WE APPARENTLY HAVE FINISHED PROCESSING THE MIGHTY DECLARE STMT
* NOW BEFORE WE GO (AND BASICALLY BECAUSE THE TABLE IS IN CORE NOW)
*     WE WILL HAVE A DECKO AT THE UNIT AND VOLUME TO SEE IF WE CAN
*     FILL IN THE 'DDUNITYP' FIELD.
           IFNULL DDVOLUME,C03CANT CANT DO ANYTHING WITH THE UNIT
*                         TYPE SO LEAVE IT FOR A LATER PHASE.
           SPACE 3
* NOW WE HAVE A VOLUME
* LET'S SEE IF IT IS IN THE TABLE
         L     R1,AJOLGEN                                         75128
         USING UJGENDET,R1                                        75128
         L     R1,APREFVOL                                        75128
         DROP  R1                                                 75128
           LM R7,R9,0(R1)      LOAD BXLE CONSTANTS
C03FV01    CLC 0(6,R7),DDVOLUME
           BE  C03UN01
           BXLE R7,R8,C03FV01
           B  C03CANT          DIDN'T FIND IT IN TABLE,SO AWAY,AWAY
C03UN01    EQU *
* NOW IF THE DDUNITYP FIELD IS '' THE WE'LL COPY THIS ONE IN
           CLI DDUNITYP,C' '
           BNE C03COMPU
           MVC DDUNITYP,6(R7) SHIFT IN UNIT TYPE (JOLS)
           B  C03CANT
C03COMPU   EQU *
* THE UNIT TYPE HAS BEEN SET UP.
*     IT COULD HAVE BEEN SET UP BY THE VOLUME PROCESSOR EQRLIER
*     OR FROM THE UNIT PROCESSOR.
*     BECAUSE THIS IS A PREFERRED VOLUME WE CAN AT LEAST CHECK
*     THAT THE UNIT TYPES ARE VAGUELLY SIMILIAR.
           SR R0,R0
           IC R0,6(R7)    GET UNIT TYPE FROM VOL TABLE
           SR R1,R1
           IC R1,DDUNITYP GET UNIT TYPE FROM DD CARD
           LA R15,X'1F'   ONLY WANT TO LOOK AT LAST 5 BITS          J60
           NR R0,R15      SO AND
           NR R1,R15      SO AND
           CR R0,R1       AND COMPARE
           BE C03CANT     ALL'S OK
         JOLERR 202,'UNIT SPECIFIED FOR PREFERRED VOLUME CHANGED AS IT *
               WAS INCORRECT'
           MVC DDUNITYP,6(R7)  OVER-WRITE THE UNIT HE SPECIFIED
C03CANT    EQU *
* IF THIS IS A SYSIN CARD FILE,SHIFT IN THE DEFAULT BLOCKSIZE SO THAT
*    IF THE USER IS OVER-RIDING A DSID,THE THE BLOCKSIZE FROM THE OLD
*    ONE WON'T GET IN THE JCL.
           CLC =C'*,$$',DDDSNAME SYSIN ?
           BNE C03FINDO        NO,FIND OLD ONE(IF THERE IS ONE)
* NOW SHIFT IN BLOCKSIZE AND LRECL,IF NOT THERE ALREADY
           IFVALUE DDBLKSZE,C03LSYSB
         L     R1,AJOLGEN                                         75128
         USING GENDETS,R1                                         75128
           MVC DDBLKSZE(L'PARMSYSB),PARMSYSB SHIFT IN BLOCK SIZE
C03LSYSB   IFVALUE DDLRECL,C03LREC
           MVC DDLRECL,=H'80'                                    80290
C03LREC    IFVALUE DDRECFM,C03FINDO
           MVC DDRECFM(2),=C'FB'
C03FINDO   EQU *
* NEW FOR 5.2                                                     J52
         CLC   =C'DBF',ICOMMAND                                   J52
         BNE   C03FNDO2                                           J52
         JOLRETN  RC=0                                            J52
C03FNDO2   DS  0H                                                 J52
* THE NEXT PIECE OF CODE FROM HERE TO LABEL 'C03STRIT' GOES
*    SOMETHING LIKE THIS:-
*
* C03FINDO:FIND DSID NAME IN TABLE;
*          IF NOT_THERE, GO TO C03STRIT;       /* STORE NEW NAME */
*     /* NAME IS IN TABLE */
*
*          IS THIS CURRENT NAME 'EXTERNAL'
*              YES, RETURN (2 EXTERNALS, IGNORE SECOND)
*     /* NO */
*          SAVE R1 IN R7       /* R1 -> NAME IN TABLE */
*          IS THIS_LEVEL = OLD_LEVEL ?
*              NO-> C03NEWID;                  /* DIFF LEVEL, NEW DSID
*                                              */
*     /* HERE OVER-RIDE */
*          ISSUE MESSAGE;
*          MOVE CURRENT DSID TO WORK;
*          READ OLD ONE INTO TRANS;
*C03TYP9:  DO MERGE OF ATTRIBUTES;
*C03LIKE:  WAS LIKE SPECIFIED ?
*          NO; DO;
*              IS THIS EXTERNAL? YES, SET LEVEL=0;
*              OUTPUT DSID TO WORK FILE;
*              MOVE TTR ETC TO IDTABLE(R7);
*              RETURN;
*          END;
*     /* LIKE WAS SPECIFIED */
*          MOVE DSID TO WORK;
*          GET THE LIKE ONE INTO TRANS;
*          CLEAR LIKE INDICATOR SO NO LOOP;
*          GO  TO C03TYP9;     /* NOTE, R7 SET CORRECTLY */
*
*C03NEWID: SET R1 TO R0;       /* R0 = EMPTY SPOT IN TABLE*/
*C03STRIT: SET R7 TO R1;
*          ADD 1 TO NOIDS;     /* ADD 1 TO NUMBER OF NAMES IN TABLE */
*          GO  TO C03LIKE;     /* CHECK IF 'LIKE' SPECIFIED, OUTPUT
*                              AND STORE NAME IN TABLE */
*
***** AND THATS IT ****
*
* NOW WE HAD BETTER STORE THIS DD'CARD' AND ENTER ITS NAME
*     INTO THE IDENTIFIER TABLE
          $CALL UJS17FID,DDDSID                                 74256
           LTR R15,R15    WAS IT ALREADY THERE ?
           BNZ  C03STRIT NO,SO MERELY STORE IT
* HERE THE DATA SET IDENTIFIER HAS BEEN DOUBLY DECLARED.
         SPACE 2                                                  75128
         CLI   C03DEXT,C' '   WAS EXTERNAL CODED ?                75128
         BE    C03DOVRD       NO, DO OVERIDE                      75128
         JOLRETN RC=0                                             75128
C03DOVRD EQU   *                                                  75128
         SPACE 2                                                  75128
* THE THING TO NOW IS TO READ THE OLD ONE IN (THE FIRST DECLARE)
* AND OVERWRITE THIS (THE 2ND) DECLARE WITH ANY NON-BLANK INFO
* FROM THE FIRST.
           LR R7,R1            SAVE THE ID ADDRESS FROM THE TABLE
*                              COS WEHN WE RE-WRITE IT,WE WILL
*                              HAVE TO UPDATE THE ADDRESS ON DISC
*                              (OR CORE)
********* HOWEVER, THE LEVEL WE ARE CURRENTLY AT COULD BE A LITTLE
*     HIGHER THAN THE ONE IN THE TABLE,AND THIS (BEING A DECLARE)
*     MUST GO INTO THE TABLE AS A NEW ITEM (AT A HIGHER LEVEL)
           USING IDTABLE,R7
           CLC IDLEVEL,LEVEL+1
           BNE C03NEWID
         TM    IDTYPE,X'03'   IS THIS A DSID ?                    75128
           BO C03OVRD
C03ERR15 EQU   *                                                  75128
         JOLERR 308,'DSID NAME ''',DDDSID,''' WAS PREVIOUSLY DECLARED A*
               T ',IDSTMT,' AND WAS NOT A DSID:- NO MERGE CAN OCCUR'
           JOLRETN RC=16
C03OVRD   EQU  *
           SPACE 2
           MVC WORK(255),DDDSNDET SHIFT TO WORK TO SAVE
          $CALL UJS16GID,WORK+5 ********** UJS16GID CLEARS TRANS  75128
           JOLERR 007,'OVERRIDING/MERGING STATEMENT ',IDSTMT      75311
* AND SO WE HAVE THE FOLLOWING SITUATION:-
*     1.  THE OLD DSID IS IN TRANS
*     2.  THE NEW DSID IS IN WORK
* NOW ANY INFORMATION THAT IS IN THE NEW ONE THAT ISN'T IN THE OLD75128
*  ONE MUST BE SHIFTED TO THE NEWONE                              75128
           LA  R4,TRANS        STILL A USING FOR DDDSNDET
           LA R6,WORK
C03DOLIK EQU   *                                                  75128
           IFVALUE DDDSNAME,C03LDSN            ALREADY HAS VALUE,LEAVE
           MVC DDDSNAME,DDDSNAME-DDDSNDET(R6)
           MVC DDMBR,DDMBR-DDDSNDET(R6)
            MVC  DDTYPE,DDTYPE-DDDSNDET(R6)
C03LDSN    EQU *
C03LMBR    IFVALUE DDVOLUME,C03LVOL            ALREADY HAS VALUE,LEAVE
           MVC DDVOLUME,DDVOLUME-DDDSNDET(R6)
C03LVOL    IFVALUE DDUNIT,C03LUNIT             ALREADY HAS VALUE,LEAVE
           MVC DDUNIT(L'DDUNIT+L'DDUNITNO+L'DDUNITYP),DDUNIT-DDDSNDET(R*
               6)
C03LUNIT   IFVALUE DDPRIM,DDSEC,DDDIRECT,C03LSPC LEAVE AS IS
           MVC DDPRIM(L'DDPRIM+L'DDSEC+L'DDDIRECT+L'DDCYLTRK),DDPRIM-DD*
               DSNDET(R6)
C03LSPC    IFVALUE DDRECFM,C03LRECF           ALREADY HAS VALUE,LEAVE
         MVC   DDRECFM,DDRECFM-DDDSNDET(R6)              JOL30053 76200
C03LRECF IFVALUE DDBLKSZE,C03LBLKS                       JOL30053 76200
         MVC   DDBLKSZE,DDBLKSZE-DDDSNDET(R6)            JOL30053 76200
C03LBLKS IFVALUE DDLRECL,C03LRECL                        JOL30053 76200
         MVC   DDLRECL,DDLRECL-DDDSNDET(R6)              JOL30053 76200
C03LRECL IFVALUE DDCATLGS,C03LCATS                       JOL30053 76200
         MVC   DDCATLGS,DDCATLGS-DDDSNDET(R6)            JOL30053 76200
C03LCATS IFVALUE DDBUFL,C03LBUFL                         JOL30053 76200
         MVC   DDBUFL,DDBUFL-DDDSNDET(R6)                JOL30053 76200
C03LBUFL IFVALUE DDBUFNI,C03LBFNI                                 82300
         MVC   DDBUFNI,DDBUFNI-DDDSNDET(R6)                       82300
C03LBFNI IFVALUE DDBUFND,C03LBFND                                 82300
         MVC   DDBUFND,DDBUFND-DDDSNDET(R6)                       82300
C03LBFND IFVALUE DDVOLSEQ,C03LVSEQ                       JOL30053 76200
         MVC   DDVOLSEQ,DDVOLSEQ-DDDSNDET(R6)            JOL30053 76200
C03LVSEQ IFVALUE DDMAXVOL,C03LMXV                        JOL30053 76200
         MVC   DDMAXVOL,DDMAXVOL-DDDSNDET(R6)            JOL30053 76200
C03LMXV  IFVALUE DDCODE,C03LCODE                         JOL30053 76200
         MVC   DDCODE,DDCODE-DDDSNDET(R6)                JOL30053 76200
C03LCODE IFVALUE DDFUNC,C03LFUNC                         JOL30053 76200
         MVC   DDFUNC,DDFUNC-DDDSNDET(R6)                JOL30053 76200
C03LFUNC IFVALUE DDSIZE(DDSIZEQ-DDSIZE),C03LSIZE         JOL30053 76200
         MVC   DDSIZE(DDSIZEQ-DDSIZE+L'DDSIZEQ),DDSIZE-DDDSNDET(R6) 200
C03LSIZE IFVALUE DDBUFNO,C03LBUF            ALREADY HAS VALUE,LEAVE
         MVC DDBUFNO,DDBUFNO-DDDSNDET(R6)
C03LBUF  IFVALUE DDOPTCD,C03LOPTC           ALREADY HAS VALUE,LEAVE
         MVC DDOPTCD,DDOPTCD-DDDSNDET(R6)
C03LOPTC IFVALUE DDDENS,C03LDENS            ALREADY HAS VALUE,LEAVE
         MVC DDDENS,DDDENS-DDDSNDET(R6)
C03LDENS IFVALUE DDRKP,C03LRKP              ALREADY HAS VALUE,LEAVE
         MVC DDRKP,DDRKP-DDDSNDET(R6)
C03LRKP  IFVALUE DDKEYLEN,C03LKEYL          ALREADY HAS VALUE,LEAVE
         MVC DDKEYLEN,DDKEYLEN-DDDSNDET(R6)
C03LKEYL IFVALUE DDOVERFL,C03LOVRF          ALREADY HAS VALUE,LEAVE
         MVC DDOVERFL,DDOVERFL-DDDSNDET(R6)
C03LOVRF IFVALUE DDDSORG,C03LDSRG           ALREADY HAS VALUE,LEAVE
         MVC DDDSORG,DDDSORG-DDDSNDET(R6)
C03LDSRG IFVALUE DDLABEL,C03LLAB            ALREADY HAS VALUE,LEAVE
         MVC DDLABEL,DDLABEL-DDDSNDET(R6)
C03LLAB  IFVALUE DDAEXTRA,C03LPREF          ALREADY HAS VALUE,LEAVE
         MVC DDAEXTRA,DDAEXTRA-DDDSNDET(R6)
C03LPREF EQU    *           IFVALUE DDRETPD,C03LRTP                DASD
*        MVC   DDRETPD,DDRETPD-DDDSNDET(R6)                        DASD
C03LRTP  IFVALUE DDEXPDT,C03LEXP                                  75128
*DDRETPD AND DDEXPDT ARE SAME REALLY--MUST MOVE RETPD IF EXPDT     DASD
         MVC   DDRETPD,DDRETPD-DDDSNDET(R6)                        DASD
         MVC   DDEXPDT,DDEXPDT-DDDSNDET(R6)                       75128
C03LEXP  EQU   *                                                  75128
* THERE ARE A FEW THINGS THAT NEED COPYING BUT IN A 'BIT' FASHION 75128
         OC    DDSPECT2,DDSPECT2-DDDSNDET(R6)                     75128
         OC    DDSPECTP,DDSPECTP-DDDSNDET(R6)                     75128
         OC    DDUNITQU,DDUNITQU-DDDSNDET(R6)                     82200
         IFVALUE DDLABTYP,C03LTYP                                 75128
         MVC   DDLABTYP,DDLABTYP-DDDSNDET(R6)
C03LTYP  EQU   *
*
* NOW CHECK IF 'DCB-EXTRA' WAS CODED ON OLD, BUT NOT ON NEW,AND   75128
*        COPY IF SO                                               75128
         CLC   DDOSVB,=AL2(DDLEN)  LONGER THAN USUAL ?            75128
         BH    C03LEXTR            YES,LEAVE EXTRA                75128
         CLC   DDOSVB-DDDSNDET(R6),=AL2(DDLEN) IS OLD RECORD LONG?75128
         BNH   C03LEXTR            NO, BOTH RECORDS ARE SHORT     75128
         MVC   DDOSVB,DDOSVB-DDDSNDET(R6) SWAP LENGHTS            75128
         MVC   DDEXTRA,DDEXTRA-DDDSNDET(R6) SHIFT 'EXTRA' INFO    75128
C03LEXTR EQU   *                                                  75128
C03WRITE   EQU *
C03WRIT2 EQU   *                                                  75128
         CLI   C03DLIKE,C' '  WAS LIKE CODED ?                    75128
         BE    C03TEXTA       NOPE,-> TEST EXTERNAL NOW           75128
         MVC   WORK(250),DDDSNDET SAVE OLD ONE                    75128
        $CALL  UJS16GID,C03DLIKE GET THE 'LIKE' ONE               75128
         LTR   R15,R15        WAS IT THERE ?                      75128
         BZ    C03TTYP9       YES, CHECK THAT IT IS A DS DCL NOW  75128
         MVC   ERRNUM1,C03DLIKE   JOLERR CAN'T USE BASE 13,MOVE IT80290
         CLEAR ERRNUM2                                            80290
         MVC   ERRNUM2(3),C03DLIKE+L'ERRNUM1                      80290
         JOLERR 304,'CANNOT FIND LIKE DSID ''',ERRNUM1,ERRNUM2,''''
         CLEAR  C03DLIKE    SO WE WON'T LOOP                      75128
         LA  R4,WORK    SWITCH TO WORK FOR OUTPUT                 75128
         B     C03TEXTA       STILL STORE IT THOUGH               75128
         SPACE 2                                                  75128
C03TTYP9   EQU *                                                  75311
        CLEAR  C03DLIKE    SO WE WON'T LOOP                       75311
        LA   R4,WORK                                              75311
        LA   R6,TRANS                                             75311
         TM    IDTYPE-IDTABLE(R1),X'03'   IS THIS A DSID ?        75311
         BO    C03DOLIK       NOW GO DO THE 'LIKE' THING          75128
         B     C03ERR15       NOPE, ERROR, RETURN                 75128
         SPACE 2                                                  75128
C03TEXTA CLI   C03DEXT,C' '   EXTERNAL CODED ?                    75128
         BE    C03NOTEX       NOPE                                75128
         MVI   DDLEVEL,0      SET LEVEL TO 0                      75128
C03NOTEX EQU   *                                                  75128
* NOW JUST BEFORE THE DSID, SEE IF SUBSYS WAS CODED, AND WRITE    87150
* IT OUT IF NECESSARY. LONG VOLUMES WILL BE DONE LATER.           87150
         IFNULL #DDSUBS,C03NOMOR  NO EXTRA SEGMENTS TO GO OUT     87150
         LH    R15,#DDSUBS     GET LENGTH OF ITEM                 87150
         LA    R15,#DDSUBS-DDLENXTR+2(,R15) + LENGTH OF PREFIX    87150
         STH   R15,DDLENXTR    SET LENGTH FOR WRITE ROUTINE       87150
         $CALL UJS15OP,DDLENXTR                                   87150
          LTR R15,R15
          BNZ C03RETN0    COULDN'T STORE IT,ERROR MESSAGE WILL
*                         HAVE BEEN GIVEN
          ST  R1,DDAEXTRA      SAVE ADDRESS IN DDDSNDETS          87150
          SPACE 3
C03NOMOR DS    0H                                                 87150
* NOW JUST BEFORE OUTPUTTING THE DSID, CHECK IF 'DUMMY' WAS
* CODED BEFORE A DSNAME (DDTYPE COULD BE SET INCORRECTLY)
           CLC  =C'*DUMMY',DDDSNAME
           BNE  *+8
           MVI  DDTYPE,DDWORKDS    SET NON REL GEN(DONT CARE WHAT
*                                  IT IS AS LONG AS ITS NOT REL
*                                  GENERATION)
          $CALL UJS15OP,DDDSNDET                                74256
          LTR R15,R15
          BNZ C03RETN0    COULDN'T STORE IT,ERROR MESSAGE WILL
*                         HAVE BEEN GIVEN
           ST R1,IDADDR
           MVC IDSTMT,STMT
           MVC IDNAME,DDDSID
         MVI   IDTYPE,X'03'   TURN ON DSID INDICATOR              75128
         CLI   DDVOLUME,C'-'  VOLREF ? IF SO WE'D BETTER ADVISE   75128
         BNE   *+8            - SOMEONE LATER ON.                 75128
         OI    IDTYPE,X'04'   TURN ON VREF INDICATOR              75128
         MVC   IDLEVEL,DDLEVEL                                    75128
           MVC IDNEST,NEST+1
           MVI IDUSED,0
C03RETN0   JOLRETN
           SPACE 2
C03NEWID   LR    R1,R0            NEXT SPOT IN TABLE ENTRY
          SPACE 3
C03STRIT   LA    R4,DDDSNDET
           LR    R7,R1           SAVE ID TABLE ADDRESS
           LH    R1,NOIDS
           LA    R1,1(R1)
           STH   R1,NOIDS
           B     C03WRITE         STORE THE DSID AWAY FOR LATER USE
           TITLE 'SCAN FOR KEYWORDS AND DECODE'
FINDKEY    EQU *
           GETTKN LOCN=TKNCURR                 GET THE NEXT TOKEN
           CLC   TKNCURR,TKNNO
           BH    C
           STH   R1,TKNCURR
           CLI   TKN,C','                        IS IT A COMMA?
           BE    FINDKEY
         CLI   TKN,C')'       LEFT OVER ?                         75128
         BE    FINDKEY        YES,SKIP IT                         75128
         CLI   TKN,C'='       EQUAL LEFT OVER            JOL30079 76200
         BE    FINDKEY                                   JOL30079 76200
           TITLE 'TEST FOR A KEY-WORD'
           LR  R5,R1           SET R5=TKNNO OF NEXT TOKEN
           BCTR R5,0           NOW R5=TKNNO OF THIS TOKEN
           CLI TKNTYPE,0       IS THIS TOKEN NUMERIC ?            74256
           BE  C03NUM01        YES,SO IT CANT BE A DSN WITH NO KEY74256
         CLI   TKN,C''''      DSN CODED AS 'DSNAME'?              75311
         BE    C03QDSN        YES, GO STORE R1, THEN START DSN    75311
           CLI TKNTYPE,2       IS THIS TOKEN A CHARACTER ?        74256
           BNE C03NOTSP        NO,MUST BE A LITERAL OR SPECIAL    74256
*                              SO KICK IT OUT                     74256
          GETTKN REG=R1
         CLC   =C'||',TKN     DSN TYPE A||B.C ?                   75311
         BE    C03DSNNK       YES                                 75311
          CLI TKN,C'.'
           BNE C03NOTSP        NO,TEST IF A KEYWORD               74256
* HERE IT IS A DSN WITH NO KEYWORD
C03DSNNK EQU   *                                                  75311
           GETTKN (R5)
C03QDSN  EQU   *                                                  75311
          STH R1,TKNCURR
          B STARTDSN
C03NUM01   LR  R8,R5           R8=TKNNO OF *THIS* NUMERIC TOKEN   74256
         MVC   #WORK(20),#TKN SAVE THIS NUMERIC TOKEN             75128
           GETTKN (R1)         GET TOKEN AFTER NUMBER             74256
           CLC =C'PART ',TKN   <NUMBER> PART ?                    74256
           BNE C03TCOPY        NO,TEST IF 'COPIES' FOR PRINTER    74256
* PART NUMBER HERE                                                74256
           STH R1,TKNCURR      STORE NUMBER TO SKIP 'PART' LATER  74256
         CLI   DDTYPE,DDSYSOUT SYSOUT ?                           75128
           BNE C03SYS1E        ERROR                              74256
         JOLSTOR 'PART NUMBER',DDSYSFRM,#WORK                     75128
         GETTKN TKNCURR        GET NEXT TOKEN
         CLC =C'STATIONERY ',TKN
         BE   SKIP1
         CLC  =C'PAPER ',TKN
         BE   SKIP1
           B   C               BACK TO MAIN PROCESSING            74256
C03TCOPY   CLC =C'COPIES ',TKN <NUMBER> COPIES ?                  74256
         BNE   C03TREC        NO, TEST RECORDS THEN               75128
           STH R1,TKNCURR      STORE SO WE SKIP 'COPIES' LATER    74256
         CLI   DDTYPE,DDSYSOUT SYSOUT ?                           75128
           BNE C03SYS1E        NO,CAN'T HAVE 'COPIES' THEN        74256
DSCOPIES JOLSTOR 'COPIES',DDCOPIES,#WORK,NUM                      75128
           B   C               AND ON TO MAIN LINE                74256
C03SYS1E   GETTKN (R1)                                            74256
           B   C03PRONL                                           75311
           SPACE
* NOW TEST IF  NK | N-NZ RECORDS | 2400-3 TYPE UNIT               75128
C03TREC  EQU   *                                                  75128
* IN FACT BEFORE TESTING RECORD NUMBERS, WE WILL TEST
* THAT HE DIDN'T CODE 3 UNITS
         B     C03TREC2       ********                            75128
         CLC =C'UNITS ',TKN
         BNE  C03TREC2
         MVC #TKN(30),#WORK
         B   DSUNITNO        GO STORE UNIT NUMBERS
C03TREC2 EQU *
         CLC   =C'K ',TKN     20K (RECORDS?)                      75128
         BE    C03RECS                                            75128
         CLC   =C'BYTES ',TKN 2000 BYTES (RECORDS | SIZE)         75128
         BE    C03RECS                                            75128
         CLC   =C'RECS ',TKN                                      75128
         BE    C03RECS                                            75128
         CLC   =C'RECORDS ',TKN                                   75128
         BE    C03RECS                                            75128
         CLI   TKN,C'-'       1000-3000 RECS  | 2400-3 ?          75128
         BNE   C03TBPI        NO, TEST BPI THEN....               75128
         LA    R1,1(R1)       SKIP NEXT ITEM                      75128
         GETTKN (R1)          NOW, IF ITS NOT 'RECORDS' | 'RECS'
*                             WE WILL ASSUME THAT IT IS A UNIT
*                             FOR NOW, BUT IT WILL BE PROPERLY
*                             VALIDATED LATER, ANYWAY
         CLC   =C'K ',TKN     20K (RECORDS?)                      75128
         BE    C03RECS                                            75128
         CLC   =C'BYTES ',TKN 2000 BYTES (RECORDS | SIZE)         75128
         BE    C03RECS                                            75128
         CLC   =C'RECS ',TKN                                      75128
         BE    C03RECS                                            75128
         CLC   =C'RECORDS ',TKN                                   75128
         BE    C03RECS                                            75128
* HERE WE MUST TEMPORARILY ASSUME A UNIT OF THE TYPE '2400'       75128
         LA    R1,1(R8)       GET THE '-'                         75128
         GETTKN (R1)                                              75128
         BAL   R14,CONCAT     CONCAT TO #WORK
         GETTKN (R1)          GET THE THING AFTER '-'             75128
         BAL   R14,CONCAT     CONCATENATE IT AS WELL              75128
         SPACE 1
         MVC   #TKN(30),#WORK
         STH   R1,TKNCURR     RESET R1 INTO TKNCURR               75128
         B     C03TKEYW                                           75128
         SPACE 2
C03TBPI    CLC =C'BPI ',TKN    BPI ?                              74256
         BNE   C03TDAYS                                           75128
           STH R1,TKNCURR      STORE R1 TO IGNORE 'BPI' NEXT TIME 74256
           LA  R1,C03BPITB     LA IN R1 OF START OF TABLE         74256
           LA  R14,6           AND 14=INCREMENT                   74256
           LA  R15,C03BPITE-6  AND END OF TABLE-6                 74256
           SPACE 1                                                74256
           CLC WORK(5),0(R1)   CHECK BPI NUMBER                   75311
           BE  C03BPIOK        GOT IT ->                          74256
           BXLE R1,R14,*-10    LOOP                               74256
         JOLERR 310,'BPI ',#WORK,' IS INCORRECT:S/B 800,1600 OR 6250'
           B   C               BACK TO PROCESSING LOOP            74256
C03BPITB DC    CL5'800  ',C'2',C'1600 ',C'3',C'6250 ',C'4'        75128
C03BPITE   EQU *                                                  74256
C03BPIOK   EQU *                                                  74256
           MVC TKN(1),5(R1)    SHIFT IN CODE                      74256
           MVI #TKN+1,1        SET LENGTH TO '1'                  74256
           JOLSTOR 'BPI',DDDENS,#TKN  STORE BPI CODE              75311
           B   C
         SPACE 3
C03TDAYS CLC   =C'DAYS ',TKN  DAYS?                               75128
         BE    C03DAYS             -> YES                         75128
         CLC   =C'DAY ',TKN   1 DAY ?                             75128
         BNE   C03TSIZE            -> NO                          75128
C03DAYS  STH   R1,TKNCURR     HERE IT IS RETPD                    75128
         GETTKN (R8)          GET THE NUMBER AGAIN                75128
C03STRTP JOLSTOR 'RETENTION PERIOD',DDEXPDT,#WORK,NUM             76200
         LTR   R15,R15                                   JOL30055 76200
         BNZ   C                                         JOL30055 76200
         MVI   DDRETPD,C'R'                              JOL30055 76200
         B     C              BACK TO MAIN LINE                   75128
         SPACE 3                                                  75128
C03TSIZE CLC   =C'K ',TKN     DID USER SAY LOOK (STORAGE REQ)     75128
         BNE   C03TSPC2       NOPE                                75128
         STH   R1,TKNCURR     YES,RESET TKN CURR                  75128
         GETTKN (R8)          GET THE NUMBER AGAIN                75128
         B     DSSIZE         GO STORE IT                         75128
C03TSPC2   EQU  *
           LA  R15,C03SPTBL    SPACE TABLE CONSTANTS              74256
C03TSPC4   CLC TKN(L'C03SPTBL),0(R15) IS IT A SPACE KEYWORD ?     74256
           BE  C03ISPC                YES ->                      74256
           LA  R15,L'C03SPTBL(R15) ADD TO LOOP                    74256
           C   R15,=A(C03SPTBE)    REACHED END OF TABLE ?         74256
           BL  C03TSPC4            NO,CHECK NEXT ENTRY            74256
* SORRY,BXLE NOT POSSIBLE,NO REGISTERS                            74256
* NOW CHECK IF NEXT TOKEN IS A COMMA FOLLOWED BY A <NUMBER>,
*     FOLLOWED BY 'TRKS' ETC AGAIN
           CLI TKN,C','
           BNE C03NOTSP        NO,SO PREVIOUS NUMBER NOT SPACE
C03N2NDS   GETTKN (R1)                                            75311
           CLI  TKN,C','                                          75311
           BE   C03N2NDS                                          75311
           CLI TKNTYPE,0       <NUMBER>
           BNE C03NOTSP        NOT A SPACE LIKE 1,2TRKS WITHOUT KEYWORD
           GETTKN (R1)         IT IS MAYBE HERE COS WE HAVE <NO>,<NO>
*                              SO GET THE 'TRKS' HOPEFULLY
           B   C03TSPC2        -> GO TEST NOW FOR 'TRKS' ETC
           SPACE 3
C03KEYWD EQU   *                                                  75128
C03NOTSP   EQU  *
* NOT SPACE WITHOUT A KEYWORD
           GETTKN REG=R5       GET ORIGINAL NUMBER AGAIN
C03TKEYW   EQU *
         MVC   DBL(8),TKN      COPY SO BIN SEARCH CAN GET IT      86210
         $CALL C03FKEY         FIND THE KEYWORD IN THE TABLE      86210
         LTR   R15,R15         GOT IT?                            86210
         BNZ   GOTKEYWD        YES                                86210
           SPACE 3
         L     R1,AJOLGEN                                         75128
         L     R1,AGENUNIT-UJGENDET(R1)                           75128
           LM R14,R15,4(R1)
           L  R1,0(R1)
TGENU      CLC TKN(8),0(R1)
           BE  GOTGENU
           BXLE R1,R14,TGENU
* NOW CHECK FOR A PREFERRED VOLUME                                75128
**** THIS WAS BEFORE THE GENERIC UNIT CHECK, BUT AS THE UNIT      75128
*  WILL  BE CODED MORE OFTEN THAN THE VOLUMES, WE HAVE MOVED      75128
*  IT TO SAVE  CPU TIME                                           75128
         SPACE 1                                                  75128
        L   R1,AJOLGEN                                            75128
         L     R1,APREFVOL-GENDETS(R1)                            75128
         LM    R14,R15,4(R1)                                      75128
         L     R1,0(R1)                                           75128
C03TPREF CLC   TKN(6),0(R1)                                       75128
         BE    GOTPREFV                                           75128
         BXLE  R1,R14,C03TPREF                                    75128
         SPACE 2
*************** NOW CHECK THE DEVICE NAME TABLE **************
*
* CHANGED TO A CALL OF UJC03DEV IN DECEMBER, 1983 FOR XA SUPPORT
*
         $CALL UJC03DEV,(TKN,DDDSNDET) EITHER SEARCH DEVNAMET        XA
*                                      OR CALL XA LOOK UP            XA
         LTR   R15,R15        WAS IT A DEVICE OR UNIT NAME?          XA
         BZ    C03DEVUN       YEP                                    XA
*
* OK,GIVE CONTROL TO A USER EXIT
         SPACE 2                                                  75128
* OK NOW WE REALLY HAVE A PROBLEM COS WE HAVEN'T A CLUE WHAT
*    IT IS BUT WE SHALL PRESS ON REGARDLESS.
WHATISIT   EQU *
* NOW WE HAVE A WORD THAT WE DON'T UNDERSTAND SO CALL THE USER
* TO DO AS HE WISHES
           SPACE
C03USER    EQU *
         L     R15,ACOMPEXI   GET COMPILER EXIT ROUTINE ADDRESS   75128
         L     R15,8(R15)                                         75128
           LTR R15,R15
           BZ C03ERR
         $CALL (15),(CALLAREA,TKNX,DDDSNDET)                      75128
           LTR R15,R15
         BNZ   C03ERR         EXIT DIDN'T FIX IT                  75128
* NOW WE MUST SEARCH THE TABLE (KEYWORD) AND TRY AND FIND         75128
*  THE KEYWORD THE USER GAVE BACK TO US                           75128
         $CALL C03FKEY         FIND THE KEYWORD IN THE TABLE      86210
         LTR   R15,R15         GOT IT?                            86210
         BNZ   GOTKEYUS        YES                                86210
* WELL, ERROR HERE I'M AFRAID                                     75128
         B     C03INV
         B     C                                                  75128
GOTKEYUS EQU *
       LH  R15,TKNCURR
       BCTR R15,0
       STH  R15,TKNCURR
       B    GOTKEYWD
C03ERR     EQU *
         MVC   #WORK(L'TKN),#TKN                                  75128
C03NERR2 GETTKN TKNCURR                                           75128
         CLI   TKN,C'='                                           75128
         BE    C03CONEQ                                           75128
         CLI   TKN,C'('                                           75128
         BE    C03CONEQ                                           75128
C03INV   CLI   WORK,C'*'
         BE    C
         JOLERR 301,'''',#WORK,''' IS NOT A VALID KEYWORD:-IGNORED'
         B     C              BACK TO MAINLINE                    75128
         SPACE 1                                                  75128
C03CONEQ BAL   R14,CONCAT                                         75128
C03CONE2 GETTKN (R1)                                              75128
         BAL   R14,CONCAT                                         75128
         CLI   TKN,C'('                                           75128
         BE    C03CONE2                                           75128
         STH   R1,TKNCURR                                         75128
         B     C03NERR2                                           75128
         SPACE 2                                                  75128
GOTKEYWD   EQU *
* PREVIOUSLY, THERE USED TO BE TWO TABLES, ONE CONTAINING         75128
*  KEYWORDS SUCH AS VB (IE NO KEYWORD EXPECTED AFTER) AND         75128
*  KEYWORDS SUCH AS DSN.                                          75128
* THE NEW TABLE CONTAINS AND INDICATOR TO SAY WHETHER WE HAVE     75128
*  TO FIND THE VALUE AFTER THE KEYWORD, OR NOT                    75128
         LR    R1,R15         COPY TABLE POSITION TO R1           86210
         MVC   #WORK(L'TKN+2),#TKN SAVE KEYWORD FOR ERR-MSSGE     75128
         MVC   DBL(2),C03LA10 MOVE LA R10 INSTRUCTION TO DBL      75128
         MVC   DBL+2(2),10(R1) SHIFT IN S(RTN)                    75128
         EX    R0,DBL         LOAD R10 WITH RTN                   75128
         SPACE 1
* NOW WE HAD BETTER CHECK OUR INDICATORS TO SAY WHETHER THIS      75128
*  KEYWORD WE HAVE GOT IS EXPECTING A VALUE AFTER IT OR NOT       75128
         SPACE 1                                                  75128
         ST    R1,DBL         SAVE POINTER TO TABLE FOR NOW       75128
         TM    8(R1),X'01'    IF 0 MEANS NO VALUE AFTER KEYWORD   75128
         BZ    C03BALR        NO VALUE, JUST BRANCH TO ROUTINE    75128
           GETTKN LOCN=TKNCURR
           CLI TKN,C'='
           BNE GOTTOKEY
           GETTKN REG=R1
GOTTOKEY    EQU *
         CLI   TKN,C'('       '(' FOLLOWING KEYWORD ?             75128
         BNE   C03GOTRK       NO,USE WHATS HERE                   75128
         GETTKN (R1)          YES, SKIP IT                        75128
C03GOTRK EQU   *                                                  75128
           STH R1,TKNCURR
C03BALR  EQU   *                                                  75128
         L     R1,DBL         RELOAD POINTER TO TABLE ENTRY       75128
         TM    8(R1),X'04'    SYSOUT ONLY KEYWORD ?               75128
         BZ    C03NSYS9       NOPE                                75128
         CLI   DDTYPE,DDSYSOUT IS THIS A SYSOUT DECLARE ?          SOCO
         BL    C03PRONL       NOPE,BALR                            SOCO
         CLI   DDTYPE,DDSPECOU IS THIS A SPECIAL SYSOUT ?          SOCO
         BNH   C03BALR2        YES, I AM ONE OF THEM, SO BRANCH    SOCO
C03PRONL JOLERR 311,'''',#WORK,'=',#TKN,''' IGNORED:- FOR SYSOUT ONLY'
         B     C              BACK TO MAIN-LINE                  75128
         SPACE 1                                                  75128
C03NSYS9 TM    8(R1),X'08'    KEY ONLY FOR NON-SYSOUT DATA SET ?  75128
         BZ    C03BALR2                                           75128
         CLI   DDTYPE,DDSYSOUT  WELL, IS THIS A SYSOUT DATA SET?  75128
         BH    C03BALR2         NOPE, GO TO ROUTINE               75128
         JOLERR 312,'''',#WORK,'=',#TKN,''' IGNORED:- FOR',       75128*
               ' NON-SYSOUT FILES ONLY'                           75128
         B     C
C03BALR2 EQU   *
         LTR   R15,R10                                            75128
           BZ  C03NOTIM        NOT YET IMPLEMENTED MESSAGE,MATE
         MVC   #WORK(L'TKN+2),#TKN                                75128
            BALR R14,R15
           B C
         SPACE 3
GOTGENU    MVC TKN(8),8(R1) SWAP UNIT NAMES
           MVC DDUNITYP,16(R1) MOVE IN UNIT TYPE
C03DEVUN EQU   *              UNIT FOUND IN DEVNAMET                 XA
C03SET8    MVI #TKN+1,8        SET LENGTH TO 8                    74256
           B   DSUNIT
C03ISPC    GETTKN REG=R8
           B  DSSPACE
           SPACE
GOTPREFV   EQU *
           MVC DDUNITYP,6(R1)  MOVE IN UNIT TYPE
           B   DSVOL
C03NOTIM   JOLERR 309,'''',#WORK,' ',#TKN,''' IS NOT YET IMPLEMENTED'
           B   C
           TITLE 'DSNAME SET UP'
DSDSN      EQU *
DSDSNAME   EQU *
DSNAME     EQU *
STARTDSN   EQU *
           SPACE 3
* NOW WE MUST FORM UP THE FULL DSNAME.EACH PART IS SEPERATED
*    BY A '.' OR THERE MAY BE'( )' AFTER
           MVC CALLAREA(2),=C'||'                                 75311
           $CALL UJS60DSN      PROCESS DSNAME                     75311
           MVC DDPASSW,WORK+120 SHIFT PASSWORD IN                 80290
           B   C03DSRET(R15)                                      75311
C03DSRET   B   C03DSN1         STRAIGHT DSNAME                    75311
           B   C03DSN2         DSNAME + GENERATION NUMBER         75311
           B   C03DSN3         DSNAME + MEMBER                    75311
           B   C03DSN4         ERROR IN GENERATION NUMBER         75311
           B   C03DSN16        INVALID DSNAME                     75311
C03DSN1    BAL R10,C03STRDS    STORE DSN,GO TO C IF NOT STORED    75311
           MVC  DDMBR,WORK+100
           MVI DDTYPE,DDREALDS SET UP REAL DSN INDICATOR          75311
           CLI WORK,C'&&'    IS IT A TEMPORARY DSNAME ?           75311
           BNE C                                                  75311
           MVI DDTYPE,DDTEMPPS YES, SET TEMPORARY INDICATOR       75311
           B   C                                                  75311
           SPACE 3                                                75311
C03DSN2    BAL R10,C03STRDS    STORE DSN,GO TO C IF ERROR         75311
           MVC DDMBR,WORK+100                                     75311
           MVI DDTYPE,DDGDGREL SET RELATIVE FLAG                  75311
           CLC DDMBR+2(4),=C'0000' 0 GEN (IE RELATIVE)            75311
           BE  C               YES -> C                           75311
           CLI DDMBR+1,C' '    IS RELATIVE IND SET (+ | -)        75311
           BNE C               YES -> C                           75311
           MVI DDTYPE,DDGDGABS ABSOLUTE HERE, SET IT              75311
           B   C               GO TO C                            75311
           SPACE 3                                                75311
C03DSN3    BAL R10,C03STRDS    STORE DSN, GO TO C IF ERROR        75311
          MVI DDTYPE,DDMEMSPC  TURN ON REAL DSN WITH MEMBER       75311
           MVC DDMBR,WORK+100                                     75311
           CLI DDDSNAME,C'&&'  TEMPORARY DSNAME ?                 75311
           BNE C                                                  75311
           MVI DDTYPE,DDTEMPPO TURN ON TEMPORARY WITH MEMBER      75311
           B   C                                                  75311
C03DSN4    EQU *                                                  75311
GENERR   JOLERR 303,'ERROR IN GENERATION NUMBER'
           B   C
C03DSN16   JOLERR 313,'ERROR IN DSNAME'                           75311
           B   C                                                  75311
C03STRDS   CLI   DDDSNAME,C'%'      NODSN ALREADY CODED??
           BE    C
           JOLSTOR 'DSNAME',DDDSNAME,#WORK
           BR  R10
           SPACE 3
           TITLE 'DECODE DCB'
DSFIXED  EQU   *                                                  75128
DSUNDEF  EQU   *                                                 75128
DSVARIAB EQU   *                                                  75128
DSVAR    EQU   *                                                  75128
DSUNDEFI EQU   *                                                  75128
         MVI   #WORK+1,1      SET LENGTH INITIALLY TO ONE         75128
         MVC   WORK(1),TKN    SHIFT IN 1 CHAR (F OR V)            75128
C03NEXTZ GETTKN TKNCURR       GET NEXT TOKEN                      75128
         CLC   =C'BLOCKED ',TKN                                   75128
         BE    DSRECADD                                           75128
         CLC   =C'SPANNED ',TKN                                   75128
         BE    DSRECADD                                           75128
         CLC   =C'B ',TKN                                         75128
         BE    DSRECADD                                           75128
         CLC   =C'S ',TKN                                         75128
         BE    DSRECADD                                           75128
         CLC   =C'ASA ',TKN                                       75128
         BE    DSRECADD                                           75128
         CLC   =C'MACHINE ',TKN                                   75128
         BE    DSRECADD                                           75128
         CLC   =C'A ',TKN                                         75128
         BE    DSRECADD                                           75128
         CLC   =C'M ',TKN                                         75128
         BE    DSRECADD                                           75128
         JOLSTOR 'RECORD FORMAT',DDRECFM,#WORK                    75128
           OI  DDSPECT2,X'20'  TURN ON NODEFAULT BIT BECAUSE      75311
*                              RECFM WAS SPECIFIED BY ITSELF      75311
         B     C03TLNO                                            75311
DSRECADD STH   R1,TKNCURR     RESET TKN-CURRENT                   75128
         MVI   #TKN+1,1       SET LENGTH TO 1                     75128
         BAL   R14,CONCAT                                         75128
         B     C03NEXTZ       GET NEXT ITEM (IF ANY)              75128
ISARECFM   JOLSTOR 'RECORD FORMAT',DDRECFM,#TKN
C03TLNO     LTR R15,R15
           BNZ C
           GETTKN LOCN=TKNCURR                 GET THE NEXT TOKEN
           CLI TKNTYPE,0                       NUMERIC
           BNE C                               NOT NUMERIC,SO BUZZ OFF
****************************************************************** DASD
* PROBLEM HERE FOLKS IF THE CURRENT TOKEN IS NUMERIC AFTER       * DASD
* A RECORD FORMAT- YOU SEE THAT WE JUST HAPPEN TO HAVE SOME      * DASD
* KEY WORDS WHICH HAVE NUMERICS PRECEEDING THEM.  SOO AT THIS    * DASD
* POINT I CANNT REALLY TELL YOU WHO BELONGS WITH WHOM.           * DASD
* WHAT WE HAVE TO DO IS CHECK AHEAD 1,2 OR 3 TOKENS FOR KEY      * DASD
* WORDS WHICH MIGHT CAUSE US PROBLEMS                            * DASD
* THE FOLLOWING WAS REPORTED AS A PROBLEM                        * DASD
*                                                                * DASD
* DCL BUG DS WHY.ME RECFM=F 1,1 TRACKS;                          * DASD
* JOL SAID THAT 'TRACKS' WAS AND INVALID KEY WORD ----OK?        * DASD
****************************************************************** DASD
         STH    R1,TKNCURR         SAY CURRENT GUY                 DASD
         BCTR   R1,0           BACKOFF CURRENT TOKEN IN CASE OF A HDASD
         STH    R1,C03STKN         SAVE CURRENT TOKEN NUMBER       DASD
         LA     R10,C03FDKEY       BRANCH TO HERE ON A HIT         DASD
         GETTKN LOCN=TKNCURR       LETS LOOK AT NEXT GUY           DASD
         CLI    TKNTYPE,0          NUMERIC                         DASD
         BE     C03ITSOK           GOOD MOVE ON                    DASD
         CLI    TKN,C','           KEY WORD YET??                  DASD
         BE     C03LKEY2           NOPE LOOK AGAIN                 DASD
* SINGLE GUYS HERE--ONLY ONE TOKEN AHEAD OF IT                     DASD
         CLC    =C'PART ',TKN                                      DASD
         BER R10                                                   DASD
         CLC    =C'COPIES ',TKN                                    DASD
         BER R10                                                   DASD
         CLC    =C'UNITS ',TKN                                     DASD
         BER R10                                                   DASD
         CLC    =C'K ',TKN                                         DASD
         BER R10                                                   DASD
         CLC    =C'- ',TKN                                         DASD
         BER R10                                                   DASD
         CLC    =C'BYTES ',TKN                                     DASD
         BER R10                                                   DASD
         CLC    =C'RECS ',TKN                                      DASD
         BER R10                                                   DASD
         CLC    =C'RECORDS ',TKN                                   DASD
         BER R10                                                   DASD
         CLC    =C'BPI ',TKN                                       DASD
         BER R10                                                   DASD
         CLC    =C'DAYS ',TKN                                      DASD
         BER R10                                                   DASD
         CLC    =C'DAY ',TKN                                       DASD
         BER R10                                                   DASD
* OK NOW LOOK AT THE SPACE PARAMS    TRK, CYLS,....ECT             DASD
         LA     R15,C03SPTBL                                       DASD
C03SPL1  CLC    TKN(L'C03SPTBL),0(R15)                             DASD
         BER R10                                                   DASD
         LA     R15,L'C03SPTBL(R15)    INCR TO NEXT GUY            DASD
         C      R15,=A(C03SPTBE)       END??                       DASD
         BL     C03SPL1                                            DASD
         B      C03ITSOK               OK---ITS A GOOD LRECL/BLKSIZDASD
C03LKEY2 EQU    *    OK LETS LOOK AT NEXT TOKEN                    DASD
         GETTKN (R1)                                               DASD
         CLI    TKN,C','                                           DASD
         BER R10      TWO COMMAS IN A ROW ? 1,,1 TRKS ?            DASD
         CLI    TKNTYPE,0      NUMERIC??                           DASD
         BE     C03LKEY3       YES GO LOOK AGAIN                   DASD
C03CKTN2 EQU    *                                                  DASD
* DOUBLE TOKNES HERE                                               DASD
         CLC    =C'K ',TKN                                         DASD
         BER R10                                                   DASD
         CLC    =C'- ',TKN                                         DASD
         BER R10                                                   DASD
         CLC    =C'BYTES ',TKN                                     DASD
         BER R10                                                   DASD
         CLC    =C'RECS ',TKN                                      DASD
         BER R10                                                   DASD
         CLC    =C'RECORDS ',TKN                                   DASD
         BER R10                                                   DASD
         LA     R15,C03SPTBL                                       DASD
C03SPL2  CLC    TKN(L'C03SPTBL),0(R15)                             DASD
         BER R10                                                   DASD
         LA     R15,L'C03SPTBL(R15)    INCR TO NEXT GUY            DASD
         C      R15,=A(C03SPTBE)       END??                       DASD
         BL     C03SPL2                                            DASD
         B      C03ITSOK               OK---ITS A GOOD LRECL/BLKSIZDASD
C03LKEY3 EQU   *                                                   DASD
         GETTKN (R1)                                               DASD
         CLI    TKNTYPE,0                                          DASD
         BNE    C03CKTN2                                           DASD
         B      C03ITSOK    DONT KNOW WHAT THIS IS BUT IM GONE     DASD
C03FDKEY EQU    *                                                  DASD
         LH     R1,C03STKN         GET BACK OLD TOKEN NUMBER       DASD
         STH    R1,TKNCURR         SAY THIS IS THE CURRENT TOKEN   DASD
         B      C                  RETURN TO THE MAIN LAND         DASD
C03ITSOK EQU    *                                                  DASD
         LH     R1,C03STKN     RESTORE CURRENT TOKEN               DASD
         GETTKN (R1) RESTORE #TKN AS BEFORE THIS MESS              DASD
* THE NEXT TOKEN AFTER THE RECFM KEYWORD IS A NUMBER SO WE SHALL
*    HAVE A FIDDLE WITH IT
           STH R1,TKNCURR
           TNUM    #TKN
           LTR R15,R15
         BNZ   BLKERR                                             75128
         CH    R1,=H'32767'                                       75128
           BH  BLKERR
         LR    R10,R1         SAVE NUMBER                         75128
           GETTKN LOCN=TKNCURR                 SEE IF A COMMA FOLLOWS,
           CLI     TKN,C','                    IE LRECL (OR BLKSIZE)
           BE  ISLRECL
ONLYONE  EQU   *                                                  75128
           STH  R10,DDLRECL                                       80290
           CLI  DDRECFM,C'U'   RECFM U?
           BNE  C              NO,BACK TO MAIN LINE
           MVC  DDBLKSZE,DDLRECL
           CLEAR DDLRECL
           B   C                               CONTINUE PROCESSING
ISLRECL    EQU *
           STH R1,TKNCURR
           GETTKN LOCN=TKNCURR                 GET THE NEXT NUMBER(?)
           CLI TKNTYPE,0
         BNE   ONLYONE        ERROR WILL GET PICKED UP ELSEWHERE  75128
LRECSIZE   EQU *
           STH R1,TKNCURR
           TNUM    #TKN
           LTR R15,R15
         BNZ   BLKERR         ERROR,DIDN'T CONVERT                75128
         CH    R1,=H'32767'                                       75128
           BH  BLKERR
         CR    R10,R1         NOW TAKE THE SMALLEST               75128
*                                              AS BEING THE LRECL
           BH  STRLREC
           LR  R9,R10
         LR    R10,R1                                             75128
         LR    R1,R9                                              75128
STRLREC    EQU *
* R1 CONTAINS THE SMALLER NUMBER, THE LRECL                       75128
           CLC =C'VS',DDRECFM
           BE  C03VBSWP
           CLC =C'VBS',DDRECFM
           BE  C03VBSWP
C03VSOK    EQU *
           STH  R1,DDLRECL                                        80290
           STH  R10,DDBLKSZE                                      80290
           B   C                               CONTINUE PROCESSING
BLKERR   JOLERR 305,'ERROR IN BLOCKSIZE/RECORDSIZE:->32768'
           B   C
C03VBSWP   LR  R9,R10
         LR    R10,R1                                             75128
         LR    R1,R9                                              75128
           B   C03VSOK
         BOX   'STORE RECSIZE AND BLOCKSIZE IF SPECIFIED'         75128
DSBLOCK  EQU   *                                                  75128
DSBLOCKS EQU   *                                                  75128
         JOLSTOR 'BLOCKSIZE',DDBLKSZE,#TKN,NUM                    75128
           OI  DDSPECT2,X'20'  TURN ON NODEFAULT BIT TOO          75311
         B     C                                                  75128
         SPACE 3
DSRECSIZ EQU   *                                                  75128
DSRECORD EQU   *                                                  75128
         JOLSTOR 'RECORDSIZE',DDLRECL,#TKN,NUM                    75128
           OI  DDSPECT2,X'20'  TURN ON NODEFAULT BIT TOO          75311
         B     C                                                  75128
DSRECFM  EQU   *
         OI  DDSPECT2,X'20'  TURN ON 'NODEFAULT' BIT
         CLI  #TKN+1,1        RECFM (V B A) TYPE?                 75311
         BE   DSFIXED         MAYBE,SEE WHATS NEXT                75311
         B    ISARECFM        NO,STORE VBA ETC                    75311
           SPACE
           TITLE 'FIX OPTCD'
DSOPTCD    DS  0H
           JOLSTOR 'OPTCD',DDOPTCD,#TKN
           B  C
           TITLE 'FIX JOL DATA BASE FUNCTIONS'                    J52
DSOWNER    EQU *                                                  J52
           CLI PHASE,C'P'                                         J52
           BNE C                 IGNORE IT                        J52
*          JOLSTOR 'OWNER',DDDDNAME,#TKN                          J52
           MVC DDDDNAME,TKN                                       J52
           B  C                                                   J52
DSMODEL    DS  0H                                                 J52
           CLI PHASE,C'P'                                         J52
           BNE C                 IGNORE IT                        J52
           MVC DDDSID(12),TKN    USES LARGER FIELD                J52
         B     C                                                  J52
DSVSAMXT   DS  0H                                                 J52
         CLI   PHASE,C'P'                                         J52
         BNE   C                 IGNORE IF NOT IN PREPROCESSOR    J52
*          JOLSTOR 'VSAMTXT',DDVSAMXT,#TKN                        J52
           MVC #DDVSAML(L'DDVSAMXT+2),#TKN                        J52
*        LA    R1,DDLEN+L'DDEXTRA+2   +2 FOR LENGTH DDVSAML       J52
         LA    R1,DDVSAMXT-DDOSVB    +2 FOR LENGTH DDVSAML        J52
         AH    R1,#DDVSAML       ADD LENGTH OF ACTUAL FIELD       J52
         STH   R1,DDOSVB                                          J52
*          B   C                                                  J52
         SPACE 3                                                  75128
           TITLE 'FIX VSAM KEYWORD'                               74256
DSVSAM     EQU *                                                  74256
         OI   DDSPECT2,X'01'    TURN ON VSAM                      75128
           B   C               BACK TO MAIN LOOP                  74256
DSPATH     DS  0H               STORE PATH NAME                   J60
         CLI   PHASE,C'P'                                         J60
         BE    C                 IGNORE IF NOT IN PREPROCESSOR    J60
*          JOLSTOR 'PATH   ',DDVSAMXT,#TKN                        J60
* THROUGHOUT JOL'S HISTORY, EFFORTS HAVE BEEN MADE TO HAVE NO     J60
* 'GETMAIN'S OTHER THAN 1 IN THE COMPILER.
*   HOWEVER, WITH THE PATH, I FINALLY ADMIT DEFEAT!!  DEC 2, 2010 J60
* THE PATH WILL BE STORE IN A GOTTEN AREA!                        J60
*
         LA    R0,DDVSAMXT-DDOSVB    +2 FOR LENGTH DDVSAML        J60
         GETMAIN R,LV=(0)        JUST SAVE IT                     J60
         ST    R1,DDAEXTRA       SAVE THE ADDRESS                 J60
*        MVC #DDVSAML(L'DDVSAMXT+2),#TKN                          J60
         MVC   0(DDVSAMXT-DDOSVB,R1),#TKN  SAVE THE PATH          J60
* TEST IF DSNAME ALREADY CODED - GIVE ERROR
         MVI   DDDSNAME,C'%'                                      J60
         SPACE 3                                                  J60
*SVSAM     EQU *                                                  J60
         MVI  DDTYPE,DDPATH     TURN ON PATH                      J60
           B   C               BACK TO MAIN LOOP                  J60
DSBIN      DS  0H               SET BINARY MODE FOR PATH          J60
DSTEXT     DS  0H               SET TEXT MODE FOR PATH            J60
           JOLSTOR 'PATH TYPE',DDFLTYPE,#TKN                      J60
           B   C               BACK TO MAIN LOOP                  J60
DSFORM     EQU *                                                  J60
* NEW FOR VERSION 3.0                                             75311
*                                                                 75311
* IF SOME-ONE CODES FORM=1, WE GENERATE 'S1', BUT IF THEY CODE    75311
*    FORM 'A1', WE POP IT OUT AS CODED.                           75311
*                                                                 75311
           CLI TKN,C''''       QUOTES AROUND FORM ?               75311
           BE  C03FQU          YES                                75311
* NO QUOTES HERE                                                  75311
           L   R1,AJOLGEN                                         75311
           USING GENDETS,R1                                       75311
         CLI   DDSYSCLS,C' '                             JOL30048 76200
         BNE   C03FORMO        BRANCH TO STORE FORM                SOCO
            MVC  DDSYSCLS,SPECFORM
           B   C03FORMO        STORE FORM NUMBER                  75311
           SPACE 1                                                75311
C03FQU     EQU *                                                  75311
* FORM KEYWORD FOUND                                              74256
           BAL R14,DROPQUOT    DROP QUOTES, IF ANY                75311
C03FORMO JOLSTOR 'FORM',DDSYSFRM,#TKN                             75128
           B   C               FORM DONE,BACK TO MAINLINE         74256
           TITLE 'FIX CYLOFL (CYLINDER OVERFLOW)'
DSCYLOFL   JOLSTOR 'CYLOFL',DDOVERFL,#TKN,NUM  STORE AS REQUESTED 80290
           B   C               BACK TO MAIN LOOP MAN.
           TITLE 'FIX KEY'
DSKEY      EQU *
         JOLSTOR 'KEY LENGTH',DDKEYLEN,#TKN,255                   75128
           GETTKN TKNCURR
           CLI TKN,C','
           BNE C
           GETTKN (1)
           STH R1,TKNCURR
DSKEYPOS EQU   *
           JOLSTOR 'KEY POSITION',DDRKP,#TKN,NUM                  80290
           B  C
DSKEYLEN EQU   *
         JOLSTOR 'KEY LENGTH',DDKEYLEN,#TKN,255                   75128
         B     C
           TITLE 'DECODE SUBSYS INFORMATION'                      87150
DSSUBSYS EQU   *
         MVC   #WORK(L'TKN),#TKN                                  87150
SUBSLOOP GETTKN LOCN=TKNCURR                                      87150
         CLI   TKN,C','        MORE THAN 1 PARAMETER ?            87150
         BNE   STORSUBS                                           87150
         STH   R1,TKNCURR                                         87150
         BAL   R14,CONCAT      PUT COMMA IN                       87150
         GETTKN LOCN=TKNCURR                                      87150
         STH   R1,TKNCURR                                         87150
         BAL   R14,CONCAT                                         87150
         B     SUBSLOOP                                           87150
STORSUBS EQU   *
         LA    R7,DDLENXTR
         USING DDLENXTR,R7
         JOLSTOR  'SUBSYS',DDSUBS,#WORK
         LTR   R15,R15
         BNZ   C
         MVC   #DDSUBS,#WORK
         DROP  R7
         B     C
         TITLE 'DECODE VOLUME INFORMATION'
DSVOL    EQU   *
DSVOLS   EQU   *                                                  75128
DSVOLUME EQU   *
STARTVOL EQU   *
         MVI   #TKN+1,6       SET LENGTH OF FIRST VOL=6           75128
           MVC #WORK(10),#TKN
VOLLOOP    GETTKN LOCN=TKNCURR
           CLI TKN,C','                        MORE THAN 1 VOLUME ?
           BNE STORVOLS
           STH R1,TKNCURR
           GETTKN LOCN=TKNCURR
           STH R1,TKNCURR
           CLC #TKN,=H'6'
           BH VOLTOOBG
CONTVOL     EQU *
         MVI   #TKN+1,6       SET LENGTH OF SECOND VOL=6          75311
         BAL   R14,CONCAT                                         75128
           B   VOLLOOP
STORVOLS   EQU *
           JOLSTOR  'VOLUME',DDVOLUME,#WORK
           B   C
VOLTOOBG JOLERR 306,'VOLUME NAME''',#TKN,'''> 6 CHARACTERS-TRUNCATED'
           B CONTVOL
         SPACE 3
DSVOLSEQ EQU   *
         JOLSTOR 'VOLUME SEQUENCE NUMBER',DDVOLSEQ,#TKN,NUM
         B     C
           TITLE 'STORE DSORG'
DSORG    EQU   *                                                 75128
DSPS       EQU *
DSPSU      EQU *
DSIS       EQU *
DSISU      EQU *
DSPO       EQU *
DSPOU      EQU *
DSDA       EQU *
DSDAU      EQU *
DSSEQ      EQU *
           JOLSTOR 'DATA SET ORGANISATION',DDDSORG,#TKN
           B C
          TITLE 'HANDLE VOLUME REFERENCE'
DSVOLREF   EQU *
* NEW FEATURE,VOLREF='SYS1.LINKLIB' IS ALLOWED    3 NOV,1974 C.CLA?5)
*    WHEN A RUN IS BEING DECODED,IT EXAMINES THE VOLREF FIELD FOR
*    A LITERAL STRING AND MAY GENERATE AN EXTRA DD-CARD FOR THE
*    VOLREF
           MVI WORK,C'-'                  INDICATES TO A LATER
*                         ROUTINE THAT VOL REF WAS CODED
           LH R1,#TKN
           LA R1,1(R1)
           STH R1,#WORK
           MVC WORK+1(40),TKN
           JOLSTOR 'VOLUME REFERENCE',DDVOLUME,#WORK
           B C
       FIX 'NO QUOTES'
           TITLE 'UNIT DECODE'
* UNIT SLIGHTLY COMPLICATED BY THE FACT THAT IT MAY HAVE A KEYWORD
*    OR IT MAY NOT.
* HOWEVER,THIS IS EASILY RESOLVED
DSUNIT     EQU *
DSUNITS    EQU *
STRUNIT    EQU *
           MVC #WORK(12),#TKN
           GETTKN LOCN=TKNCURR
           CLI TKN,C'-'
           BNE TUNITCOM        TEST IF UNIT FOLLOWED BY A','
         BAL   R14,CONCAT                                         75128
           GETTKN (R1)         GET NEXT TOKEN                     74256
           STH R1,TKNCURR
         BAL   R14,CONCAT                                         75128
           GETTKN (R1)         GET NEXT TOKEN                     74256
           SPACE 1
TUNITCOM   EQU *
           CLI TKN,C','
           BNE C03STRUN        NO-> STORE UNIT                    74256
           STH R1,TKNCURR
           GETTKN (R1)         GET TKN AFTER ',',IE UNIT NUMBER   74256
           CLI TKNTYPE,0       <NUMBER>                           74256
           BE  C03VALUC        YES,VALID UNIT COUNT               74256
           CLC =C'P ',TKN      PARALLEL MOUNTING ?                74256
           BNE C03STRUN        NO, DON'T ISSUE AN ERROR THOUGH.   74256
*                              COULD BE SOMEONE CODED A UNIT,VOL  74256
*                              BUT ANYWAY,THE ERROR WILL SHOW UP  74256
*                              IF IT WASN'T A VALID KEYWORD       74256
*                              FOLLOWING.                         74256
C03VALUC   STH R1,TKNCURR      FIX IT UP SO WE WON'T GET IT AGAIN 74256
           JOLSTOR 'UNIT COUNT',DDUNITNO,#TKN STORE UNIT COUNT
C03STRUN   EQU *                                                  74256
           JOLSTOR 'UNIT',DDUNIT,#WORK
* SET UP DDUNITYP NOW, IF STORED                         JOL30030 76200
         LTR   R15,R15       DID WE STORE IT             JOL30030 76200
         BNZ   C              NO,SKIP THIS               JOL30030 7620/
           SPACE 3
         L     R1,AJOLGEN                                         75128
         L     R1,AGENUNIT-UJGENDET(R1)                           75128
           LM R14,R15,4(R1)
           L  R1,0(R1)
TGENU9X    CLC DDUNIT,0(R1)
           BE  C03GOTGU
           BXLE R1,R14,TGENU9X
         SPACE 1                                         JOL30030 76200
         L     R1,AJOLGEN                                JOL30030 76200
         USING GENDETS,R1                                JOL30030 76200
         L     R1,ADEVNAME    GET ADDRESS OF DEVNAMET    JOL30030 76200
         LTR   R1,R1          DID WE LOAD IT ?           JOL30030 76200
         BZ    C              NOPE                       JOL30030 76200
         L     R15,0(R1)     LOAD # ENTRIES IN TABLE     JOL30030 76200
         MH     R15,=H'12'   MULT                        JOL30030 76200
         LA    R1,4(R1)      SET TABLE START             JOL30030 76200
         AR    R15,R1         SET TABLE END              JOL30030 76200
         LA    R14,12         SET INCREMENT              JOL30030 76200
         CLC   DDUNIT,0(R1)   FIND UNIT IN TABLE         JOL30030 76200
         BE    C03UNDE        GOT IT                     JOL30030 76200
         BXLE  R1,R14,*-10    LOOP                       JOL30030 76200
         B     C              ERROR WILL BE PICKED UP LATER   30 76200
         SPACE 1                                         JOL30030 76200
C03UNDE    IFVALUE  DDUNITYP,C    SKIP IF ALREADY SET UP          76200
         MVI   DDUNITYP,X'80' SET UP DISK IND            JOL30030 76200
         CLI   10(R1),X'80'  IS DEVNAMET=TAPE            JOL30030 76200
         BNE   C                                         JOL30030 76200
         MVI   DDUNITYP,X'04' SET TO TAPE                JOL30030 76200
         LTR   R15,R15
           B   C
C03GOTGU EQU   *
         MVC   DDUNIT,8(R1)   SWAP UNIT NAMES
         MVC   DDUNITYP,16(R1)
         B     C
         SPACE 2
           TITLE 'DECODE MASS STORAGE FACILITY'
DSMSSG     DS  0H
           JOLSTOR 'MASS STORAGE',DDVOLUME,#TKN                   80290
           LTR R15,R15                                            80290
           BNZ C
           OI  DDUNITQU,DDUNITM TURN ON MASS STORAGE BIT          80290
           B   C
           SPACE 3
DSQNAME    DS  0H
           JOLSTOR 'QNAME',DDVOLUME,#TKN                          80290
           LTR R15,R15                                            80290
           BNZ C
           OI  DDUNITQU,DDUNITQ TURN ON QNAME BIT                 80290
           MVI DDTYPE,DDSYSIN  TURN ON INDICATOR TO STOP VETS     80290
           MVI DDDSNAME,C'*'   TURN ON INDICATOR TO STOP VETS     82300
           B   C
           SPACE 3
         AGO   .C0301                                             75128
DSHIGH     EQU *                                                  74256
DSLOW      EQU *                                                  74256
DSDUAL     EQU *                                                  74256
* THIS CODE PROBABLY NEVER GETS EXECUTED,BUT ....                 74256
           MVC #WORK(6),#TKN                                      74256
           GETTKN TKNCURR                                         74256
*                                                                 74256
           CLC TKN(7),=C'DENSITY'
           BNE C03STRUN        GO STORE UNIT                      74256
           STH R1,TKNCURR
           GETTKN (R1)                                            74256
           CLC TKN(4),=C'TAPE'
           BNE C03STRUN        GO STORE UNIT                      74256
           STH R1,TKNCURR
           B   C03STRUN        GO STORE UNIT                      74256
.C0301   ANOP                                                     75128
           TITLE 'BUFFER KEYWORD DECODE'
DSBUFNO    EQU *
DSBUFFER   EQU *
* THANK HEAVENS WE CAN'T GET INTO TOO MUCH TROUBLE WITH BUFFERS
           JOLSTOR 'BUFFER NUMBER',DDBUFNO,#TKN,255               74256
           B   C
DSBUFNI    EQU *
           JOLSTOR 'BUFNI PARAMETER',DDBUFNI,#TKN,255
           B   DSVSAM
DSBUFND    EQU *
           JOLSTOR 'BUFND PARAMETER',DDBUFNO,#TKN,255
           B   DSVSAM
DSBUFL   EQU   *
         JOLSTOR 'BUFFER LENGTH',DDBUFL,#TKN,NUM
           B   C
           TITLE 'DECODE SPACE '
DSABSTR  MVI DDCYLTRK,C'A'
DSSPACE    EQU *
SPACE1OK EQU *
         JOLSTOR 'PRIMARY SPACE',DDPRIM,#TKN,NUM                  75128
           LTR R15,R15
           BNZ C
           GETTKN LOCN=TKNCURR
           CLI TKN,C','
           BNE SPACEQAL
           STH R1,TKNCURR
           GETTKN (R1)                                            74256
* TEST FOR SPACE 10,,7
           CLI  TKN,C','
           BNE  C03TS2
           STH  R1,TKNCURR
           B    C03THIRD
C03TS2     EQU  *
           CLI TKNTYPE,0
         BNE   SPACEQAL                                           75128
           STH R1,TKNCURR
         JOLSTOR 'SECONDARY SPACE',DDSEC,#TKN,NUM                 75128
          GETTKN LOCN=TKNCURR                                    75311
           CLI     TKN,C','
           BNE SPACEQAL
* HERE THERE IS A THIRD SPACE
C03THIRD   STH R1,TKNCURR
           GETTKN (R1)                                            74256
           CLI TKNTYPE,0                       NUMERIC
         BNE   SPACEQAL                                           75128
           STH R1,TKNCURR
         JOLSTOR 'DIRECTORY/INDEX SPACE',DDDIRECT,#TKN,NUM        75128
           GETTKN LOCN=TKNCURR
SPACEQAL   EQU *
           LA  R15,C03SPTBL    SPACE TABLE CONSTANTS              74256
C03TSPC9   CLC TKN(L'C03SPTBL),0(R15) SPACE KEYWORD (TRK ETC?)    74256
           BE  C03GOTQ1               YES                         74256
           LA  R15,L'C03SPTBL(R15)    NOT YET,LOOK AT NEXT        74256
           C   R15,=A(C03SPTBE)                                   74256
           BL  C03TSPC9                                           74256
* NOT TRACKS OR ANYTHING SO MAKE IT BLANK AND LET THE VET ROUTINES
*         PUT SOMETHING APPROPRIATE THERE LATER
           B   C
C03GOTQ1   MVC DDCYLTRK,TKN           MOVE FIRST CHAR IN          74256
           STH R1,TKNCURR
           B   C
         SPACE 3
DSDIRECT EQU   *                                                  75128
DSDIR    EQU   *                                                  75128
         JOLSTOR 'DIRECTORY BLOCKS',DDDIRECT,#TKN,NUM             75128
         B     C                                                  75128
         SPACE 3                                                  75128
DSROUND  EQU   *                                                  75128
         OI    DDSPECTP,X'01'      TURN ON ROUND                  75128
         B     C                                                  75128
         SPACE 3                                                  75128
DSALX    OI    DDSPECTP,X'02'      TURN ON ALX                    75128
         B     C                                                  75128
DSMXIG   OI    DDSPECTP,X'04'      TURN ON MXIG                   75128
         B     C                                                  75128
         SPACE 3
DSUNITNO EQU   *
         CLI   TKN,C'P'       PARALLEL?                           75311
         BE    DSPARUN                                            75311
         JOLSTOR 'NO. OF UNITS',DDUNITNO,#TKN,NUM                 75128
         B     C                                                  75128
DSCONTIG EQU *
         OI DDSPECT2,X'80'                                        75128
         B C
           TITLE 'DECODE SIZE KEYWORD'
C03RECS  EQU   *                                                  75128
         GETTKN (R5)        GET THE ORIGINAL NUMBER AGAIN
DSSIZE     EQU *
           JOLSTOR 'SIZE',DDSIZE,#TKN,NUM                         74256
           GETTKN LOCN=TKNCURR
           CLC     TKN(2),=C'K '
           BE   C03STRSQ
           CLC =C'BYTES ',TKN
           BE  C03STRSQ
           CLC =C'RECS ',TKN
           BE   SKIP1
          CLC =C'RECORDS ',TKN                                    75128
         BE    SKIP1       ONLY 1ST SIZE QUANTITY GIVEN US     75128
DSSIZE2  CLI   TKN,C'-'       N-N2 K CODED ?                      75128
         BNE   C              NO -> BACK TO MAIN LINE             75128
         GETTKN (R1)          GET NEXT SIZE                       75128
         STH   R1,TKNCURR     SAVE CURRENT POINTER                75128
         JOLSTOR 'SIZE',DDSIZE2,#TKN,NUM STORE SIZE2              75128
         GETTKN TKNCURR       GET NEXT TOKEN                      75128
         CLC   =C'K ',TKN                                         75128
         BE    C03STRSQ       YES                                 75128
C03TREC9   CLC =C'RECS ',TKN
         BE SKIP1
         CLC  =C'RECORDS ',TKN
         BE SKIP1
         CLC =C'BYTES ',TKN
         BNE  C
C03STRSQ MVI   DDSIZEQ,C'K'                                       75128
         STH  R1,TKNCURR
         GETTKN (R1)
         CLI  TKN,C'-'
         BE   DSSIZE2
         B    C03TREC9                                            75128
DSOF     EQU *   ********* KEYWORD, NOT NECESSARILY CODED AFTER
*               RECORDS NUMBER
        JOLSTOR  'MODAL-SIZE',DDAVREC,#TKN,NUM
        B   C
           TITLE 'DECODE LABEL'
DSLABEL    EQU *
DSFILENO   JOLSTOR 'LABEL/FILENO',DDLABEL,#TKN,255                74256
           B   C
DSRETAIN EQU   *                                                  75128
         LH  R1,TKNCURR
         CLC   =C'UNTIL ',TKN                                     75128
         BE    C03TILL                                            75128
         CLC   =C'TILL ',TKN                                      75128
         BE    C03TILL                                            75128
           CLC =C'TIL ',TKN                                       75311
           BE  C03TILL                                            75311
         CLC   =C'FOR ',TKN                                       75128
         BE    C03FOR                                             75128
* ASSUME 'RETAIN 10 DAYS' HERE                                    75128
DSFOR    EQU   *
C03RTPZ  JOLSTOR 'RETENTION PERIOD',DDEXPDT,#TKN,NUM              76200
         LTR   R15,R15                                   JOL30053 76200
         BNZ   C03TDX2       J                           JOL30053 76200
         MVI    DDRETPD,C'R'                             JOL30053 76200
C03TDX2  EQU   *                                                  75128
         GETTKN TKNCURR                                           75128
         CLC   =C'DAY ',TKN                                       75128
         BE    SKIP1                                              75128
         CLC   =C'DAYS ',TKN                                      75128
         BNE   C                                                  75128
         B   SKIP1    RESET TKNCURR ONE ABOVE
C03FOR   GETTKN (R1)                                              75128
         STH   R1,TKNCURR                                         75128
         B     C03RTPZ                                            75128
         SPACE 3                                                  75128
C03TILL  GETTKN (R1)                                              75128
         STH   R1,TKNCURR                                         75128
DSUNTIL  EQU *
DSTILL   EQU *
         JOLSTOR 'EXPIRY DATE',DDEXPDT,#TKN              JOL30053 76200
         B     C03TDX2        TEST 'DAY' | 'DAYS' NOW             75128
           TITLE 'FIX NL,BLP ETC'
DSSL       LA  R1,0
           B   C03STRLT
DSNL       LA  R1,1
           B   C03STRLT
DSBLP      LA  R1,2
           B   C03STRLT
DSSUL      LA  R1,3
           B   C03STRLT
DSNSL      LA  R1,4
           B   C03STRLT
DSAL   EQU  *
DSASCII    LA  R1,5
           B   C03STRLT
DSLTM    LA    R1,6                                               75128
         B     C03STRLT
DSAUL    LA R1,7
C03STRLT   STC R1,TKN
           MVI #TKN+1,1
           JOLSTOR 'LABEL TYPE',DDLABTYP,#TKN
           B   C               BACK TO MAIN LINE
         SPACE 3
DSPRIVAT EQU   *                                                  75128
         OI    DDSPECTP,X'08'      TURN ON PRIVATE                75128
         B     C                                                  75128
         SPACE 3                                                  75128
DSREAD   EQU   *                                                  75128
DSREADON EQU   *                                                  75128
         OI    DDSPECTP,X'10'      TURN ON READONLY PROTECT       75128
           GETTKN TKNCURR      GET ITEM AFTER READ                75311
           CLC =C'ONLY ',TKN   'ONLY'                             75311
           BE  SKIP1           YES, IGNORE IT                     75311
         B     C                                                  75128
         SPACE 1                                                  75128
DSPROTEC EQU   *                                                  75128
         OI    DDSPECTP,X'20'      TURN ON DATA SET PROTECT (ALL) 75128
         B     C                                                  75128
         SPACE 3                                                  75128
DSNOCAT  EQU   *                                                  75128
         MVI   DDCATLGS,0                                         75128
           OI  PARMSRCH,X'02'  TELL US LATER THAT WE HAVE TO
*                              GENERATE A SPECIAL 'UJEINIT' STEP
*                              THAT SEARCHES THE SYSTEM CATALOG
*                              FOR THE DATA-SETS AT EXECUTE TIME
         B     C                                                  75128
         SPACE 3                                                  75128
* THE FOLLOWING SUPPORTS 'NODEFAULT' | 'NODEF', WHICH MEANS       75311
*    NO DEFAULTS TO BE MADE TO THE DDCARD.                        75311
DSNODEF    OI  DDSPECT2,X'20'  TURN ON DEFAULT                    75311
           B   C                                                  75311
           SPACE 3                                                75311
DSEXT    EQU   *                                                  75128
         MVI   C03DEXT,1      SET NON BLANK                       75128
         B     C                                                  75128
         SPACE 3                                                  75128
DSLIKE   MVC C03DLIKE,TKN    ***JOLSTOR CAN'T USE R13 AS A BASE **75128
         B     C                                                  75128
DSDSID   EQU   *                                                  75128
         JOLSTOR 'DSID',DDDSIDR,#TKN                              75128
         B     C                                                  75128
DSNORLSE   EQU  *
DSNORELE   EQU  *
         OI    DDSPECT2,X'02' TURN ON NORLSE FLAG                 75128
           B C
DSRLSE   B     C              DUMMY FOR RLSE                     75128
           SPACE 3
DSNONAME    EQU  *
DSNODSN   CLI DDDSNAME,C' '    ALREADY GOT SOMETHING IN IT ?
          BNE C03STNDS
          MVI DDDSNAME,C'%'
          B   C
C03STNDS  JOLSTOR 'NO DSNAME ',DDDSNAME,#TKN
          B   C
          SPACE 3
DSNODCB   CLI DDRECFM,C' '
          BNE C03STNRF
          MVI DDRECFM,C'%'
          B   C
C03STNRF  JOLSTOR 'NO DCB',DDRECFM,#TKN
          B   C
          SPACE 3
DSNOIO     EQU  *
DSDUMMY    EQU *
*          CLI DDDSNAME,C' '                                      74256
*          BNE C03STDUM                                           74256
          MVC DDDSNAME(6),=C'*DUMMY'
           B  C
*03STDUM   JOLSTOR 'NO I0/DUMMY',DDDSNAME,#TKN                    74256
*          B   C                                                  74256
DSEROPT    LH  R1,TKNCURR      DUMMY                              75128
           BCTR  R1,0         -1 SO NEXT GET WILL GET ACCEPT ETC  75311
           B     SKIP1                                            75311
DSACCEPT EQU   *                                                  75128
         OI    DDSPECTP,X'40'      TURN ON ACCEPT ERRORS          75128
         B     C03CHKER       CHECK IF NEXT TKN='ERRORS'          75128
DSSKIP   EQU   *                                                  75128
         OI    DDSPECTP,X'80'      TURN ON SKIP ERRORS            75128
C03CHKER GETTKN TKNCURR       GET NEXT TOKEN                      75128
         CLC =C'ERRORS ',TKN  ='ERRORS'                           75128
         BNE   C              NO ->                               75128
         B     SKIP1                                              75128
DSCODE   JOLSTOR 'CODE',DDCODE,#TKN
         B   C
         TITLE ' HANDLE OTHER SYSIN / SYSOUT PARAMETERS'          75128
DSREPLAC EQU   *                                                  75128
DSPRINTA EQU   *                                                  75128
DSJOLCRD EQU   *                                                  82300
DSPRINTN EQU   *                                                  75128
DSNOSPLT EQU   *                                                   DASD
DSPRINTO EQU   *                                                  75128
         CLI   DDTYPE,DDSYSIN  SYSIN DECLARE ?                    75128
         BE    C               YES,JUST IGNORE IT                 75128
         B     C03ERR         GIVE ERROR MSSGE                    75128
         SPACE 3                                                  75128
DSOUTLIM  EQU   *
* WE USED TO HAVE (STILL DO IN FACT) A 'JOLSTOR' BUT IT
* WONT STORE NUMBER INTO AL3 FIELDS, SO :
          TNUM  #TKN
          LTR   R15,R15
          BNZ   DSOUTL2
          ST    R1,DBL
          CLC   DDOUTLIM,ZERO
          BNE   DSOUTL2
          MVC   DDOUTLIM,DBL+1
          B     C
DSOUTL2   JOLSTOR 'OUTLIM',DDOUTLIM,#TKN,NUM
          B   C
DSDEST   BAL   R14,DROPQUOT   DROP QUOTES (IF ANY) FROM TKN       75128
         JOLSTOR 'ROUTE/DEST',DDROUTE,#TKN                        75128
         B     C                                                  75128
DSFLASH  EQU *
         MVI   #TKN+1,4
         MVC   #WORK(10),#TKN
         GETTKN LOCN=TKNCURR
         CLI   TKN,C','
         BNE   STORFLSH
         STH   R1,TKNCURR
         BAL   R14,CONCAT
         GETTKN LOCN=TKNCURR
         STH   R1,TKNCURR
         MVI   #TKN+1,3
         BAL   R14,CONCAT
STORFLSH JOLSTOR 'FLASH',DDFLASH,#WORK
         OI    DDUNITQU,DD3800
         B     C                                                  75128
DSCHARS  EQU *
         MVI   #TKN+1,4                         SET LENGTH TO 4
         MVC   #WORK(10),#TKN                   MOVE TKN TO WORK
         GETTKN LOCN=TKNCURR                    GET NEXT TKN
         CLI   TKN,C','                         IS IT A COMMA
         BNE   STORCHRS                         NO GO STORE IT
         STH   R1,TKNCURR                      YES IT IS, SAVE ADDRESS
         BAL   R14,CONCAT                      SAVE COMMA
         GETTKN LOCN=TKNCURR                   GET 2ND PARM
         STH   R1,TKNCURR                      SAVE ADDRESS
         MVI   #TKN+1,4                        SET LENGTH TO 4
         BAL   R14,CONCAT                      SAVE IT
         GETTKN LOCN=TKNCURR                    GET NEXT TKN
         CLI   TKN,C','                         IS IT A COMMA
         BNE   STORCHRS                         NO GO STORE IT
         STH   R1,TKNCURR                      YES IT IS, SAVE ADDRESS
         BAL   R14,CONCAT                      SAVE COMMA
         GETTKN LOCN=TKNCURR                   GET 3RD PARM
         STH   R1,TKNCURR                      SAVE ADDRESS
         MVI   #TKN+1,4                        SET LENGTH TO 4
         BAL   R14,CONCAT                      SAVE IT
         GETTKN LOCN=TKNCURR                    GET NEXT TKN
         CLI   TKN,C','                         IS IT A COMMA
         BNE   STORCHRS                         NO GO STORE IT
         STH   R1,TKNCURR                      YES IT IS, SAVE ADDRESS
         BAL   R14,CONCAT                      SAVE COMMA
         GETTKN LOCN=TKNCURR                   GET 4TH PARM
         STH   R1,TKNCURR                      SAVE ADDRESS
         MVI   #TKN+1,4                        SET LENGTH TO 4
         BAL   R14,CONCAT                      SAVE IT
STORCHRS JOLSTOR 'CHARS',DDCHARS,#WORK
         OI    DDUNITQU,DD3800
         B     C                                                  75128
DSMODIFY EQU *
         MVI   #TKN+1,4
         MVC   #WORK(10),#TKN
         GETTKN LOCN=TKNCURR
         CLI   TKN,C','
         BNE   STORMDFY
         STH   R1,TKNCURR
         BAL   R14,CONCAT
         GETTKN LOCN=TKNCURR
         STH   R1,TKNCURR
         MVI   #TKN+1,3
         BAL   R14,CONCAT
STORMDFY JOLSTOR 'MODIFY',DDMODIFY,#WORK                          75128
         OI    DDUNITQU,DD3800
         B     C                                                  75128
DSBURST  MVI   #TKN+1,1              SET LENGTH TO 1              75128
         JOLSTOR 'BURST',DDBURST,#TKN                             75128
         OI    DDUNITQU,DD3800
         B     C                                                  75128
DSPROG   JOLSTOR 'SYSOUT PROGRAM',DDSYSPGM,#TKN                   75128
         B     C                                                  75128
DSUCS    JOLSTOR 'UCS',DDUCS,#TKN                                 75128
         B     C                                                  75128
DSFCB    JOLSTOR 'FCB',DDFCB,#TKN                                 75128
         B     C                                                  75128
DSVERIFY MVI   DDVERIFY,C'1'                                      75128
         B     C                                                  75128
DSALIGN  MVI   DDALIGN,C'1'                                       75128
         B     C                                                  75128
DSHOLD   OI    DDSPECT2,X'04'                                     75128
         B     C                                                  75128
DSFREE    EQU   *
         GETTKN TKNCURR         GET NEXT TOKEN
         CLI   TKN,C'='       = AFTER FREE ?                      75128
         BNE   C03FREEB       NOPE SEE IF FREE(CLOSE)             75128
         GETTKN (R1)          SKIP '='                            75128
         STH   R1,TKNCURR                                         75128
C03FREEB CLI   TKN,C'('       =( | FREE(CLOSE)                    75128
         BNE   C03FREEA       NO, SHOULD BE 'AT'|'CLOSE'|'END'    75128
         GETTKN (R1)          SKIP '()'                           75128
         STH   R1,TKNCURR                                         75128
C03FREEA EQU   *
         CLC     =C'AT ',TKN
          BNE    C03FRCL
          GETTKN (R1)
         STH R1,TKNCURR
C03FRCL  CLC  =C'END ',TKN
          BE   C03ENDFR
         CLC   =C'CLOSE ',TKN                                     75128
         BE    C03CLOSE                                           75128
         OI    DDSPECT2,X'08' FREE WITHOUT 'CLOSE' | 'END'        75128
         B     C                                                  75128
C03CLOSE OI    DDSPECT2,X'08'                                     75128
C03ENDFR   STH   R1,TKNCURR
         OI    DDSPECT2,X'10'  SET 'FREE AT END' BIT ON           80290
         B     C                                                  75128
DSDEN    JOLSTOR 'DEN',DDDENS,#TKN,NUM                            75128
         B     C                                                  75128
DSDIAGNS EQU   *                                                  75128
DSTRACE  OI    DDSPECT2,X'40'                                     75128
         B     C                                                  75128
DSMAXVOL JOLSTOR 'MAXVOL',DDMAXVOL,#TKN,NUM                       75128
         B     C
         SPACE 1                                                  75128
DSDCBEXT EQU   *                                                  75128
         MVI   DDOSVB+1,DDLEN+L'DDEXTRA                           75128
         BAL   R14,DROPQUOT   DROP ''                             75128
         JOLSTOR 'DCB-EXTRA',DDEXTRA,#TKN                         75128
         B     C                                                  75128
         SPACE 1                                                  75128
DSPARUN  MVI   #TKN+1,1       SET LENGTH TO 1                     75128
         MVI  TKN,C'1'        SET IN CASE WE HAVE TO USE JOLSTOR
*                             TO GIVE ERROR MESSAGE
         CLI   DDUNITNO,C' ' ANYTHING IN IT YET???
         BNE   DSUNITNO      USE JOLSTOR FOR ERROR      JOL30041 76200
         MVI   DDUNITNO,C'P'
         GETTKN               TKNCURR                             75311
         CLC   =C'MOUNT ',TKN                                     75311
         BE    SKIP1                                              75311
         B     C
         SPACE 1
DSFOLD   MVI   #TKN+1,1    SET LENGTH TO '1'                      75311
         JOLSTOR              'FOLD',DDFOLD,#TKN                  75311
         B     C                                                  75311
         SPACE 1                                                  75128
C03ZAP1  JOLSTOR '        ',TKN,#TKN
         B     C
C03ZAP2  JOLSTOR '        ',TKN,#TKN
         B     C
*03ZAP3  JOLSTOR '        ',TKN,#TKN
*        B     C
*03ZAP4  JOLSTOR '        ',TKN,#TKN
*        B     C
*03ZAP5  JOLSTOR '        ',TKN,#TKN
*        B     C
           SPACE 1                                                74256
C03SPTBL   DC  CL7'TRKS'                                          74256
         DC    CL7'CYLS'                                          75128
         DC    CL7'BLKS'                                          75128
         DC    CL7'TRK'                                           75128
         DC    CL7'CYL'                                           75128
         DC    CL7'BLK'                                           75128
         DC    CL7'BLOCKS'                                        75128
         DC    CL7'BLOCK'
         DC    CL7'TRACKS'                                        75128
         DC    CL7'TRACK'                                          DASD
C03SPTBE   EQU *                                                  74256
C03LA10  LA    R10,0          ****** MOVED TO DBL AND EXECUTED    75128
           LTORG                                                  74256
         CNOP  0,8
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*          DC   S(*),S(*),S(*),S(*)
*        JOLSAVE CSECT=C03FKEY,BASE=R10                           86210
C03FKEY  CSECT                                                    86210
         USING C03FKEY,R10                                        86210
         SAVE  (14,12)                                            86210
         LR    R10,R15                                            86210
* BINARY SEARCH ROUTINE, JULY, 1986 BY C. V. CLARKE.
*
* THE TABLE IS NOT A MULTIPLE OF TWO, AND THEREFORE WE WILL ASSUME
* THE TABLE HAS A MAXIMUM OF 256 ENTRIES, AND THAT THE MIDPOINT IS 128.
*
         LA    R1,128          BINARY MIDPOINT OF TABLE           86210
         LR    R6,R1           COPY TO R6                         86210
         AR    R6,R1           DOUBLE IT BECAUSE WE MISS 1ST ENTRY87150
         L     R8,CTBLEND      GET THE REAL END OF THE TABLE      86210
         L     R0,=V(UJC03KWD) LOAD ADDRESS OF TABLE TO REGISTER  86210
BINLOOP  SRL   R6,1            SET NEXT HALF POINT (DIV BY 2)     86210
         LR    R7,R1           COPY CURRENT POINT IN TABLE TO R7  86210
         SLL   R7,2            MULT BY 4                          86210
         LR    R14,R7          SAVE                               86210
         SLL   R7,1            MULT BY 2 (NOW 8 TIMES)            86210
         AR    R7,R14          NOW R7 CONTAINS 12 TIMES R6        86210
         AR    R7,R0           POINT TO REAL ADDRESS IN TABLE     86210
         CR    R7,R8           OVER THE END OF THE REAL TABLE ?   86210
         BNL   BINHIGH         TOO HIGH, NOT FOUND                87150
         CLC   0(8,R7),DBL     HAVE WE GOT THE KEYWORD?           86210
         BE    BINFOUND        YES                                86210
         BH    BINHIGH         TOO HIGH                           86210
BINLOW   SR    R15,R15         SET 'TOOHIGH_IND' OFF              86210
         AR    R1,R6           PREPARE TO LOOK HIGHER IN TABLE    86210
         B     BINTEND         CHECK IF END OF SEARCH             86210
BINHIGH  LA    R15,1           SET 'TOOHIGH_IND' ON               86210
         SR    R1,R6           PREPARE TO LOOK LOWER IN TABLE     86210
BINTEND  LTR   R6,R6           IF 0, END OF SEARCH, NOT FOUND     86210
         BNZ   BINLOOP         GO ROUND AGAIN ....                86210
         RETURN (14,12),,RC=0  DIDN'T FIND THE REQUIRED TOKEN     86210
BINFOUND LR    R15,R7          GOT THE REQUIRED TOKEN             86210
         RETURN (14,12),,RC=(15) GOT IT                           86210
         B     JRETN           ***********************            86210
CTBLEND  DC    A(UJCKEYE)      END OF THE TABLE ADDRESS           86210
         LTORG
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
           DC   S(*),S(*),S(*),S(*)
* 30 MAY,1975  *****
* THESE KEYWORDS USED TO BE IN A SEPERATELY ASSEMBLED CSECT
* THE FORMAT OF THE TABLE HAS BEEN CHANGED TO PUT THE MOST
*  FREQUENTLY USED KEY-WORDS UP THE FRONT OF THE TABLE
* FURTHERMORE, AN INDICATOR IS NOW USED TO INDICATE WHETHER
*  THE WORD EXPECTS A VALUE (OR=) AFTER IT, OR WHETHER IT IS
*  A WORD WITH MEANING ON ITS OWN (EG DUMMY)
*
* THE INDICATOR VALUES ARE:-
*
*        LAST 2 BITS INDICATE WHETHER A VALUE IS EXPECTED AFTER
*              THE KEYWORD OR NOT
*        01,1  = YES
*        10,2  = NO
*
*        NEXT 2 BITS INDICATE WHETHER KEYWORD IS ONLY FOR SYSOUT
*              OR NON SYSOUT FILE
*        01,4  = SYSOUT ONLY
*        10,8  = NON-SYSOUT ONLY
*
* VALUES THEREFORE ARE 1,2,3,4 IN DEC
*
         PRINT DATA
UJC03KWD CSECT
* TABLE OF KEYWORDS FOR UJC03DS, NOW IN ORDER OF USE FOR BINARY
*     SEARCH.   C. CLARKE, JULY 27, 1986
         USING UJC03KWD,15
UJC03KWD CSECT
         DC    CL8'&&         ',AL1(2+8),AL1(0),S(DSDSN)
         DC    CL8'ABE        ',AL1(2),AL1(0),S(C)  IGNORE        75311
         DC    CL8'ABEND      ',AL1(2),AL1(0),S(C)  IGNORE        75311
         DC    CL8'ABSTR      ',AL1(1),AL1(0),S(DSABSTR)
         DC    CL8'ACC       ',AL1(2),AL1(0),S(DSACCEPT)          75311
         DC    CL8'ACCEPT     ',AL1(2),AL1(0),S(DSACCEPT)
         DC    CL8'AL         ',AL1(2+8),AL1(0),S(DSAL)
         DC    CL8'ALIGN      ',AL1(2+4),AL1(0),S(DSALIGN)
         DC    CL8'ALX        ',AL1(2),AL1(0),S(DSALX)
         DC    CL8'ASCII      ',AL1(2),AL1(0),S(DSASCII)
         DC    CL8'AUL        ',AL1(2+8),AL1(0),S(DSAUL)
         DC    CL8'B          ',AL1(1),AL1(0),S(DSBLOCK)
*        DC    CL8'BIN        ',AL1(2),AL1(0),S(DSBIN)            J60
         DC    CL8'BINARY     ',AL1(2),AL1(0),S(DSBIN)            J60
         DC    CL8'BLKSIZE    ',AL1(1),AL1(0),S(DSBLOCK)
         DC    CL8'BLOCK      ',AL1(1),AL1(0),S(DSBLOCK)
         DC    CL8'BLOCKSIZE  ',AL1(1),AL1(0),S(DSBLOCK)
         DC    CL8'BLP        ',AL1(2+8),AL1(0),S(DSBLP)
         DC    CL8'BUFFER     ',AL1(1),AL1(0),S(DSBUFNO)
         DC    CL8'BUFFERS    ',AL1(1),AL1(0),S(DSBUFNO)
         DC    CL8'BUFFNO     ',AL1(1),AL1(0),S(DSBUFNO)          75311
         DC    CL8'BUFL       ',AL1(1),AL1(0),S(DSBUFL)
         DC    CL8'BUFLEN     ',AL1(1),AL1(0),S(DSBUFL)
         DC    CL8'BUFLENGTH  ',AL1(1),AL1(0),S(DSBUFL)
         DC    CL8'BUFND      ',AL1(1),AL1(0),S(DSBUFND)
         DC    CL8'BUFNI      ',AL1(1),AL1(0),S(DSBUFNI)
         DC    CL8'BUFNO      ',AL1(1),AL1(0),S(DSBUFNO)
         DC    CL8'BURST      ',AL1(2+4),AL1(0),S(DSBURST)
         DC    CL8'CHARS      ',AL1(1+4),AL1(0),S(DSCHARS)
         DC    CL8'CODE       ',AL1(1+8),AL1(0),S(DSCODE)
         DC    CL8'CONTIG     ',AL1(2),AL1(0),S(DSCONTIG)
         DC    CL8'COPIES     ',AL1(1+4),AL1(0),S(DSCOPIES)
         DC    CL8'CYLOFL     ',AL1(1+8),AL1(0),S(DSCYLOFL)       75311
         DC    CL8'CYLOFLO    ',AL1(1+8),AL1(0),S(DSCYLOFL)
         DC    CL8'D          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'DA         ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'DAU        ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'DCBEXTRA   ',AL1(1),AL1(0),S(DSDCBEXT)
         DC    CL8'DEN        ',AL1(1+8),AL1(0),S(DSDEN)
         DC    CL8'DEST       ',AL1(1+4),AL1(0),S(DSDEST)
         DC    CL8'DIAGNS     ',AL1(1+8),AL1(0),S(DSDIAGNS)
         DC    CL8'DIR        ',AL1(1+8),AL1(0),S(DSDIR)
         DC    CL8'DM         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'DSN        ',AL1(1),AL1(0),S(DSDSN)
         DC    CL8'DSNAME     ',AL1(1+8),AL1(0),S(DSDSN)
         DC    CL8'DSORG      ',AL1(1),AL1(0),S(DSORG)
         DC    CL8'DUMMY      ',AL1(2),AL1(0),S(DSDUMMY)
         DC    CL8'EROPT      ',AL1(1),AL1(0),S(DSEROPT)
         DC    CL8'EXT        ',AL1(2),AL1(0),S(DSEXT)
         DC    CL8'EXTERNAL   ',AL1(2),AL1(0),S(DSEXT)
         DC    CL8'F          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FA         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FB         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBA        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBM        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBS        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBSA       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBSM       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBST       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FBT        ',AL1(2),AL1(0),S(ISARECFM)         75311
         DC    CL8'FCB        ',AL1(1+4),AL1(0),S(DSFCB)
         DC    CL8'FILENO     ',AL1(1+8),AL1(0),S(DSLABEL)
         DC    CL8'FILESEQ    ',AL1(1+8),AL1(0),S(DSLABEL)
         DC    CL8'FIXED      ',AL1(2),AL1(0),S(DSFIXED)
         DC    CL8'FLASH      ',AL1(1+4),AL1(0),S(DSFLASH)
         DC    CL8'FM         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FOLD       ',AL1(2+4),AL1(0),S(DSFOLD)         75311
         DC    CL8'FOR        ',AL1(1+8),AL1(0),S(DSFOR)
         DC    CL8'FORM       ',AL1(1+4),AL1(0),S(DSFORM)
         DC    CL8'FREE       ',AL1(2),AL1(0),S(DSFREE)
         DC    CL8'FS         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FST        ',AL1(2),AL1(0),S(ISARECFM)         75311
         DC    CL8'FST        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'FT         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'G          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'GROUP      ',AL1(1),AL1(0),S(DSMSSG)           80290
         DC    CL8'HOLD       ',AL1(2+4),AL1(0),S(DSHOLD)
*        DC    CL8'INDEXED    ',AL1(2+8),AL1(0),S(DSIND)           SOCO
*        DC    CL8'INDOFL     ',AL1(1+8),AL1(0),S(DSINDOFL)        SOCO
         DC    CL8'IS         ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'ISU        ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'JOL        ',AL1(2),AL1(0),S(DSJOLCRD)         82300
         DC    CL8'KEY        ',AL1(1+8),AL1(0),S(DSKEY)
         DC    CL8'KEYLEN     ',AL1(1+8),AL1(0),S(DSKEYLEN)
         DC    CL8'KEYPOS     ',AL1(1+8),AL1(0),S(DSKEYPOS)
         DC    CL8'KEYPOSN    ',AL1(1+8),AL1(0),S(DSKEYPOS)
         DC    CL8'LABEL      ',AL1(1+8),AL1(0),S(DSLABEL)
         DC    CL8'LIKE       ',AL1(1),AL1(0),S(DSLIKE)
*        DC    CL8'LIMCT      ',AL1(1+8),AL1(0),S(DSLIMCT)
         DC    CL8'LRECL      ',AL1(1),AL1(0),S(DSRECSIZ)
         DC    CL8'LTM        ',AL1(2+8),AL1(0),S(DSLTM)          75128
         DC    CL8'MAXVOL     ',AL1(1+8),AL1(0),S(DSMAXVOL)
         DC    CL8'MAXVOLS    ',AL1(1+8),AL1(0),S(DSMAXVOL)       75311
         DC    CL8'MODEL      ',AL1(1+8),AL1(0),S(DSMODEL)         J52
         DC    CL8'MODIFY     ',AL1(1+4),AL1(0),S(DSMODIFY)
         DC    CL8'MSVGP      ',AL1(1),AL1(0),S(DSMSSG)           80290
         DC    CL8'MSVGRP     ',AL1(1),AL1(0),S(DSMSSG)           80290
         DC    CL8'MXIG       ',AL1(2),AL1(0),S(DSMXIG)
         DC    CL8'NAME       ',AL1(1+8),AL1(0),S(DSDSN)
*        DC    CL8'NCP        ',AL1(1+8),AL1(0),S(DSNCP)           SOCO
         DC    CL8'NL         ',AL1(2+8),AL1(0),S(DSNL)
         DC    CL8'NOCAT      ',AL1(2+8),AL1(0),S(DSNOCAT)
         DC    CL8'NOCATLG    ',AL1(2+8),AL1(0),S(DSNOCAT)
         DC    CL8'NODCB      ',AL1(2),AL1(0),S(DSNODCB)
         DC    CL8'NODEF      ',AL1(2),AL1(0),S(DSNODEF)          75311
         DC    CL8'NODEFAULT  ',AL1(2),AL1(0),S(DSNODEF)          75311
         DC    CL8'NODSN      ',AL1(2+8),AL1(0),S(DSNODSN)
         DC    CL8'NODSNAME   ',AL1(2+8),AL1(0),S(DSNODSN)        75311
         DC    CL8'NOIO       ',AL1(2),AL1(0),S(DSDUMMY)
*        DC    CL8'NOMOVE     ',AL1(2),AL1(0),S(DSNOMOVE)          SOCO
         DC    CL8'NONAME     ',AL1(2+8),AL1(0),S(DSNODSN)        75311
*        DC    CL8'NOOPT      ',AL1(2),AL1(0),S(DSNOOPT)           SOCO
         DC    CL8'NORELEASE  ',AL1(2+8),AL1(0),S(DSNORLSE)
         DC    CL8'NORLSE     ',AL1(2),AL1(0),S(DSNORLSE)
         DC    CL8'NOSPLIT    ',AL1(2),AL1(0),S(DSNOSPLT)          DASD
         DC    CL8'NSL        ',AL1(2+8),AL1(0),S(DSNSL)          75311
         DC    CL8'OF         ',AL1(1),AL1(0),S(DSOF)  *****
         DC    CL8'OPTCD      ',AL1(1+8),AL1(0),S(DSOPTCD)
         DC    CL8'OUTLIM     ',AL1(1+4),AL1(0),S(DSOUTLIM)
         DC    CL8'OWNER      ',AL1(1+8),AL1(0),S(DSOWNER)         J52
         DC    CL8'PARALLEL   ',AL1(2+8),AL1(0),S(DSPARUN)        75128
*        DC    CL8'PARTITIONED',AL1(2+8),AL1(0),S(DSPART)          SOCO
         DC    CL8'PATH       ',AL1(1+8),AL1(0),S(DSPATH)           J60
         DC    CL8'PGM        ',AL1(1+4),AL1(0),S(DSPROG)
         DC    CL8'PO         ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'POSITION   ',AL1(1+8),AL1(0),S(DSLABEL)        75128
         DC    CL8'POU        ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'PRINTALL   ',AL1(2),AL1(0),S(DSPRINTA)
         DC    CL8'PRINTNONE  ',AL1(2),AL1(0),S(DSPRINTN)
         DC    CL8'PRINTOFF   ',AL1(2),AL1(0),S(DSPRINTO)
         DC    CL8'PRIVATE    ',AL1(2+8),AL1(0),S(DSPRIVAT)
*        DC    CL8'PROFILE    ',AL1(1+4),AL1(0),S(DSPROFIL)        SOCO
         DC    CL8'PROG       ',AL1(1+4),AL1(0),S(DSPROG)
         DC    CL8'PROTECT    ',AL1(2+8),AL1(0),S(DSPROTEC)
         DC    CL8'PS         ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'PSU        ',AL1(2+8),AL1(0),S(DSORG)
         DC    CL8'QNAME      ',AL1(1),AL1(0),S(DSQNAME)           SOCO
         DC    CL8'R          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'READ       ',AL1(2+8),AL1(0),S(DSREAD)
         DC    CL8'READONLY   ',AL1(2+8),AL1(0),S(DSREAD)
         DC    CL8'RECFM      ',AL1(1),AL1(0),S(DSRECFM)          75311
         DC    CL8'RECORDSIZE ',AL1(1),AL1(0),S(DSRECSIZ)
         DC    CL8'RECSIZE    ',AL1(1),AL1(0),S(DSRECSIZ)
         DC    CL8'REPLACE    ',AL1(2),AL1(0),S(DSREPLAC)
         DC    CL8'RETAIN     ',AL1(1+8),AL1(0),S(DSRETAIN)
         DC    CL8'RLSE       ',AL1(2),AL1(0),S(DSRLSE)
         DC    CL8'ROUND      ',AL1(2),AL1(0),S(DSROUND)
         DC    CL8'ROUTE      ',AL1(1+4),AL1(0),S(DSDEST)
         DC    CL8'S          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'SEQ        ',AL1(2+8),AL1(0),S(DSSEQ)
         DC    CL8'SEQUENTIAL ',AL1(2+8),AL1(0),S(DSSEQ)
         DC    CL8'SIZE       ',AL1(1),AL1(0),S(DSSIZE)
         DC    CL8'SKIP       ',AL1(2),AL1(0),S(DSSKIP)
         DC    CL8'SKP       ',AL1(2),AL1(0),S(DSSKIP)            75311
         DC    CL8'SL         ',AL1(2+8),AL1(0),S(DSSL)
         DC    CL8'SPACE      ',AL1(1),AL1(0),S(DSSPACE)
         DC    CL8'SUBSYS     ',AL1(1),AL1(0),S(DSSUBSYS)         87150
         DC    CL8'SUL        ',AL1(2+8),AL1(0),S(DSSUL)
         DC    CL8'TEXT       ',AL1(2),AL1(0),S(DSTEXT)           J60
         DC    CL8'TIL        ',AL1(1+8),AL1(0),S(DSTILL)         75311
         DC    CL8'TILL       ',AL1(1+8),AL1(0),S(DSTILL)
         DC    CL8'TRACE      ',AL1(2+8),AL1(0),S(DSTRACE)
*        DC    CL8'TRKOFL     ',AL1(1+8),AL1(0),S(DSTRKOFL)        SOCO
         DC    CL8'U          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'UA         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'UCOUNT     ',AL1(1),AL1(0),S(DSUNITNO)   30034 76200
         DC    CL8'UCS        ',AL1(1+4),AL1(0),S(DSUCS)
         DC    CL8'UM         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'UNDEF      ',AL1(2),AL1(0),S(DSUNDEF)
         DC    CL8'UNDEFINED  ',AL1(2),AL1(0),S(DSUNDEF)
         DC    CL8'UNIT       ',AL1(1),AL1(0),S(DSUNIT)           76200
         DC    CL8'UNITNO     ',AL1(1),AL1(0),S(DSUNITNO)   30034 76200
         DC    CL8'UNITS      ',AL1(1),AL1(0),S(DSUNIT)           76200
*        DC    CL8'UNMOVEABLE ',AL1(2),AL1(0),S(DSNOMOVE)          SOCO
         DC    CL8'UNTIL      ',AL1(1+8),AL1(0),S(DSUNTIL)
         DC    CL8'UT         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'UTA        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'UTM        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'V          ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VA         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VAR        ',AL1(2),AL1(0),S(DSVARIAB)
         DC    CL8'VARIABLE   ',AL1(2),AL1(0),S(DSVARIAB)
         DC    CL8'VB         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBA        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBM        ',AL1(2),AL1(0),S(ISARECFM)         75311
         DC    CL8'VBS        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBSA       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBSM       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBST       ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VBT        ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VERIFY     ',AL1(2+4),AL1(0),S(DSVERIFY)
         DC    CL8'VM         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VOL        ',AL1(1+8),AL1(0),S(DSVOL)
         DC    CL8'VOLCOUNT   ',AL1(1+8),AL1(0),S(DSMAXVOL)       75128
         DC    CL8'VOLREF     ',AL1(1+8),AL1(0),S(DSVOLREF)
         DC    CL8'VOLS       ',AL1(1+8),AL1(0),S(DSVOL)
         DC    CL8'VOLSEQ     ',AL1(1+8),AL1(0),S(DSVOLSEQ)
         DC    CL8'VOLUME     ',AL1(1+8),AL1(0),S(DSVOL)
         DC    CL8'VOLUMES    ',AL1(1+8),AL1(0),S(DSVOL)
         DC    CL8'VS         ',AL1(2),AL1(0),S(ISARECFM)
         DC    CL8'VSAM       ',AL1(2+8),AL1(0),S(DSVSAM)
         DC    CL8'VSAMTXT    ',AL1(1+8),AL1(0),S(DSVSAMXT)       J52
         DC    CL8'VSEQ       ',AL1(1+8),AL1(0),S(DSVOLSEQ)       75128
         DC    CL8'VST        ',AL1(2),AL1(0),S(ISARECFM)         75311
         DC    CL8'VT         ',AL1(2),AL1(0),S(ISARECFM)
UJCKEYE  EQU   *              END OF KEYWORD TABLE                86200
         DC    XL8'FFFFFFFFFF',AL1(2),AL1(0),S(0)                 87150
         END
