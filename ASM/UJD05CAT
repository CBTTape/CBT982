UJD05CAT TITLE 'CATALOG DATA SET'
* JOL COPYRIGHT CCS-JOL PTY LTD 1988
* REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO CCS-E001.
         SPACE 3
*                   J             000000            L
*                   J            0      0           L
*                   J           0        0          L
*                   J          0          0         L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*                   J         0            0        L
*        J          J         0            0        L
*         J        J           0          0         L
*          J      J             0        0          L
*           J    J               0      0           L
*            JJJJ                 000000            LLLLLLLLLLL
           SPACE 3
* LAST SOURCE UPDATE
*   CHANGES:-
*     1.
         COPY  JOLGLOBL
         $UJEPARM
&TYPE    SETC  'SCHED'                                           88036
         PRINT ON,DATA
UJECOMM  DSECT
         USING UJECOMM,R3,R5
         COPY  UJECOMM
D05WORK  DSECT                                                    J40B
         DS    18F                                                J40B
D05WORKS EQU   *-D05WORK                                          J40B
         JOLSAVE CSECT=UJD05CAT,                                       .
               SIZE=D05WORKS,                                     J40B .
               BASE=R11
         L     R9,AWINST                                         88036
         USING INST,R9                                           88036
         L     R6,AE05VOLS             LOAD CAMLIST ADDRESS        DASD
         USING D05VOLS,R6                                        88036
         L     R7,AJFCB                                          88036
         USING INFMJFCB,R7                                       88036
* THIS ROUTINE DOES THE CATALOGUING
*
* NOW:- WE FACE MANY PROBLEMS WHEN DOING A 'CATLG' (OR A KEEP)
* FIRST, WE SHALL CALL THE TALLOC ROUTINE WHICH READS THE ODD
*     JFCB,FIXES VOLUME LISTS ETC
         CLI   ICOMMAND,C'U'   UNCATALOG CALLING ME TO PRINT
*                              ERROR MESSAGES ?
*        BNE   D05DOCAT
         BE    D05PRNTE
         CLI   ICOMMAND,C'D'   DELETE CALLING ME TO PRINT ERR?
         BE    D05PRNTE
D05DOCAT DS    0H
         SPACE 3
         CALL  UJD48TAL      TEST IF THE DATA SET IS ALLOCATED TO US
         LTR   R15,R15
         BZ    D05TVOLS      YES, IT WAS ALLOCATED TO US
         BNE   D05KEEPE      NO,THERE WAS A REAL ERROR
         JOLRETN RC=4
D05KEEPE LA  R2,32
D05UNSUC DS    0H
         JOLERR 202,                                                   *
               'DSID ''',DDDSID,''' DSNAME ''',#DSNAME,''' KEPT ON VOL(*
               S) ',#VOL,' BUT NOT CATALOGED BECAUSE:-'           74365
         B     D05PRNTE
D05TVOLS DS    0H
         ST    R15,TASKRETN    SAVE FOR LASTC TESTS
         FIX ' CHECK THIS'
         CALL  UJD49VOL      GET DATA SET VOLUMES
* NOW TEST RE-CATLG FUNCTION
         IFNULL DDLABTYP,D05NALWA
* ALWAYS HERE
         MVI D05CAT,X'54'  TURN ON RE-CATLG BIT                   75311
D05NALWA DS    0H
D05FREE  DYNPARM DSN=(44,#DSN),ID=D05CAT,DDNAME=(8,#DDNAME),      ******
               VRB=UN,DISP=(CATLG),UNALLOC=YES
         LTR     R15,R15   DID WE RECATLG IT?                     75311
         BZ   D05SUCES YES                                        75311
* NOW RECATLG FAILED, SO ATTEMPT STRAIGHT CATLG THEN              75311
         MVI   D05CAT,X'64'                                       75311
* D05NALWA DS    0H
         $CTLG D05CAT                    DO THE CATALOG
         LTR R15,R15
         BNZ D05ERR
D05SUCES DS   0H
* NOW WE MUST CHECK AGAIN FOR GREATER THAN 5 VOLUMES BECAUSE       DASD
* ALL OF THE VOLUMES MIGHT NOT FIT ON 1 MESSAGE                    DASD
         LH    R14,D05#VOLS         LOAD VOL COUNT                 DASD
         XC    D05#VOLS,D05#VOLS    CLEAR VOL COUNT                DASD
         CH    R14,=H'5'            > 5 VOLUMES                    DASD
         BNH   D05SUCE2             NO   LETS DOIT THE OLD WAY     DASD
         SH    R14,=H'5'            SUBTRACT 5                     DASD
         STH   R14,D05#VOLS         STORE BACK FOR LATER           DASD
         MVI   DDVOLUME,C' '        MOVE IN JUST THE 1ST FIVE      DASD
         MVC   DDVOLUME+1(L'DDVOLUME-1),DDVOLUME                   DASD
         MVC   DDVOLUME(L'JFCBVOLS),JFCBVOLS                       DASD
         L     R14,=V(E00FXVOL)                                   J40B
         BALR  R14,R14                                            J40B
*        BAL   R14,E00FXVOL         MOVE TO #VOL,VOL EDITTED       DASD
         LA    R15,D05UNIT+60       POINT TO 6TH VOLSER IN CAMLST  DASD
         ST    R15,D05START         SAVE FOR LATER                 DASD
D05SUCE2 DS    0H                                                  DASD
         JOLERR 101,'DSID ''',DDDSID,''' DSNAME ''',                   *
               #DSNAME,''' CATALOGED ON VOL(S) ',#VOL             74365
         LH    R14,D05#VOLS         LOAD NUMBER OF VOLS LEFT       DASD
         LTR   R14,R14              ALL DONE                       DASD
         BZ    D05GOODR             YES SIR LETS GO                DASD
D05LMSGS DS    0H                                                  DASD
         L     R15,D05START         POINT TO NEXT AREA             DASD
         MVI   DDVOLUME,C' '        CLEAR CONSTRUCT AREA           DASD
         MVC   DDVOLUME+1(L'DDVOLUME-1),DDVOLUME                   DASD
         LA    R1,DDVOLUME          POINT TO CONSTRUCT AREA        DASD
         LA    R2,5                 LETS STOP AFTER 5              DASD
D05LMSG  MVC   0(6,R1),4(R15)       MOVE IN NEXT VOLUME SERIAL     DASD
         LA    R1,6(R1)             BUMP DDVOLUME BY 6             DASD
         LA    R15,12(R15)          BUMP CAMLST BY 12              DASD
         SH    R14,=H'1'            BACKOFF VOL COUNT BY 1         DASD
         BZ    D05LMSGD             IF ALL DONE GET OUT EARLY      DASD
         BCT    R2,D05LMSG          RETURN FOR THE REST            DASD
D05LMSGD DS    0H                                                  DASD
         STH    R14,D05#VOLS        SAVE # OF VOLS LEFT            DASD
         ST     R15,D05START        STORE NEXT AREA ADDRESS        DASD
         L     R14,=V(E00FXVOL)                                   J40B
         BALR  R14,R14                                            J40B
*        BAL    R14,E00FXVOL        MOVE TO #VOL,VOL EDITTED       DASD
         B      D05SUCE2            GO PRINT THE  MESSAGE          DASD
D05GOODR DS    0H                                                  DASD
         JOLRETN
D05ERR   LR    R2,R15
         B     D05UNSUC
         SPACE 3
D05PRNTE DS    0H
         B  D05RETNC(R2)
D05RETNC B     D05SUCES
         B     D05RET4
         B     D05RET8
         B     D05RET12
         B     D05RET16
         B     D05RET20
         B     D05RET24
         B     D05RET28
         B     D05RET32
         B     D05RET36
D05RET4  JOLERR 203,'CATALOG VOLUME NOT MOUNTED'
         B  D05RETN0
         FIX  'NOTE R0=0 IF C AND ALREADY CATLGD'           FIX-X 76200
         FIX  'NOTE R0=60 IF U/D AND YSAM'                  FIX-X 76200
D05RET8  JOLERR 204,'DATA SET NAME FOUND AT LOWER LEVEL'
         B     D05RETN0
D05RET12 DS    0H
         JOLERR 205,'CATLG RETURNED 12'
         B     D05RETN0
D05RET16 JOLERR 206,'NO INDEX LEVEL FOR DATA SET'
         B     D05RETN0
D05RET20 JOLERR 407,'CATALOG DATA SET IS FULL'
         B     D05RETN0
D05RET24 JOLERR 208,'INVALID GENERATION NAME'
         B     D05RETN0
D05RET28 JOLERR 409,'PERMANENT I/O ERROR ON CATALOG DATA SET'
         B     D05RETN0
D05RET32 JOLERR 210,'KEEP FAILED FOR DATA SET'
         B     D05RETN0
D05RET36 JOLERR 211,'NO VOLUMES WERE SUPPLIED'
         B     JRETN0
         SPACE 3
* WHILE HERE, COPY THE VOLUME SERIAL NUMBERS TO RESOLVE FUTURE VOLREFS
         SPACE 2
         CLEAR DDVOLUME
         CALL  UJD49VOL             GET THE DATA SET VOLUMES
         SPACE 2
D05CVC9  DS    0H
         L     R14,=V(E00FXVOL)                                   J40B
         BALR  R14,R14                                            J40B
*        BAL   R14,E00FXVOL         COPY VOLS TO #VOLS
         SPACE
         UJE22UPD DDDSNAME,DDVOLUME,DDDDNAME FIX VOLUME REFERENCES
         MVI   DDDISP+1,C'L' LAST USE                                  .
                                (A LIE OF COURSE, BUT HE WON'T KNOW)
*        MVC   DDDDNAME,FCTACSN    ENSURE HE USES THE CORRECT DDNAME
*        CALL  UJD14FRE            FREE THE DATASET
         B     JRETN0
D05NVCT  JOLERR 212,                                                   .
               'DSID ',DDDSID,' NOT CATALOGED, HAS NO VOLUMES  '
.NX8220  ANOP
D05RETN0 B     JRETN0
D05NOFCT CLC   DDVOLUME,=C'     '     DOES IT HAVE A VOLUME
         BNE   D05CVC9                YES
         JOLERR 213,                                                   .
               'DSID ',DDDSID,' NOT CATALOGED, IS NOT ALLOCATED'
         B     JRETN0
         LTORG                                                     DASD
         DC    20S(*)                                              DASD
         END
