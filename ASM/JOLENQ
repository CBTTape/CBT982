***  ENQUEUE OR ENQ JOL INSTRUCTION FOR ENQUING             ***********
*          ON DATA SETS OR ANY RESOURCE                               *
*                                                                     *
*    MODIFIED FROM US26ENQ BY MIKE STRAHAN, SHELL OIL, MELBOURNE.     *
*                                                                     *
*    FUNCTION:                                                        *
*                                                                     *
*         TO ISSUE AN ENQUEUE ON MAJOR AND MINOR NAMES AS             *
*         SPECIFIED BY THE USER.                                      *
*                                                                     *
*    LINKAGE:                                                         *
*                                                                     *
*         INVOKED BY $CALL JOLENQ,(MAJOR,MINOR,WAIT)                  *
*                                                                     *
*                                                                     *
*         ON ENTRY, REGISTERS CONTAIN STANDARD JOL REGISTER VALUES.   *
*                                                                     *
*         ON RETURN, %LASTCC IS SET TO :-                             *
*         REGISTER 15 CONTAINS A RETURN CODE :                        *
*         FOR ENTRY POINT US26ENQ                                     *
*                                                                     *
*         RC = 0   CONTROL OF RESOURCE HAS BEEN ASSIGNED.             *
*                                                                     *
*         RC = 4   APPLICABLE ONLY FOR BACKGROUND TASKS.              *
*                  CONTROL OF THE RESOURCE HAS BEEN ASSIGNED,         *
*                  BUT THE TASK WAITED FOR THE RESOURCE TO            *
*                  BECOME AVAILABLE.                                  *
*                  THE OPERATOR WAS INFORMED THAT THE TASK WAITED     *
*                  AND THAT PROCESSING SUBSEQUENTLY CONTINUED.        *
*                                                                     *
*         RC = 8   A PREVIOUS REQUEST FOR THE RESOURCE HAS BEEN       *
*                  MADE. THIS REQUEST IS IGNORED.                     *
*                                                                     *
*         RC = 12  APPLICABLE ONLY TO FOREGROUND TASKS.               *
*                  CONTROL OF THE RESOURCE WAS NOT ASSIGNED           *
*                  BECAUSE THE TASK WOULD HAVE WAITED FOR CONTROL     *
*                  OF THE RESOURCE.                                   *
*                  THE FOREGROUND USER WAS INFORMED THAT THE          *
*                  RESOURCE WAS NOT IMMEDIATELY AVAILABLE.            *
* NOTE:            IT IS UP TO THE MODULE INVOKING US26ENQ TO         *
* *****            DECIDE WHAT ACTION IS TO BE TAKEN.                 *
*                                                                     *
*                                                                     *
*                                                                     *
*    MODULE ATTRIBUTES:                                               *
*                                                                     *
*         RE-ENTERABLE.                                               *
*                                                                     *
***********************************************************************
JOLENQ   TITLE 'ENQUE ON NAMES SPECIFIED BY USER'
         GBLC  &JOLOS
         $UJEPARM
         PRINT OFF
         COPY  JOLCOM
         PRINT ON
         SPACE
P26WORK  DSECT
SAVE     DS    18F                 SAVE AREA
LTIMESUP EQU   X'20'               LENGTH OF STIMER EXIT ROUTINE
*                                  SHOULD = EXITLNTH
TIMESUP  DS    0F                  STIMER EXIT ROUTINE
         ORG   *+LTIMESUP
WAITBIT  DS    X'0'                NOT 0 IF WE ARE TO WAIT
TSOIND   DS    X'0'                NOT 0 IF WE ARE UNDER TSO
MINORNM  DS    CL60                MINOR NAME COPIED HERE
EXENQ    ENQ   (,,E,0,SYSTEM),RET=HAVE,MF=L
WTOID    DS    F                   USED TO ISSUE A 'DOM'
MSGAREA  DS    0F                  TPUT/WTO MESSAGE SPACE
         DS    F                   FOR SVC 35 RECORD LENGTH ETC
MSJOBNAM DS    CL8                 JOBNAME (FROM TIOT = TIOTNJOB)
MSWAITFR DC    C':WAITING FOR:-'
QNAME    DS    CL8                 MAJOR NAME COPIED HERE
RNAME2   DS    CL60                MINOR NAME COPIED HERE
         DS    2F                  FOR SVC 35 DESCRIPTION/RTE CDES
OPNFRMD  DS    CL1                 FLAG SHOWING THAT STIMER EXIT
*                                  WAS ENTERED
LNTHWRK  EQU   *-P26WORK           LENGTH OF WORK AREA
*R8       EQU   8                   STORES RETURN CODE DURING EXECUTION
*R10      EQU   10                  POINTS TO WORK AREA
*R11      EQU   11                  BASE REGISTER FOR BULK OF PROGRAM
*   EQUATES FOR FIELDS IN SYSTEM CONTROL BLOCKS
         SPACE 1
TCBTSFLG EQU   148                 OFFSET OF FLAGS INTO TCB
TIOCNJOB EQU   0                   OFFSET OF JOBNAME INTO TIOT
TIOEDDNM EQU   28                  OFFSET OF DDNAME IN TIOT(1ST ENTRY)
UCBNAME  EQU   13                  OFFSET FOR UCBNAME WITHIN UCB
         SPACE 3
JOLENQ   CSECT
         USING JOLENQ,R15
         L     R15,=V(UJP26EN2)
         BR    R15
         LTORG
         DROP R15
JOLDEQ   CSECT
         USING JOLDEQ,R15
         L     R15,=V(UJP26DE2)
         BR    R15
         LTORG
         DROP R15
         LTORG
         JOLSAVE CSECT=UJP26EN2,SIZE=LNTHWRK
         USING P26WORK,R13
         LR    R10,R13             SET TO POINT TO WORK AREA
         DROP  R13
         USING P26WORK,R10
         SR    R8,R8               CLEAR RETURN CODES
         $CALL UJP26REL
         LTR   R15,R15
         BZ    P26STRT
         JOLRETN RC=16
*    DO THE ENQUEUE ON REQUIRED RESOURCES
         SPACE 1
P26STRT  MVC   EXENQ(ENQLNTH),ENQ  SET UP LIST
RETRYENQ ENQ   (QNAME,MINORNM+1,,0),MF=(E,EXENQ)  TRY RET = USE
         LTR   R15,R15             ENQ'D OK?
         BNZ   P26ENBAD            NO, FIND OUT THE ERROR
ENQRET   JOLRETN                   ,RETURN A 0
P26ENBAD SR    R8,R8
         IC    R8,3(R15)           PICK UP ENQ RETURN CODE
         CH    R8,=H'8'            WHAT HAPPENED?
         BE    ENQRET              ALREADY HAD IT
         BL    ENQRET4             ENQ RETURNED A FOUR.
* HERE WE HAVE AN ERROR
         JOLRETN RC=16
         SPACE 1
*   MUST BE RETURN CODE = 4
*   WE WOULD HAVE HAD TO WAIT FOR OUR ENQUEUE TO BE PROCESSED
         SPACE 1
ENQRET4  CLI   WAITBIT,X'0'        DO WE WANT TO WAIT?
         BNE   P26WAIT
         JOLRETN RC=4
RETURN   JOLRETN RC=(8)
         SPACE  3
P26WAIT  CLI   TSOIND,X'0'         ARE WE RUNNING UNDER TSO?
         BE    P26BATCH            NO, SO DO BATCH THINGS
         MVC   #WORK(L'MINORNM+2),MINORNM
         JOLERR 103,'WAITING FOR ''',QNAME,'/',#WORK,''''
         TPUT   TSOWAIT,L'TSOWAIT+1,ASIS
*        TPUT   TSOBELL,1,ASIS
         LA    R1,TKN
         TGET  (R1),10
         OI    TKN,C' '
         CLI   TKN,C'W'
         BE    P26BATCH
         CLI   TKN,C'N'
         BE    SETRC4
         B     RETRYENQ
TSOWAIT  DC    C'REPLY ''W'' FOR WAIT, ''N'' FOR NOWAIT, OR ENTER FOR R*
               ETRY '
TSOBELL  DC    X'2F'
         SPACE 1
*   FOR BACKGROUND TASKS WE WILL ENQUEUE WITH RET=HAVE BUT WITH THE
*   INTERVAL TIMER RUNNING. IF WE WAIT FOR MORE THAN 2 MINUTES THE
*   OPERATOR WILL BE INFORMED THAT WE ARE WAITING. WHEN WE KICK OFF
*   THE OPERATOR WILL BE INFORMED THAT WE HAVE RESUMED PROCESSING.
         SPACE 1
P26BATCH MVC   TIMESUP(EXITLNTH),TIMERXIT  SET UP TIMER EXIT CODE IN
*                                          WORKING STORAGE
         MVI   OPNFRMD,X'80'       CLEAR WTO1 ISSUED FLAG
         WTO   MF=(E,MSGAREA)      TELL THE OPERATOR WE ARE WAITING
         ST    R1,WTOID            STORE MESSAGE ID FOR DOM
         B     NOTIMER             DON'T WANT A TIMER (TEST)
         STIMER REAL,TIMESUP,BINTVL=TWOMINS  START ELAPSED TIMER TO
*              RUN FOR 2 MINUTES SO THAT WE CAN SEE HOW LONG WE HAVE
*              BEEN WAITING FOR OUR ENQUEUE TO BE PROCESSED.
NOTIMER  ENQ   ,RET=HAVE,MF=(E,EXENQ)  TRY ENQ WITH RET=HAVE
         LR    R6,R15              SAVE ENQ RETURN CODE, IF ANY
         TTIMER CANCEL             REMOVE STIMER IF STILL ACTIVE
         TM    OPNFRMD,X'80'       WAS WTO1 ISSUED?
         BZ    NOTMADE             NO SET RETURN CDE AND RETURN TO USER
         SPACE 1
         AIF   ('&JOLOS' EQ 'X8').X8010
*   TELL OPERATOR THAT WE ARE RUNNING AGAIN
         L     R1,WTOID
         DOM   MSG=(1)               DELETE OPERATOR MESSAGE
.X8010   SPACE 1
         MVC   CALLAREA(8),MSJOBNAM  SAVE JOBNAME TEMPORARILY
         MVC   MSGAREA(LNTHWTO2),WTO2
         MVC   MSJOBNAM,CALLAREA
         WTO   MF=(E,MSGAREA)
         SPACE 1
*   ALL MESSAGES HAVE BEEN OUTPUT TO THE OPERATOR
*   INVESTIGATE RETURN CODE FROM ENQ
         SPACE 1
NOTMADE  EQU   *
         LTR   R6,R6               ANY RETURN CODE FROM ENQ?
         BZ    SETRC4              NO, SET RETCODE =4 'COS WE WAITED
         SR    R8,R8
         IC    R8,3(R6)            WHAT HAPPENED?
         CH    R8,=H'8'
         BNE   SETRC16             DON'T KNOW WHAT HAPPENED
         JOLRETN    ,              ALREADY HAD IT(?!) - RETURN 0
SETRC4   DS    0H                  SET RETURN CODE = 4
         JOLRETN ,                 SET RETURN CODE = 0
         SPACE 3
*   RETURN CODE FROM FIRST ENQ WAS = 4 AND WE ARE RUNNING UNDER TSO
         SPACE 1
SETRC12  JOLRETN RC=12             SET RETURN CODE = 12
SETRC16  JOLRETN RC=16             SET RETURN CODE = 16
         SPACE 3
*    STIMER EXIT ROUTINE - HANDLES LONG ENQUEUE WAITS
         SPACE 1
         USING TIMESUP,R15
TIMERXIT SAVE  (14,12)
         MVI   OPNFRMD,X'80'       SET STIMER EXIT ENTERED FLAG
         LR    R8,R15
         DROP  R15
         USING TIMESUP,R8
         WTO   MF=(E,MSGAREA)      TELL THE OPERATOR WE ARE WAITING
         ST    R1,WTOID            STORE MESSAGE ID FOR DOM
         RETURN (14,12)
EXITLNTH EQU   *-TIMERXIT
         DROP  R8
         JOLRETN
*    CONSTANTS
         SPACE 1
ENQ      ENQ   (,,E,0,SYSTEM),RET=USE,MF=L
         ORG   *-4
ENQLNTH  EQU   *-ENQ
TWOMINS  DC    F'12000'            TIMER INTERVAL OF 2 MINUTES
WTO2     WTO   'XXXXXXXX:CONTINUES',                                   *
               ROUTCDE=(2),DESC=(7),MF=L
LNTHWTO2 EQU   *-WTO2
         SPACE 1
         LTORG
         SPACE 3
         JOLSAVE CSECT=UJP26DE2,SIZE=LNTHWRK
         USING P26WORK,R13
         LR    R10,R13             SET TO POINT TO WORK AREA
         DROP  R13
         USING P26WORK,R10
         $CALL UJP26REL
         MVC   EXENQ(DEQLNTH),DEQ  SET UP DEQ PARAMETERS
         DEQ   (QNAME,MINORNM+1,0),MF=(E,EXENQ)  TRY RET = USE
         LTR   R15,R15
         BZ    DEQRET0
         JOLRETN RC=4
DEQRET0  JOLRETN
DEQ      DEQ   (,,0,SYSTEM),RET=HAVE,MF=L
         ORG   *-4
DEQLNTH  EQU   *-DEQ               LENGTH OF DEQ LIST
         LTORG
         SPACE 3
         JOLSAVE CSECT=UJP26REL,BASE=12
         SPACE 2
* ON ENTRY, R1 POINTS TO:-
*        DC    A(QNAME)
*        DC    A(MINORNAME)
*        DC    A(WAITBIT)
         LM    R5,R7,0(R1)         GET THE PARAMETERS
         MVC   MSGAREA(LNTHWTO1),WTO1  SET UP WTO TEXT AND CODE
*                                      IN MESSAGE WORK LOCATION
         MVC   QNAME,0(R5)         SHIFT QNAME
         MVC   MINORNM,0(R6)       AND MINOR-NAME
         MVC   WAITBIT,0(R7)       AND WAITBIT
*
* GET RNAME FOR ANY POSSIBLE ERROR MESSAGES
         MVC   RNAME2,BLANKS       CLEAR AREA FIRST
         MVC   RNAME2,MINORNM+2    DROP VARYING STRING LENGTH
         MVI   TSOIND,X'0'         CLEAR TSO INDICATOR
* TEST IF TSO
         CLI   IFTSO,C'T'                                         88020
         BNE   NOTSO                                              88020
         IFOS  =X8,P26X8                                          88020
         L     R15,16
         L     R15,0(R15)
         L     R15,4(R15)          GET TCB ADDRESS VIA THE CVT
         L     R15,12(R15)         GET TIOT ADDRESS FROM TCB
         MVC   MSJOBNAM,TIOCNJOB(R15) STORE JOB/TASK NAME
         B     P26NX8
P26X8    DS    0H                                                 88020
*    IS THIS A TSO TASK?
         L     R14,X'2EC'     THIS IS FLCTOLD ON X8 SYSTEMS       88020
         L     R14,12(,R14)   THIS POINTS TO THE JSCB             88020
         L     R15,0(,R14)    AND NOW THE CSCB                    88020
         MVC   MSJOBNAM,X'058'(R15) THE JOB NAME                  88020
*        MVC   MSJOBNAM,X'090'(R15)                               88020
P26NX8   DS    0H                                                 88020
         MVI   TSOIND,X'1'         SET TSO TASK FLAG ON
         CLI   WAITBIT,X'0'
         BE    NOTSO               IS TSO, BUT NO MESSAGE
         B     NOTSO               **********************
         SPACE 2
*    FIND THE RESOURCES WE WANT TO ENQ ON
         SPACE 1
         JOLERR 307,'WAIT NULLIFIED UNDER TSO/TSS'
         MVI   WAITBIT,X'0'
NOTSO    EQU   *
WAITRET  JOLRETN
         LTORG
WTO1     WTO   'XXXXXXXX:WAITING FOR:-                                 *
                                   ',ROUTCDE=(2),DESC=(7),MF=L
LNTHWTO1 EQU   *-WTO1
         SPACE 3
         LTORG
         END
